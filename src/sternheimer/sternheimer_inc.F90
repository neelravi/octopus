!! Copyright (C) 2005-2006 M. Marques, X. Andrade
!!
!! This program is free software; you can redistribute it and/or modify
!! it under the terms of the GNU General Public License as published by
!! the Free Software Foundation; either version 2, or (at your option)
!! any later version.
!!
!! This program is distributed in the hope that it will be useful,
!! but WITHOUT ANY WARRANTY; without even the implied warranty of
!! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!! GNU General Public License for more details.
!!
!! You should have received a copy of the GNU General Public License
!! along with this program; if not, write to the Free Software
!! Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
!! 02111-1307, USA.
!!
!! $Id: em_resp_inc.F90 2663 2007-01-25 09:04:29Z lorenzen $


! -------------------------------------------------------------
! This routine calculates the first-order variations of the wavefunctions 
! for an applied perturbation.
!--------------------------------------------------------------

subroutine X(sternheimer_solve)(                           &
     this, sys, hm, lr, nsigma, omega, perturbation,        &
     restart_dir, rho_tag, wfs_tag, have_restart_rho)
  type(sternheimer_t),    intent(inout) :: this
  type(system_t), target, intent(inout) :: sys
  type(hamiltonian_t),    intent(inout) :: hm
  type(lr_t),             intent(inout) :: lr(:) 
  integer,                intent(in)    :: nsigma 
  R_TYPE,                 intent(in)    :: omega
  type(pert_t),           intent(in)    :: perturbation
  character(len=*),       intent(in)    :: restart_dir
  character(len=*),       intent(in)    :: rho_tag
  character(len=*),       intent(in)    :: wfs_tag
  logical,      optional, intent(in)    :: have_restart_rho

  FLOAT :: dpsimod, tol
  integer :: iter, sigma, ik, ist, is, err
  R_TYPE, allocatable :: dl_rhoin(:, :, :), dl_rhonew(:, :, :), dl_rhotmp(:, :, :)
  R_TYPE, allocatable :: Y(:, :, :), hvar(:, :, :)
  R_TYPE, allocatable :: tmp(:)
  real(8):: abs_dens
  R_TYPE :: omega_sigma, proj
  logical, allocatable :: orth_mask(:)

  logical :: conv_last, conv, states_conv

  type(mesh_t), pointer :: mesh
  type(states_t), pointer :: st

  integer :: total_iter, idim, ip

  character(len=100) :: dirname

  call push_sub('sternheimer_inc.Xsternheimer_solve')
  call profiling_in(prof, "STERNHEIMER")

  ASSERT(nsigma == 1 .or. nsigma == 2)

  mesh => sys%gr%mesh
  st => sys%st
  
  call mix_init(this%mixer, sys%gr%mesh%np, sys%st%d%nspin, 1, func_type=sys%st%wfs_type)
  call mesh_init_mesh_aux(sys%gr%mesh)
  
  SAFE_ALLOCATE(      tmp(1:mesh%np))
  SAFE_ALLOCATE(        Y(1:mesh%np, 1:1, 1:nsigma))
  SAFE_ALLOCATE(     hvar(1:mesh%np, 1:st%d%nspin, 1:nsigma))
  SAFE_ALLOCATE( dl_rhoin(1:mesh%np, 1:st%d%nspin, 1:1))
  SAFE_ALLOCATE(dl_rhonew(1:mesh%np, 1:st%d%nspin, 1:1))
  SAFE_ALLOCATE(dl_rhotmp(1:mesh%np, 1:st%d%nspin, 1:1))
  SAFE_ALLOCATE(orth_mask(1:st%nst))

  conv = .false.
  conv_last = .false.

  if (present(have_restart_rho)) then 
    if (.not. have_restart_rho) call X(lr_build_dl_rho)(mesh, st, lr, nsigma)
  else
    call X(lr_build_dl_rho)(mesh, st, lr, nsigma)
  end if
  
  message(1)="--------------------------------------------"
  call write_info(1)

  total_iter = 0

  ! preorthogonalization
  if (this%preorthogonalization) then 
    do sigma = 1, nsigma
      call X(lr_orth_response)(mesh, st, lr(sigma))
    enddo
  endif
   
  tol = scf_tol_step(this%scf_tol, 0, M_ONE)

  !self-consistency iteration for response
  iter_loop: do iter = 1, this%scf_tol%max_iter
    if (this%add_fxc .or. this%add_hartree) then
       write(message(1), '(a, i3)') "LR SCF Iteration: ", iter
       call write_info(1)
    endif
    ! otherwise it is not actually SCF, and there can only be one pass through

    write(message(1), '(a, f20.6, a, f20.6, a, i1)') &
         "Frequency: ", units_from_atomic(units_out%energy,  R_REAL(omega)), &
         " Eta : ",     units_from_atomic(units_out%energy, R_AIMAG(omega))
    write(message(2), '(a)') &
         '   ik  ist                norm   iters            residual'
    call write_info(2)

    dl_rhoin(1:mesh%np, 1:st%d%nspin, 1) = lr(1)%X(dl_rho)(1:mesh%np, 1:st%d%nspin)
    call X(sternheimer_calc_hvar)(this, sys, hm, lr, nsigma, hvar)

    do ik = 1, st%d%nik
      !now calculate response for each state
      is = states_dim_get_spin_index(sys%st%d, ik)

      states_conv = .true.

      do ist = 1, st%nst
        do sigma = 1, nsigma
          !calculate the RHS of the Sternheimer eq
          if(sternheimer_have_rhs(this)) then
            ASSERT(associated(this%X(rhs)))
            forall(idim = 1:st%d%dim, ip = 1:mesh%np) y(ip, idim, sigma) = this%X(rhs)(ip, idim, ist, ik)
          else
            Y(1:mesh%np, 1, sigma) = R_TOTYPE(M_ZERO)
            call X(pert_apply)(perturbation, sys%gr, sys%geo, hm, ik, st%X(psi)(:, :, ist, ik), Y(:, :, sigma))
          end if
          Y(1:mesh%np, 1, sigma) = -Y(1:mesh%np, 1, sigma) - hvar(1:mesh%np, is, sigma)*st%X(psi)(1:mesh%np, 1, ist, ik)

          ! Let Pc = projector onto unoccupied states, Pn` = projector that removes state n
          ! For an SCF run, we will apply Pn` for the last step always, since the whole wavefunction is useful for some
          ! things and the extra cost here is small. If occ_response, previous steps will also use Pn`. If !occ_response,
          ! previous steps will use Pc, which generally reduces the number of linear-solver iterations needed. Only the
          ! wavefunctions in the unoccupied subspace are needed to construct the first-order density.
          ! For a non-SCF run, we will apply Pn` if occ_response, and Pc if !occ_response.
          ! I am not sure what the generalization of this scheme is for metals, so we will just use Pc if there is smearing.

          if ((this%occ_response .or. conv_last) .and. st%smear%method == SMEAR_SEMICONDUCTOR) then
            ! project out only the component of the unperturbed wavefunction
            proj = X(mf_dotp)(mesh, st%d%dim, st%X(psi)(:, :, ist, ik), y(:, :, sigma))
            do idim = 1, st%d%dim
              call lalg_axpy(mesh%np, -proj, st%X(psi)(:, idim, ist, ik), y(:, idim, sigma))
            end do
          else
            ! project RHS onto the unoccupied states
            call X(lr_orth_vector)(mesh, st, Y(1:mesh%np, 1:1, sigma), ist, ik)
          endif
        
          if(sigma == 1) then 
            omega_sigma = omega
          else 
            omega_sigma = -R_CONJ(omega)
          end if

          !solve the Sternheimer equation
          call X(solve_HXeY) (this%solver, hm, sys%gr, sys%st, ist, ik, &
               lr(sigma)%X(dl_psi)(1:mesh%np_part, 1:st%d%dim, ist, ik), &
             Y(1:mesh%np, 1:1, sigma), -sys%st%eigenval(ist, ik) + omega_sigma, tol, this%occ_response)

          if (this%preorthogonalization) then 
            !re-orthogonalize the resulting vector
            if (this%occ_response) then
              proj = X(mf_dotp)(mesh, st%d%dim, st%X(psi)(:, :, ist, ik), lr(sigma)%X(dl_psi)(:, :, ist, ik))
              do idim = 1, st%d%dim
                call lalg_axpy(mesh%np, -proj, st%X(psi)(:, idim, ist, ik), lr(sigma)%X(dl_psi)(:, idim, ist, ik))
              end do
            else
              call X(lr_orth_vector)(mesh, st, lr(sigma)%X(dl_psi)(1:mesh%np_part, 1:st%d%dim, ist, ik), ist, ik)
            endif
          end if

          ! print the norm of the variations, and the number of
          ! iterations and residual of the linear solver
          dpsimod = X(mf_nrm2)(mesh, st%d%dim, lr(sigma)%X(dl_psi)(1:mesh%np, 1:st%d%dim, ist, ik))

          write(message(1), '(i5, i5, f20.6, i8, e20.6)') &
            ik, (3 - 2 * sigma) * ist, dpsimod, this%solver%iter, this%solver%abs_psi 
          call write_info(1)

          states_conv = states_conv .and. (this%solver%abs_psi < tol)
          total_iter = total_iter + this%solver%iter
          
        end do !sigma
      end do !ist
    end do !ik

    call X(lr_build_dl_rho)(mesh, st, lr, nsigma)

    dl_rhonew(1:mesh%np, 1:st%d%nspin, 1) = M_ZERO

    !write restart info
    call X(restart_write_lr_rho)(lr(1), sys%gr, st%d%nspin, restart_dir, rho_tag)

    do sigma = 1, nsigma 
      write(dirname,'(a,a,i1)') trim(restart_dir)//trim(wfs_tag),'_', sigma
      call restart_write(trim(tmpdir)//dirname, st, sys%gr, err, iter=iter, lr=lr(sigma))
    end do
    
    if (.not.(this%add_fxc .or. this%add_hartree)) then
    ! no need to deal with mixing, SCF iterations, etc.
    ! dealing with restart density above not necessary, but easier to just leave it
    ! convergence criterion is now about individual states, rather than SCF residual
      this%ok = states_conv

      if (.not. this%ok) then
         message(1) = "Linear solver failed to converge all states."
         call write_warning(1)
      endif

      message(1)="--------------------------------------------"
      write(message(2), '(a, i8)') &
           'Info: Total Hamiltonian applications:', total_iter * linear_solver_ops_per_iter(this%solver)
      call write_info(2)
      exit
    endif

    ! all the rest is the mixing and checking for convergence

    if(this%scf_tol%max_iter == iter) then 
      message(1) = "Self-consistent iteration for response did not converge."
      this%ok = .false.
      call write_warning(1)
    end if

    dl_rhotmp(1:mesh%np, 1:st%d%nspin, 1) = lr(1)%X(dl_rho)(1:mesh%np, 1:st%d%nspin)

    call X(mixing)(this%mixer, iter, dl_rhoin, dl_rhotmp, dl_rhonew, X(mf_dotp_aux))

    abs_dens = M_ZERO

    do ik = 1, st%d%nspin
      tmp(1:mesh%np) = dl_rhoin(1:mesh%np, ik, 1) - dl_rhotmp(1:mesh%np, ik, 1)
      abs_dens = hypot(abs_dens, real(X(mf_nrm2)(mesh, tmp), 8))
    end do

    write(message(1), '(a, e20.6)') "SCF Residual ", abs_dens

    message(2)="--------------------------------------------"
    call write_info(2)
      
    if( abs_dens <= this%scf_tol%conv_abs_dens ) then 
      if(conv_last) then 
        conv = .true.
      else
        conv_last = .true.
      end if
    end if

    if(conv) then
      this%ok = .true.

      write(message(1), '(a, i4, a)') &
           'Info: SCF for response converged in ', iter, ' iterations.'
      write(message(2), '(a, i8)') &
           '      Total Hamiltonian applications:', total_iter*linear_solver_ops_per_iter(this%solver)
      call write_info(2)
      exit
    else
      lr(1)%X(dl_rho)(1:mesh%np, 1:st%d%nspin) = dl_rhonew(1:mesh%np, 1:st%d%nspin, 1)
      if(nsigma == 2) lr(2)%X(dl_rho)(1:mesh%np, 1:st%d%nspin) = R_CONJ(dl_rhonew(1:mesh%np, 1:st%d%nspin, 1))

      tol = scf_tol_step(this%scf_tol, iter, TOFLOAT(abs_dens))
    end if
    
  end do iter_loop

  call mix_end(this%mixer)

  SAFE_DEALLOCATE_A(tmp)
  SAFE_DEALLOCATE_A(Y)
  SAFE_DEALLOCATE_A(hvar)
  SAFE_DEALLOCATE_A(dl_rhoin)
  SAFE_DEALLOCATE_A(dl_rhonew)
  SAFE_DEALLOCATE_A(dl_rhotmp)
  SAFE_DEALLOCATE_A(orth_mask)

  call profiling_out(prof)
  call pop_sub()

end subroutine X(sternheimer_solve)

subroutine X(sternheimer_calc_hvar)(this, sys, hm, lr, nsigma, hvar)
  type(sternheimer_t),    intent(inout) :: this
  type(system_t),         intent(inout) :: sys
  type(hamiltonian_t),    intent(inout) :: hm
  type(lr_t),             intent(inout) :: lr(:) 
  integer,                intent(in)    :: nsigma 
  R_TYPE,                 intent(out)   :: hvar(:,:,:)

  R_TYPE, allocatable :: tmp(:), hartree(:)
  integer :: np, i, ik, ik2

  call push_sub('sternheimer_inc.Xsternheimer_calc_hvar')
  call profiling_in(prof_hvar, "STERNHEIMER_HVAR")

  np = sys%gr%mesh%np

  if (this%add_hartree) then 
    SAFE_ALLOCATE(    tmp(1:np))
    SAFE_ALLOCATE(hartree(1:np))
    do i = 1, np
      tmp(i) = sum(lr(1)%X(dl_rho)(i, 1:sys%st%d%nspin))
    end do
    hartree(1:np) = R_TOTYPE(M_ZERO)
    call X(poisson_solve)(psolver, hartree, tmp, all_nodes = .false.)

    SAFE_DEALLOCATE_A(tmp)
  end if

  do ik = 1, sys%st%d%nspin
    
    !* initialize
    hvar(1:np, ik, 1) = M_ZERO

    !* hartree
    if (this%add_hartree) hvar(1:np, ik, 1) = hvar(1:np, ik, 1) + hartree(1:np)
    
    !* fxc
    if(this%add_fxc) then
      if(.not. this%oep_kernel) then 
        do ik2 = 1, sys%st%d%nspin
          hvar(1:np, ik, 1) = hvar(1:np, ik, 1) + this%fxc(1:np, ik, ik2)*lr(1)%X(dl_rho)(1:np, ik2)
        end do
      else
        call X(xc_oep_kernel_calc)(sys, hm, lr, nsigma, hartree(:))
        hvar(1:np, ik, 1) = hvar(1:np, ik, 1) + hartree(1:np)
      end if
    end if
  end do
  
  if (nsigma == 2) hvar(1:np, 1:sys%st%d%nspin, 2) = R_CONJ(hvar(1:np, 1:sys%st%d%nspin, 1))

  if (this%add_hartree) then
    SAFE_DEALLOCATE_A(hartree)
  end if

  call profiling_out(prof_hvar)
  call pop_sub()
end subroutine X(sternheimer_calc_hvar)

!-----------------------------------------------------------
subroutine X(sternheimer_set_rhs)(this, rhs)
  type(sternheimer_t), intent(inout) :: this
  R_TYPE, target,      intent(in)    :: rhs(:, :, :, :)

  this%X(rhs) => rhs
end subroutine X(sternheimer_set_rhs)

!! Local Variables:
!! mode: f90
!! coding: utf-8
!! End:
