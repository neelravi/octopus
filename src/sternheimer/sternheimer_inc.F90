!! Copyright (C) 2005-2006 M. Marques, X. Andrade
!!
!! This program is free software; you can redistribute it and/or modify
!! it under the terms of the GNU General Public License as published by
!! the Free Software Foundation; either version 2, or (at your option)
!! any later version.
!!
!! This program is distributed in the hope that it will be useful,
!! but WITHOUT ANY WARRANTY; without even the implied warranty of
!! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!! GNU General Public License for more details.
!!
!! You should have received a copy of the GNU General Public License
!! along with this program; if not, write to the Free Software
!! Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
!! 02111-1307, USA.
!!
!! $Id: em_resp_inc.F90 2663 2007-01-25 09:04:29Z lorenzen $


! -------------------------------------------------------------
! This is the central routine of the electromagnetic response.
! It calculates the first-order variations of the wavefunctions 
! for an applied perturbation.
!--------------------------------------------------------------

subroutine X(sternheimer_solve)(                           &
     this, sys, h, lr, nsigma, omega, perturbation,        &
     restart_dir, rho_tag, wfs_tag, have_restart_rho)
  type(sternheimer_t),    intent(inout) :: this
  type(system_t), target, intent(inout) :: sys
  type(hamiltonian_t),    intent(inout) :: h
  type(lr_t),             intent(inout) :: lr(:) 
  integer,                intent(in)    :: nsigma 
  R_TYPE,                 intent(in)    :: omega
  type(pert_t),           intent(in)    :: perturbation
  character(len=*),       intent(in)    :: restart_dir
  character(len=*),       intent(in)    :: rho_tag
  character(len=*),       intent(in)    :: wfs_tag
  logical,      optional, intent(in)    :: have_restart_rho

  FLOAT :: dpsimod
  integer :: iter, sigma, ik, ist, is, err
  R_TYPE, allocatable :: dl_rhoin(:, :, :), dl_rhonew(:, :, :), dl_rhotmp(:, :, :)
  R_TYPE, allocatable :: Y(:, :, :), hvar(:, :, :)
  R_TYPE, allocatable :: tmp(:)
  real(8):: abs_dens
  R_TYPE :: omega_sigma
  logical, allocatable :: orth_mask(:)

  logical :: conv_last, conv, states_conv

  type(mesh_t), pointer :: m
  type(states_t), pointer :: st

  integer total_iter

  character(len=100) :: dirname

  call push_sub('sternheimer.sternheimer_solve')

  ASSERT(nsigma == 1 .or. nsigma == 2)

  m => sys%gr%m
  st => sys%st
  
  call mix_init(this%mixer, sys%gr%m%np, sys%st%d%nspin, 1, func_type=sys%st%wfs_type)
  call mesh_init_mesh_aux(sys%gr%m)
  
  ALLOCATE(tmp(m%np),m%np)
  ALLOCATE(Y(m%np, 1, nsigma), m%np * 1 * nsigma)
  ALLOCATE(hvar(m%np, st%d%nspin, nsigma), m%np * st%d%nspin * nsigma)
  ALLOCATE(dl_rhoin(m%np, st%d%nspin, 1), 1 * m%np * st%d%nspin)
  ALLOCATE(dl_rhonew(m%np, st%d%nspin, 1), 1 * m%np * st%d%nspin)
  ALLOCATE(dl_rhotmp(m%np, st%d%nspin, 1), 1 * m%np * st%d%nspin)
  ALLOCATE(orth_mask(st%nst), st%nst)

  conv = .false.
  conv_last = .false.

  if (present(have_restart_rho)) then 
    if (.not. have_restart_rho) call X(lr_build_dl_rho)(m, st, lr, nsigma)
  else
    call X(lr_build_dl_rho)(m, st, lr, nsigma)
  end if
  
  this%solver%tol = scf_tol_start(this%scftol, this%solver%initial_tol, this%solver%final_tol)

  message(1)="--------------------------------------------"
  call write_info(1)

  total_iter = 0
    
  !self-consistency iteration for response
  iter_loop: do iter = 1, this%scftol%max_iter

    if (this%add_fxc .or. this%add_hartree) then
       write(message(1), '(a, i3)') "LR SCF Iteration: ", iter
       call write_info(1)
    endif
    ! otherwise it is not actually SCF, and there can only be one pass through

    write(message(1), '(a, f20.6, a, f20.6, a, i1)') &
         "Frequency: ", R_REAL(omega), " Eta : ", R_AIMAG(omega)
    write(message(2), '(a)') &
         '   ik  ist                norm   iters            residual'
    call write_info(2)

    dl_rhoin(1:m%np, 1:st%d%nspin, 1) = lr(1)%X(dl_rho)(1:m%np, 1:st%d%nspin)
    call X(sternheimer_calc_hvar)(this, sys, h, lr, nsigma, hvar)

    do ik = 1, st%d%nik
      !now calculate response for each state
      is = states_dim_get_spin_index(sys%st%d, ik)

      states_conv = .true.

      do ist = 1, st%nst
        do sigma = 1, nsigma
          !calculate the RHS of the Sternheimer eq
          Y(1:m%np, 1, sigma) = R_TOTYPE(M_ZERO)
          call X(pert_apply)(perturbation, sys%gr, sys%geo, h, ik, st%X(psi)(1:m%np_part, 1, ist, ik), Y(1:m%np, 1, sigma))
          Y(1:m%np, 1, sigma) = -Y(1:m%np, 1, sigma) - hvar(1:m%np, is, sigma) * st%X(psi)(1:m%np, 1, ist, ik)

          if (this%occ_response) then
          ! project out only the component of the unperturbed wavefunction
          ! |Y> := (1 - |ist><ist|)|Y>
!           alternative direct method below
!             Y(1:m%np, 1, sigma) = Y(1:m%np, 1, sigma) - st%X(psi)(1:m%np, 1, ist, ik) * &
!               X(mf_integrate)(sys%gr%m, st%X(psi)(1:m%np, 1, ist, ik) * Y(1:m%np, 1, sigma))

            orth_mask(1:st%nst) = .true.
            orth_mask(ist) = .false.
            call X(states_gram_schmidt)(m, st%nst, st%d%dim, st%X(psi)(1:m%np, 1:1, 1:st%nst, ik), &
              Y(1:m%np, 1:1, sigma), mask = orth_mask(1:st%nst))
          else
          ! project RHS onto the unoccupied states
            call X(lr_orth_vector)(m, st, Y(1:m%np, 1:1, sigma), ist, ik)
          endif
        
          if(sigma == 1) then 
            omega_sigma = omega
          else 
            omega_sigma = -R_CONJ(omega)
          end if

          !solve the Sternheimer equation
          call X(solve_HXeY) (this%solver, h, sys%gr, sys%st, ist, ik, &
             lr(sigma)%X(dl_psi)(1:m%np_part, 1:st%d%dim, ist, ik), &
             Y(1:m%np, 1:1, sigma), -sys%st%eigenval(ist, ik) + omega_sigma)
          
          ! print the norm of the variations, and the number of
          ! iterations and residual of the linear solver
          dpsimod = X(mf_nrm2)(m, st%d%dim, lr(sigma)%X(dl_psi)(1:m%np, 1:st%d%dim, ist, ik))

          write(message(1), '(i5, i5, f20.6, i8, e20.6)') &
            ik, (3 - 2 * sigma) * ist, dpsimod, this%solver%iter, this%solver%abs_psi 
          call write_info(1)

          states_conv = states_conv .and. (this%solver%abs_psi < this%solver%tol)
          total_iter = total_iter + this%solver%iter
          
        end do !sigma
      end do !ist
    end do !ik

    call X(lr_build_dl_rho)(m, st, lr, nsigma)

    dl_rhonew(1:m%np, 1:st%d%nspin, 1) = M_ZERO

    !write restart info
    call X(restart_write_lr_rho)(lr(1), sys%gr, st%d%nspin, restart_dir, rho_tag)

    do sigma = 1, nsigma 
      write(dirname,'(a,a,i1)') trim(restart_dir)//trim(wfs_tag),'_', sigma
      call restart_write(trim(tmpdir)//dirname, st, sys%gr, err, iter=iter, lr=lr(sigma))
    end do
    
    if (.not.(this%add_fxc .or. this%add_hartree)) then
    ! no need to deal with mixing, SCF iterations, etc.
    ! dealing with restart density above not necessary, but easier to just leave it
    ! convergence criterion is now about individual states, rather than SCF residual
      this%ok = states_conv

      if (.not. this%ok) then
         message(1) = "Linear solver failed to converge all states"
         call write_warning(1)
      endif

      message(1)="--------------------------------------------"
      write(message(2), '(a, i8)') &
           'Info: Total Hamiltonian applications:', total_iter * linear_solver_ops_per_iter(this%solver)
      call write_info(2)
      exit
    endif

    ! all the rest is the mixing and checking for convergence

    if(this%scftol%max_iter == iter) then 
      message(1) = "Self-consistent iteration for response did not converge"
      this%ok = .false.
      call write_warning(1)
    end if

    dl_rhotmp(1:m%np, 1:st%d%nspin, 1) = lr(1)%X(dl_rho)(1:m%np, 1:st%d%nspin)

    call X(mixing)(this%mixer, iter, dl_rhoin, dl_rhotmp, dl_rhonew, X(mf_dotp_aux))

    abs_dens = M_ZERO

    do ik = 1, st%d%nspin
      tmp(1:m%np) = dl_rhoin(1:m%np, ik, 1) - dl_rhotmp(1:m%np, ik, 1)
      abs_dens = hypot(abs_dens, real(X(mf_nrm2)(m, tmp), 8))
    end do

    write(message(1), '(a, e20.6)') "SCF Residual ", abs_dens

    message(2)="--------------------------------------------"
    call write_info(2)
      
    if( abs_dens <= this%scftol%conv_abs_dens ) then 
      if(conv_last) then 
        conv = .true.
      else
        conv_last = .true.
      end if
    end if

    if(conv) then
      this%ok = .true.

      write(message(1), '(a, i4, a)') &
           'Info: SCF for response converged in ', iter, ' iterations.'
      write(message(2), '(a, i8)') &
           '      Total hamiltonian applications:', total_iter * linear_solver_ops_per_iter(this%solver)
      call write_info(2)
      exit

    else
      
      lr(1)%X(dl_rho)(1:m%np, 1:st%d%nspin) = dl_rhonew(1:m%np, 1:st%d%nspin, 1)
      if(nsigma == 2) lr(2)%X(dl_rho)(1:m%np, 1:st%d%nspin) = R_CONJ(dl_rhonew(1:m%np, 1:st%d%nspin, 1))

      this%solver%tol = scf_tol_step(this%scftol, iter, TOFLOAT(abs_dens))

    end if
    
  end do iter_loop

  call scf_tol_end(this%scftol)

  call mix_end(this%mixer)

  deallocate(tmp)
  deallocate(Y)
  deallocate(hvar)
  deallocate(dl_rhoin)
  deallocate(dl_rhonew)
  deallocate(dl_rhotmp)

  call pop_sub()

end subroutine X(sternheimer_solve)

subroutine X(sternheimer_calc_hvar)(this, sys, h, lr, nsigma, hvar)
  type(sternheimer_t),    intent(inout) :: this
  type(system_t),         intent(inout) :: sys
  type(hamiltonian_t),    intent(inout) :: h
  type(lr_t),             intent(inout) :: lr(:) 
  integer,                intent(in)    :: nsigma 
  R_TYPE,                 intent(out)   :: hvar(:,:,:)

  R_TYPE, allocatable :: tmp(:), hartree(:)
  integer :: np, i, ik, ik2

  call push_sub('sternheimer_inc.sternheimer_calc_hvar')

  np = sys%gr%m%np

  if (this%add_hartree) then 

    ALLOCATE(tmp(1:np), np)
    ALLOCATE(hartree(1:np), np)
    do i = 1, np
      tmp(i) = sum(lr(1)%X(dl_rho)(i, 1:sys%st%d%nspin))
    end do
    hartree(1:np) = R_TOTYPE(M_ZERO)
    call X(poisson_solve)(sys%gr, hartree, tmp, all_nodes = .false.)

    deallocate(tmp)

  end if

  do ik = 1, sys%st%d%nspin
    
    !* initialize
    hvar(1:np, ik, 1) = M_ZERO

    !* hartree
    if (this%add_hartree) hvar(1:np, ik, 1) = hvar(1:np, ik, 1) + hartree(1:np)
    
    !* fxc
    if(this%add_fxc) then
      if(.not. this%oep_kernel) then 
        do ik2 = 1, sys%st%d%nspin
          hvar(1:np, ik, 1) = hvar(1:np, ik, 1) + this%fxc(1:np, ik, ik2)*lr(1)%X(dl_rho)(1:np, ik2)
        end do
      else
        call X(xc_oep_kernel_calc)(sys, h, lr, nsigma, hartree(:))
        hvar(1:np, ik, 1) = hvar(1:np, ik, 1) + hartree(1:np)
      end if
    end if
  end do
  
  if (nsigma == 2) hvar(1:np, 1:sys%st%d%nspin, 2) = R_CONJ(hvar(1:np, 1:sys%st%d%nspin, 1))

  if (this%add_hartree) deallocate(hartree)

  call pop_sub()
end subroutine X(sternheimer_calc_hvar)


!! Local Variables:
!! mode: f90
!! coding: utf-8
!! End:
