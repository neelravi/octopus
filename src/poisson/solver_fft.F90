!! Copyright (C) 2002-2006 M. Marques, A. Castro, A. Rubio, G. Bertsch
!!
!! This program is free software; you can redistribute it and/or modify
!! it under the terms of the GNU General Public License as published by
!! the Free Software Foundation; either version 2, or (at your option)
!! any later version.
!!
!! This program is distributed in the hope that it will be useful,
!! but WITHOUT ANY WARRANTY; without even the implied warranty of
!! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!! GNU General Public License for more details.
!!
!! You should have received a copy of the GNU General Public License
!! along with this program; if not, write to the Free Software
!! Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
!! 02111-1307, USA.
!!
!! $Id: poisson.F90 2660 2007-01-23 15:11:54Z lorenzen $

#include "global.h"

module poisson_fft_m
  use cube_function_m
  use datasets_m
  use fft_m
  use fourier_space_m
  use geometry_m
  use global_m
  use grid_m
  use lalg_basic_m
  use loct_m
  use loct_math_m
  use loct_parser_m
  use math_m
  use mesh_function_m
  use mesh_m
  use messages_m
  use mpi_m
  use par_vec_m
  use poisson_cutoffs_m
  use profiling_m
  use simul_box_m
  use splines_m
  use units_m

  implicit none

  private
  public ::                  &
    poisson_fft_build_1d,    &
    poisson_fft_build_2d_0d, &
    poisson_fft_build_2d_1d, &
    poisson_fft_build_2d_2d, &
    poisson_fft_build_3d,    &
    poisson_fft_end,         &
    poisson_fft

  integer, public, parameter :: &
       FFT_SPH       =  0,      &
       FFT_CYL       =  1,      &
       FFT_PLA       =  2,      &
       FFT_NOCUT     =  3,      &
       FFT_CORRECTED =  4

  type(dcf_t), public      :: fft_cf
  type(fourier_space_op_t) :: coulb

contains

  !-----------------------------------------------------------------
  subroutine poisson_fft_build_3d(gr, poisson_solver)
    type(grid_t), intent(inout) :: gr
    integer,      intent(in)    :: poisson_solver

    type(spline_t)     :: cylinder_cutoff_f
    FLOAT, allocatable :: x(:), y(:)
    integer :: ix, iy, iz, ixx(MAX_DIM), db(MAX_DIM), k, ngp, idim
    FLOAT :: temp(MAX_DIM), modg2, xmax
    FLOAT :: gpar, gperp, gx, gy, gz, r_c, gg(MAX_DIM)
    FLOAT :: DELTA_R = CNST(1.0e-12)
    FLOAT, allocatable :: fft_coulb_FS(:,:,:)

    call push_sub('solver_fft.poisson_fft_build_3d')
    
    ! double the box to perform the fourier transforms
    if(poisson_solver.ne.FFT_CORRECTED) then
      call mesh_double_box(gr%sb, gr%mesh, db)                 ! get dimensions of the double box
      if (poisson_solver == FFT_SPH) db(:) = maxval(db)
    else
      db(:) = gr%mesh%idx%ll(:)
    end if

    ! allocate cube function where we will perform the ffts
    call dcf_new(db, fft_cf)

    ! initialize the fft
    call dcf_fft_init(fft_cf, gr%sb)

    ! dimensions may have been optimized
    db = fft_cf%n

    if (poisson_solver <= FFT_PLA .and. poisson_solver .ne. FFT_CORRECTED) then
      call loct_parse_float(datasets_check('PoissonCutoffRadius'),&
        maxval(db(:)*gr%mesh%h(:)/M_TWO)/units_inp%length%factor , r_c)
      r_c = r_c*units_inp%length%factor
      write(message(1),'(3a,f12.6)')'Info: Poisson Cutoff Radius [',  &
        trim(units_out%length%abbrev), '] = ',       &
        r_c/units_out%length%factor
      call write_info(1)
      if ( r_c > maxval(db(:)*gr%mesh%h(:)/M_TWO) + DELTA_R) then
        message(1) = 'Poisson cutoff radius is larger than cell size.'
        message(2) = 'You can see electrons in next cell(s).'
        call write_warning(2)
      end if
    end if

    ! store the fourier transform of the Coulomb interaction
    ALLOCATE(fft_Coulb_FS(fft_cf%nx, fft_cf%n(2), fft_cf%n(3)), fft_cf%nx*fft_cf%n(2)*fft_cf%n(3))
    fft_Coulb_FS = M_ZERO

    temp(:) = M_TWO*M_PI/(db(:)*gr%mesh%h(:))

    if( (poisson_solver .eq. FFT_CYL)  .and. (gr%sb%periodic_dim == 0) ) then
      ngp = 8*db(2)
      ALLOCATE(x(ngp), ngp)
      ALLOCATE(y(ngp), ngp)
    end if


    do ix = 1, fft_cf%nx
      ixx(1) = pad_feq(ix, db(1), .true.)
      gx = temp(1)*ixx(1)

      if( (poisson_solver .eq. FFT_CYL)  .and. (gr%sb%periodic_dim == 0) ) then
        call spline_init(cylinder_cutoff_f)
        xmax = sqrt((temp(2)*db(2)/2)**2 + (temp(3)*db(3)/2)**2)
        do k = 1, ngp
          x(k) = (k-1)*(xmax/(ngp-1))
          y(k) = poisson_cutoff_fin_cylinder(gx, x(k), M_TWO*gr%mesh%sb%xsize, M_TWO*gr%mesh%sb%rsize)
        end do
        call spline_fit(ngp, x, y, cylinder_cutoff_f)
      end if

      do iy = 1, db(2)
        ixx(2) = pad_feq(iy, db(2), .true.)
        do iz = 1, db(3)
          ixx(3) = pad_feq(iz, db(3), .true.)

          gg(:) = temp(:)*ixx(:)

          gg(:) = matmul(gg, gr%mesh%sb%klattice_unitary)
          do idim = 1, gr%mesh%sb%dim
            gg(idim) = gg(idim) / lalg_nrm2(MAX_DIM, gr%mesh%sb%klattice_unitary(:, idim))
          end do

          modg2 = sum(gg(:)**2)

          if(modg2 /= M_ZERO) then
            select case(poisson_solver)
            case(FFT_SPH)
              fft_Coulb_FS(ix, iy, iz) = poisson_cutoff_sphere(sqrt(modg2),r_c)/modg2

            case(FFT_CYL)
              gperp = hypot(gg(2), gg(3))
              if (gr%sb%periodic_dim==1) then
                fft_Coulb_FS(ix, iy, iz) = poisson_cutoff_inf_cylinder(abs(gx), gperp, r_c)/modg2

              else if (gr%sb%periodic_dim==0) then
                gy = gg(2)
                gz = gg(3)
                if ((gz >= M_ZERO) .and. (gy >= M_ZERO)) then
                  fft_Coulb_FS(ix, iy, iz) = spline_eval(cylinder_cutoff_f, gperp)
                end if
                if ((gz >= M_ZERO) .and. (gy < M_ZERO)) then
                  fft_Coulb_FS(ix, iy, iz) = fft_Coulb_FS(ix, -ixx(2) + 1, iz)
                end if
                if ((gz < M_ZERO) .and. (gy >= M_ZERO)) then
                  fft_Coulb_FS(ix, iy, iz) = fft_Coulb_FS(ix, iy, -ixx(3) + 1)
                end if
                if ((gz < M_ZERO) .and. (gy < M_ZERO) ) then
                  fft_Coulb_FS(ix, iy, iz) = fft_Coulb_FS(ix, -ixx(2) + 1, -ixx(3) + 1)
                end if
              end if

            case(FFT_PLA)
              gz = abs(gg(3))
              gpar = hypot(gg(1), gg(2))
              fft_Coulb_FS(ix, iy, iz) = poisson_cutoff_slab(gpar,gz,r_c)/modg2

            case(FFT_NOCUT, FFT_CORRECTED)
              fft_Coulb_FS(ix, iy, iz) = M_ONE/modg2
            end select

          else
            select case(poisson_solver)
            case(FFT_SPH)
              fft_Coulb_FS(ix, iy, iz) = r_c**2/M_TWO

            case (FFT_CYL)
              if (gr%sb%periodic_dim == 1) then
                fft_Coulb_FS(ix, iy, iz) = -(M_HALF*log(r_c) - M_FOURTH)*r_c**2
              else if (gr%sb%periodic_dim == 0) then
                fft_Coulb_FS(ix, iy, iz) = poisson_cutoff_fin_cylinder(M_ZERO, M_ZERO, &
                  M_TWO*gr%mesh%sb%xsize, M_TWO*gr%mesh%sb%rsize)
              end if

            case(FFT_PLA)
              fft_Coulb_FS(ix, iy, iz) = -M_HALF*r_c**2

            case (FFT_NOCUT, FFT_CORRECTED)
              fft_Coulb_FS(ix, iy, iz) = M_ZERO

            end select
          end if
        end do
      end do

      if( (poisson_solver .eq. FFT_CYL) .and. (gr%sb%periodic_dim == 0) ) then
        call spline_end(cylinder_cutoff_f)
      end if
    end do

    do k=1, fft_cf%n(3)
      fft_Coulb_FS(1:fft_cf%nx, 1:fft_cf%n(2), k) = M_FOUR*M_PI*fft_Coulb_FS(1:fft_cf%nx, 1:fft_cf%n(2), k)
    end do

    call dfourier_space_op_init(coulb, fft_cf, fft_Coulb_FS)

    deallocate(fft_Coulb_FS)

    if( (poisson_solver .eq. FFT_CYL) .and. (gr%sb%periodic_dim == 0) ) then
      deallocate(x, y)
    end if
    
    call pop_sub()
  end subroutine poisson_fft_build_3d
  !-----------------------------------------------------------------


  !-----------------------------------------------------------------
  subroutine poisson_fft_build_2d_0d(gr)
    type(grid_t), intent(in) :: gr

    type(spline_t) :: besselintf
    integer :: i, ix, iy, ixx(MAX_DIM), db(MAX_DIM), npoints
    FLOAT :: temp(MAX_DIM), vec, r_c, maxf, dk
    FLOAT :: DELTA_R = CNST(1.0e-12)
    FLOAT, allocatable :: x(:), y(:)
    FLOAT, allocatable :: fft_coulb_FS(:,:,:)

    call push_sub('solver_fft.poisson_fft_build_2d_0d')

    ! double the box to perform the fourier transforms
    call mesh_double_box(gr%sb, gr%mesh, db)                 ! get dimensions of the double box
    db(1:2) = maxval(db)

    ! allocate cube function where we will perform the ffts
    call dcf_new(db, fft_cf)      
    ! initialize fft
    call dcf_fft_init(fft_cf, gr%sb)
    ! dimensions may have been optimized
    db = fft_cf%n              

    call loct_parse_float(datasets_check('PoissonCutoffRadius'),&
      maxval(db(:)*gr%mesh%h(:)/M_TWO)/units_inp%length%factor , r_c)
    r_c = r_c*units_inp%length%factor
    write(message(1),'(3a,f12.6)')'Info: Poisson Cutoff Radius [',  &
      trim(units_out%length%abbrev), '] = ',       &
      r_c/units_out%length%factor
    call write_info(1)
    if ( r_c > maxval(db(:)*gr%mesh%h(:)/M_TWO) + DELTA_R) then
      message(1) = 'Poisson cutoff radius is larger than cell size.'
      message(2) = 'You can see electrons in next cell(s).'
      call write_warning(2)
    end if
    call spline_init(besselintf)

    ! store the fourier transform of the Coulomb interaction
    ALLOCATE(fft_Coulb_FS(fft_cf%nx, fft_cf%n(2), fft_cf%n(3)), fft_cf%nx*fft_cf%n(2)*fft_cf%n(3))
    fft_Coulb_FS = M_ZERO
    temp(:) = M_TWO*M_PI/(db(:)*gr%mesh%h(:))

    maxf = r_c * sqrt((temp(1)*db(1)/2)**2 + (temp(2)*db(2)/2)**2)
    dk = CNST(0.25) ! This seems to be reasonable.
    npoints = nint(maxf/dk)
    ALLOCATE(x(npoints), npoints)
    ALLOCATE(y(npoints), npoints)
    x(1) = M_ZERO
    y(1) = M_ZERO
    do i = 2, npoints
      x(i) = (i-1) * maxf / (npoints-1)
      y(i) = y(i-1) + poisson_cutoff_fin_2D(x(i-1), x(i))
    end do
    call spline_fit(npoints, x, y, besselintf)

    do iy = 1, db(2)
      ixx(2) = pad_feq(iy, db(2), .true.)
      do ix = 1, fft_cf%nx
        ixx(1) = pad_feq(ix, db(1), .true.)
        vec = sqrt( (temp(1)*ixx(1))**2 + (temp(2)*ixx(2))**2)
        if (vec > M_ZERO) then
           fft_coulb_fs(ix, iy, 1) = (M_TWO * M_PI / vec) * spline_eval(besselintf, vec*r_c)
        else
           fft_coulb_fs(ix, iy, 1) = M_TWO * M_PI * r_c
        end if
      end do
    end do

    call dfourier_space_op_init(coulb, fft_cf, fft_Coulb_FS)

    deallocate(fft_Coulb_FS)
    deallocate(x, y)
    call spline_end(besselintf)
    call pop_sub()
  end subroutine poisson_fft_build_2d_0d
  !-----------------------------------------------------------------
    

  !-----------------------------------------------------------------
  subroutine poisson_fft_build_2d_1d(gr)
    type(grid_t), intent(in) :: gr

    integer :: ix, iy, ixx(MAX_DIM), db(MAX_DIM)
    FLOAT :: temp(MAX_DIM), vec, r_c, gx, gy
    FLOAT, allocatable :: fft_coulb_FS(:,:,:)

    call push_sub('solver_fft.poisson_fft_build_2d_1d')

    ! double the box to perform the fourier transforms
    call mesh_double_box(gr%sb, gr%mesh, db)                 ! get dimensions of the double box

    ! allocate cube function where we will perform the ffts
    call dcf_new(db, fft_cf)      
    ! initialize fft
    call dcf_fft_init(fft_cf, gr%sb)
    ! dimensions may have been optimized
    db = fft_cf%n    

    r_c = M_TWO * gr%sb%lsize(2)

    ! store the fourier transform of the Coulomb interaction
    ALLOCATE(fft_Coulb_FS(fft_cf%nx, fft_cf%n(2), fft_cf%n(3)), fft_cf%nx*fft_cf%n(2)*fft_cf%n(3))
    fft_Coulb_FS = M_ZERO
    temp(:) = M_TWO*M_PI/(db(:)*gr%mesh%h(:))

    ! First, the term ix = 0 => gx = 0.
    fft_coulb_fs(1, 1, 1) = -M_FOUR * r_c * (log(r_c)-M_ONE)
    do iy = 2, db(2)
      ixx(2) = pad_feq(iy, db(2), .true.)
      gy = temp(2)*ixx(2)
      fft_coulb_fs(1, iy, 1) = -M_FOUR * poisson_cutoff_intcoslog(r_c, gy, M_ONE )
    end do

    do ix = 2, fft_cf%nx
      ixx(1) = pad_feq(ix, db(1), .true.)
      gx = temp(1)*ixx(1)
      do iy = 1, db(2)
        ixx(2) = pad_feq(iy, db(2), .true.)
        gy = temp(2)*ixx(2)
        vec = sqrt( (temp(1)*ixx(1))**2 + (temp(2)*ixx(2))**2)
        fft_coulb_fs(ix, iy, 1) = poisson_cutoff_2d_1d(gy, gx, r_c)
      end do
    end do

    call dfourier_space_op_init(coulb, fft_cf, fft_Coulb_FS)

    deallocate(fft_Coulb_FS)

    call pop_sub()
  end subroutine poisson_fft_build_2d_1d
  !-----------------------------------------------------------------


  !-----------------------------------------------------------------
  subroutine poisson_fft_build_2d_2d(gr)
    type(grid_t), intent(in) :: gr

    integer :: ix, iy, ixx(MAX_DIM), db(MAX_DIM)
    FLOAT :: temp(MAX_DIM), vec
    FLOAT, allocatable :: fft_coulb_FS(:,:,:)

    call push_sub('solver_fft.poisson_fft_build_2d_2d')

    db(:) = gr%mesh%idx%ll(:)
    ! allocate cube function where we will perform the ffts
    call dcf_new(db, fft_cf)      
    ! initialize fft
    call dcf_fft_init(fft_cf, gr%sb)
    ! dimensions may have been optimized
    db = fft_cf%n              

    ! store the fourier transform of the Coulomb interaction
    ALLOCATE(fft_Coulb_FS(fft_cf%nx, fft_cf%n(2), fft_cf%n(3)), fft_cf%nx*fft_cf%n(2)*fft_cf%n(3))
    fft_Coulb_FS = M_ZERO
    temp(:) = M_TWO*M_PI/(db(:)*gr%mesh%h(:))

    do iy = 1, db(2)
      ixx(2) = pad_feq(iy, db(2), .true.)
      do ix = 1, fft_cf%nx
        ixx(1) = pad_feq(ix, db(1), .true.)
        vec = sqrt( (temp(1)*ixx(1))**2 + (temp(2)*ixx(2))**2)
        if (vec > M_ZERO) fft_coulb_fs(ix, iy, 1) = M_TWO * M_PI / vec
      end do
    end do

    call dfourier_space_op_init(coulb, fft_cf, fft_Coulb_FS)

    deallocate(fft_Coulb_FS)
    call pop_sub()
  end subroutine poisson_fft_build_2d_2d
  !-----------------------------------------------------------------


  !-----------------------------------------------------------------
  subroutine poisson_fft_build_1d(gr, poisson_soft_coulomb_param)
    type(grid_t), intent(in) :: gr
    FLOAT,        intent(in) :: poisson_soft_coulomb_param

    integer            :: box(MAX_DIM), ix
    FLOAT              :: g
    FLOAT, allocatable :: fft_coulb_fs(:, :, :)

    call push_sub('solver_fft.poisson_fft_build_1d')

    box = gr%mesh%idx%ll
    call dcf_new(box, fft_cf)
    call dcf_fft_init(fft_cf, gr%sb)
    box = fft_cf%n

    ALLOCATE(fft_coulb_fs(fft_cf%nx, fft_cf%n(2), fft_cf%n(3)), fft_cf%nx*fft_cf%n(2)*fft_cf%n(3))

    fft_coulb_fs = M_ZERO

    ! Fourier transform of Soft Coulomb interaction.
    do ix = 1, fft_cf%nx
      g = ix*M_PI/gr%sb%lsize(1) ! note that g is always positive with this definition
      fft_coulb_fs(ix, 1, 1) = sqrt(M_TWO/M_PI) * loct_bessel_k0(poisson_soft_coulomb_param*g)
    end do

    call dfourier_space_op_init(coulb, fft_cf, fft_coulb_fs)
    deallocate(fft_coulb_fs)
    
    call pop_sub()
  end subroutine poisson_fft_build_1d
  !-----------------------------------------------------------------


  !-----------------------------------------------------------------
  subroutine poisson_fft_end()
    call push_sub('solver_fft.poisson_fft.end')

    call dcf_free(fft_cf)
    call dfourier_space_op_end(coulb)

    call pop_sub()
  end subroutine poisson_fft_end
  !-----------------------------------------------------------------


  !-----------------------------------------------------------------
  subroutine poisson_fft(m, pot, rho, average_to_zero)
    type(mesh_t), intent(in) :: m
    FLOAT, intent(out) :: pot(:) ! pot(m%np)
    FLOAT, intent(in)  :: rho(:) ! rho(m%np)
    logical, intent(in), optional :: average_to_zero

    FLOAT, allocatable :: rho_global(:), pot_global(:)

    FLOAT :: average

    call push_sub('poisson.poisson_fft')

    average=M_ZERO !this avoids a non-initialized warning
    
    if(m%parallel_in_domains) then
      ALLOCATE(rho_global(m%np_global), m%np_global)
      ALLOCATE(pot_global(m%np_global), m%np_global)
    end if

    call dcf_alloc_RS(fft_cf)          ! allocate the cube in real space

    ! put the density in the cube
    if(m%parallel_in_domains) then
#if defined HAVE_MPI
      call dvec_gather(m%vp, rho_global, rho)
      call dmesh_to_cube(m, rho_global, fft_cf) 
#endif
    else
      call dmesh_to_cube(m, rho, fft_cf)
    end if

    ! apply the Couloumb term in Fourier space
    call dfourier_space_op_apply(coulb, fft_cf)

    !now the cube has the potential
    if(present(average_to_zero)) then
      if(average_to_zero) average = cf_surface_average(fft_cf)
#if defined HAVE_MPI
      ! Only root has the right average.
      if(m%parallel_in_domains) call MPI_Bcast(average, 1, MPI_FLOAT, 0, m%mpi_grp%comm, mpi_err)
#endif
    end if

    ! move the potential back to the grid
    if(m%parallel_in_domains) then
#if defined(HAVE_MPI)
      call dcube_to_mesh(m, fft_cf, pot_global)
      call dvec_scatter(m%vp, pot_global, pot)
#endif
    else
      call dcube_to_mesh(m, fft_cf, pot)
    end if

    if(present(average_to_zero)) then
      if(average_to_zero) pot(1:m%np) = pot(1:m%np) - average
    end if

    call dcf_free_RS(fft_cf)           ! memory is no longer needed

    if(m%parallel_in_domains) then
      deallocate(rho_global, pot_global)
    end if

    call pop_sub()
  end subroutine poisson_fft

end module poisson_fft_m

!! Local Variables:
!! mode: f90
!! coding: utf-8
!! End:
