!! Copyright (C) 2002-2006 M. Marques, A. Castro, A. Rubio, G. Bertsch
!!
!! This program is free software; you can redistribute it and/or modify
!! it under the terms of the GNU General Public License as published by
!! the Free Software Foundation; either version 2, or (at your option)
!! any later version.
!!
!! This program is distributed in the hope that it will be useful,
!! but WITHOUT ANY WARRANTY; without even the implied warranty of
!! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!! GNU General Public License for more details.
!!
!! You should have received a copy of the GNU General Public License
!! along with this program; if not, write to the Free Software
!! Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
!! 02111-1307, USA.
!!
!! $Id$

#include "global.h"

module poisson_m
  use datasets_m
  use derivatives_m
  use geometry_m
  use global_m
  use index_m
  use io_m
  use io_function_m
  use loct_math_m
  use math_m
  use mesh_m
  use mesh_function_m
  use messages_m
  use mpi_m
  use multicomm_m
  use par_vec_m
  use parser_m
  use poisson_cg_m
  use poisson_corrections_m
  use poisson_isf_m
  use poisson_fft_m
  use poisson_multigrid_m
  use poisson_sete_m
  use profiling_m
  use simul_box_m
  use unit_m
  use unit_system_m
  use varinfo_m
  use xyz_file_m      

  implicit none

  private
  public ::                      &
    poisson_t,                   &
    poisson_get_solver,          &
    poisson_init,                &
    dpoisson_solve,              &
    zpoisson_solve,              &
    poisson_solver_is_iterative, &
    poisson_solver_has_free_bc,  &
    poisson_end,                 &
    poisson_test,                &
    poisson_is_multigrid,        &
    poisson_energy,              &
    poisson_slave_work,          &
    poisson_async_init,          &
    poisson_async_end
  
  integer, public, parameter ::         &
    POISSON_DIRECT_SUM_1D = -1,         &
    POISSON_DIRECT_SUM_2D = -2,         &
    POISSON_CG            =  5,         &
    POISSON_CG_CORRECTED  =  6,         &
    POISSON_MULTIGRID     =  7,         &
    POISSON_ISF           =  8,         &
    POISSON_SETE          =  9
  ! the FFT solvers are defined in its own module

  type poisson_t
    private
    type(derivatives_t), pointer :: der
    integer :: method = -99
    type(mg_solver_t) :: mg
    FLOAT   :: poisson_soft_coulomb_param = M_ONE
    logical :: all_nodes_default
    type(poisson_corr_t) :: corrector
    type(poisson_sete_t) :: sete_solver
#ifdef HAVE_MPI2
    integer              :: nslaves
    integer              :: intercomm
#endif
  end type poisson_t

  type(poisson_t), target, save, public :: psolver

  integer, parameter ::             &
    CMD_FINISH = 1

contains

  !-----------------------------------------------------------------
  subroutine poisson_init(this, der, geo)
    type(poisson_t),             intent(out)   :: this
    type(derivatives_t), target, intent(inout) :: der
    type(geometry_t),            intent(in)    :: geo

    if(this%method.ne.-99) return ! already initialized

    PUSH_SUB(poisson_init)

    this%der => der

    call messages_print_stress(stdout, "Hartree")

#ifdef HAVE_MPI
    !%Variable ParallelizationPoissonAllNodes
    !%Type logical
    !%Default true
    !%Section Execution::Parallelization
    !%Description
    !% When running in parallel, this variable selects whether the
    !% Poisson solver should divide the work among all nodes or only
    !% among the parallelization-in-domains groups.
    !%End

    call parse_logical(datasets_check('ParallelizationPoissonAllNodes'), .true., this%all_nodes_default)
#endif

    select case(der%mesh%sb%dim)
    case(1); call init_1D()
    case(2); call init_2D()
    case(3); call init_3D()
    end select

    call messages_print_stress(stdout)

    POP_SUB(poisson_init)
  contains

    !%Variable PoissonSolver
    !%Type integer
    !%Section Hamiltonian::Poisson
    !%Description
    !% Defines which method to use to solve the Poisson equation. Defaults:
    !% <br> 1D: <tt>fft</tt> if not periodic, <tt>fft_nocut</tt> if periodic.
    !% <br> 2D: <tt>fft</tt> if not periodic, <tt>fft_cyl</tt> if periodic in 1D, <tt>fft_nocut</tt> if periodic in 2D.
    !% <br> 3D: <tt>cg_corrected</tt> if curvilinear, <tt>isf</tt> if not periodic, <tt>fft_cyl</tt> if periodic in 1D,
    !% <tt>fft_pla</tt> if periodic in 2D, <tt>fft_nocut</tt> if periodic in 3D.
    !%Option direct2D -2
    !% Direct evaluation of the Hartree potential (in 2D).
    !%Option direct1D -1
    !% Direct evaluation of the Hartree potential (in 1D).
    !%Option fft 0
    !% FFTs using spherical cutoff (in 2D or 3D; uses FFTW).
    !%Option fft_cyl 1
    !% FFTs using cylindrical cutoff (in 3D; uses FFTW).
    !%Option fft_pla 2
    !% FFTs using planar cutoff (in 3D; uses FFTW).
    !%Option fft_nocut 3
    !% FFTs without using a cutoff (in 3D; uses FFTW).
    !%Option fft_corrected 4
    !% FFTs + corrections.
    !%Option cg 5
    !% Conjugate gradients.
    !%Option cg_corrected 6
    !% Corrected conjugate gradients.
    !%Option multigrid 7
    !% Multigrid method.
    !%Option isf 8
    !% Interpolating Scaling Functions Poisson solver.
    !%Option sete 9
    !% (Experimental) SETE solver.
    !%End

    !------------------------------------------------------------------
    subroutine init_1D()
      integer :: default_solver

      PUSH_SUB(poisson_init.init_1D)

      if(der%mesh%sb%periodic_dim==0) then
        default_solver = POISSON_FFT_SPH
      else
        default_solver = POISSON_FFT_NOCUT
      end if
      call parse_integer(datasets_check('PoissonSolver'), default_solver, this%method)

      select case(der%mesh%sb%periodic_dim)
      case(0)
        if( (this%method.ne.POISSON_FFT_SPH)       .and. &
            (this%method.ne.POISSON_DIRECT_SUM_1D)) call input_error('PoissonSolver')
      case(1)
        if( (this%method.ne.POISSON_FFT_NOCUT) ) call input_error('PoissonSolver')
      end select

      if(der%mesh%use_curvilinear.and.this%method.ne.POISSON_DIRECT_SUM_1D) then
        message(1) = 'If curvilinear coordinates are used in 1D, then the only working'
        message(2) = 'Poisson solver is -1 ("direct summation in one dimension").'
        call write_fatal(2)
      end if

      call messages_print_var_option(stdout, "PoissonSolver", this%method)
      call poisson1d_init(this)

      POP_SUB(poisson_init.init_1D)
    end subroutine init_1D


    !-----------------------------------------------------------------
    subroutine init_2D()
      integer :: default_solver
      PUSH_SUB(poisson_init.init_2D)

      if (der%mesh%sb%periodic_dim > 0) then 
        default_solver = der%mesh%sb%periodic_dim
      else
        default_solver = POISSON_FFT_SPH
      end if

      call parse_integer(datasets_check('PoissonSolver'), der%mesh%sb%periodic_dim, this%method)
      if( (this%method .ne. POISSON_FFT_SPH)         .and. &
          (this%method .ne. POISSON_DIRECT_SUM_2D)   .and. &
          (this%method .ne. 1)                       .and. &
          (this%method .ne. 2)                       .and. &
          (this%method .ne. 3)                       ) then
        call input_error('PoissonSolver')
      end if

      ! In 2D, periodic in two dimensions means no cut-off at all.
      if(this%method == 2) this%method = 3

      if(der%mesh%use_curvilinear .and. (this%method .ne. -der%mesh%sb%dim) ) then
        message(1) = 'If curvilinear coordinates are used in 2D, then the only working'
        message(2) = 'Poisson solver is -2 ("direct summation in two dimensions").'
        call write_fatal(2)
      end if

      call messages_print_var_option(stdout, "PoissonSolver", this%method)
      call poisson2D_init(this)

      POP_SUB(poisson_init.init_2D)
    end subroutine init_2D


    !-----------------------------------------------------------------
    subroutine init_3D()
      integer :: default_solver 

      PUSH_SUB(poisson_init.init_3D)

#ifndef SINGLE_PRECISION
      default_solver = POISSON_ISF
#else
      default_solver = POISSON_FFT_SPH
#endif

      if (der%mesh%use_curvilinear) default_solver = POISSON_CG_CORRECTED
      if (der%mesh%sb%periodic_dim > 0) default_solver = der%mesh%sb%periodic_dim
      
      call parse_integer(datasets_check('PoissonSolver'), default_solver, this%method)
      if(this%method < POISSON_FFT_SPH .or. this%method > POISSON_SETE ) then
        call input_error('PoissonSolver')
      end if

      if(der%mesh%sb%periodic_dim > 0 .and. &
           this%method /= der%mesh%sb%periodic_dim .and. &
           this%method < POISSON_CG .and. &
           this%method /= POISSON_FFT_CORRECTED .and. this%method /= POISSON_FFT_CYL) then
        write(message(1), '(a,i1,a)')'The system is periodic in ', der%mesh%sb%periodic_dim ,' dimension(s),'
        write(message(2), '(a,i1,a)')'but Poisson solver is set for ',this%method,' dimensions.'
        message(3) =                 'You know what you are doing, right?'
        call write_warning(3)
      end if

      if(der%mesh%use_curvilinear .and. (this%method.ne.POISSON_CG_CORRECTED)) then
        message(1) = 'If curvilinear coordinates are used, then the only working'
        message(2) = 'Poisson solver is cg_corrected.'
        call write_fatal(2)
      end if

      if( (der%mesh%sb%box_shape == MINIMUM) .and. (this%method == POISSON_CG_CORRECTED) ) then
        message(1) = 'When using the "minimum" box shape and the "cg_corrected"'
        message(2) = 'Poisson solver, we have observed "sometimes" some non-'
        message(3) = 'negligible error. You may want to check that the "fft" or "cg"'
        message(4) = 'solver are providing, in your case, the same results.'
        call write_warning(4)
      end if

      if (this%method == POISSON_SETE) then
        call messages_experimental('SETE poisson solver')
      end if

      call messages_print_var_option(stdout, "PoissonSolver", this%method)
      call poisson3D_init(this, geo)

      POP_SUB(poisson_init.init_3D)
    end subroutine init_3D

  end subroutine poisson_init


  !-----------------------------------------------------------------
  subroutine poisson_end(this)
    type(poisson_t), intent(inout) :: this

    PUSH_SUB(poisson_end)

    select case(this%method)

    case(POISSON_FFT_SPH,POISSON_FFT_CYL, POISSON_FFT_PLA, POISSON_FFT_NOCUT)
      call poisson_fft_end()
    case(POISSON_FFT_CORRECTED)
      call poisson_fft_end()
      call poisson_corrections_end(this%corrector)

    case(POISSON_CG_CORRECTED, POISSON_CG)
      call poisson_cg_end()
      call poisson_corrections_end(this%corrector)

    case(POISSON_MULTIGRID)
      call poisson_multigrid_end(this%mg)
    case(POISSON_ISF)
      call poisson_isf_end()
    case(POISSON_SETE)
      call poisson_sete_end(this%sete_solver)

    end select
    this%method = -99

    POP_SUB(poisson_end)
  end subroutine poisson_end

  !-----------------------------------------------------------------

  subroutine zpoisson_solve(this, pot, rho, all_nodes)
    type(poisson_t),      intent(inout) :: this
    CMPLX,                intent(inout) :: pot(:)  ! pot(mesh%np)
    CMPLX,                intent(in)    :: rho(:)  ! rho(mesh%np)
    logical, optional,    intent(in)    :: all_nodes

    FLOAT, allocatable :: aux1(:), aux2(:)
    type(derivatives_t), pointer :: der
    logical :: all_nodes_value

    der => this%der

    PUSH_SUB(zpoisson_solve)

    if(present(all_nodes)) then
      all_nodes_value = all_nodes
    else
      all_nodes_value = this%all_nodes_default
    end if

    SAFE_ALLOCATE(aux1(1:der%mesh%np))
    SAFE_ALLOCATE(aux2(1:der%mesh%np))

    ! first the real part
    aux1(1:der%mesh%np) = real(rho(1:der%mesh%np))
    aux2(1:der%mesh%np) = real(pot(1:der%mesh%np))
    call dpoisson_solve(this, aux2, aux1, all_nodes=all_nodes_value)
    pot(1:der%mesh%np)  = aux2(1:der%mesh%np)

    ! now the imaginary part
    aux1(1:der%mesh%np) = aimag(rho(1:der%mesh%np))
    aux2(1:der%mesh%np) = aimag(pot(1:der%mesh%np))
    call dpoisson_solve(this, aux2, aux1, all_nodes=all_nodes_value)
    pot(1:der%mesh%np) = pot(1:der%mesh%np) + M_zI*aux2(1:der%mesh%np)

    SAFE_DEALLOCATE_A(aux1)
    SAFE_DEALLOCATE_A(aux2)

    POP_SUB(zpoisson_solve)
  end subroutine zpoisson_solve

  !-----------------------------------------------------------------

  subroutine dpoisson_solve(this, pot, rho, all_nodes)
    type(poisson_t),      intent(inout) :: this
    FLOAT,                intent(inout) :: pot(:)
    FLOAT,                intent(in)    :: rho(:)
    logical, optional,    intent(in)    :: all_nodes ! Is the Poisson solver allowed to utilise
                                                     ! all nodes or only the domain nodes for
                                                     ! its calculations? (Defaults to .true.)

    type(derivatives_t), pointer :: der
    integer :: counter
    integer :: nx_half, nx
    integer :: ny_half, ny
    integer :: nz_half, nz

    FLOAT :: xl, yl, zl, dx, dy, dz, sum0

    FLOAT, allocatable :: vh0(:,:,:), rh0(:,:,:)
    integer :: icase = 1, icalc = 1, i1

    FLOAT, allocatable :: rho_corrected(:), vh_correction(:)

    logical               :: all_nodes_value
    type(profile_t), save :: prof
    
    integer :: conversion(3)
    
    call profiling_in(prof, 'POISSON_SOLVE')
    PUSH_SUB(dpoisson_solve)

    der => this%der

    ASSERT(ubound(pot, dim = 1) == der%mesh%np_part .or. ubound(pot, dim = 1) == der%mesh%np)
    ASSERT(ubound(rho, dim = 1) == der%mesh%np_part .or. ubound(rho, dim = 1) == der%mesh%np)

    ! Check optional argument and set to default if necessary.
    if(present(all_nodes)) then
      all_nodes_value = all_nodes
    else
      all_nodes_value = this%all_nodes_default
    end if

    ASSERT(this%method.ne.-99)

    select case(this%method)
    case(POISSON_DIRECT_SUM_1D)
      call poisson1d_solve(this, pot, rho)

    case(POISSON_DIRECT_SUM_2D)
      call poisson2d_solve(this, pot, rho)

    case(POISSON_CG)
      call poisson_cg1(der, this%corrector, pot, rho)

    case(POISSON_CG_CORRECTED)
      SAFE_ALLOCATE(rho_corrected(1:der%mesh%np))
      SAFE_ALLOCATE(vh_correction(1:der%mesh%np))
      
      call correct_rho(this%corrector, der%mesh, rho, rho_corrected, vh_correction)
      
      pot(1:der%mesh%np) = pot(1:der%mesh%np) - vh_correction(1:der%mesh%np)
      call poisson_cg2(der, pot, rho_corrected)
      pot(1:der%mesh%np) = pot(1:der%mesh%np) + vh_correction(1:der%mesh%np)
      
      SAFE_DEALLOCATE_A(rho_corrected)
      SAFE_DEALLOCATE_A(vh_correction)

    case(POISSON_MULTIGRID)
      call poisson_multigrid_solver(this%mg, der, pot, rho)

    case(POISSON_FFT_SPH,POISSON_FFT_CYL,POISSON_FFT_PLA,POISSON_FFT_NOCUT)
      call poisson_fft(der%mesh, pot, rho)

    case(POISSON_FFT_CORRECTED)
      SAFE_ALLOCATE(rho_corrected(1:der%mesh%np))
      SAFE_ALLOCATE(vh_correction(1:der%mesh%np))

      call correct_rho(this%corrector, der%mesh, rho, rho_corrected, vh_correction)
      call poisson_fft(der%mesh, pot, rho_corrected, average_to_zero = .true.)

      pot(1:der%mesh%np) = pot(1:der%mesh%np) + vh_correction(1:der%mesh%np)
      SAFE_DEALLOCATE_A(rho_corrected)
      SAFE_DEALLOCATE_A(vh_correction)

    case(POISSON_ISF)
      call poisson_isf_solve(der%mesh, pot, rho, all_nodes_value)

    case(POISSON_SETE)

       !Probably do not need to allocate so much: erased rhop2
       i1=size(rho); !allocate(rhop_temp(i1)); allocate(rhop(i1)); allocate(rho_nuc(i1));
       i1=size(pot);


      nx = der%mesh%idx%nr(2,1) - der%mesh%idx%nr(1,1) + 1 - 2*der%mesh%idx%enlarge(1)
      ny = der%mesh%idx%nr(2,2) - der%mesh%idx%nr(1,2) + 1 - 2*der%mesh%idx%enlarge(2)
      nz = der%mesh%idx%nr(2,3) - der%mesh%idx%nr(1,3) + 1 - 2*der%mesh%idx%enlarge(3)

      nx_half = (der%mesh%idx%nr(2,1) - der%mesh%idx%nr(1,1) - 2*der%mesh%idx%enlarge(1))/2 + 1
      ny_half = (der%mesh%idx%nr(2,2) - der%mesh%idx%nr(1,2) - 2*der%mesh%idx%enlarge(2))/2 + 1
      nz_half = (der%mesh%idx%nr(2,3) - der%mesh%idx%nr(1,3) - 2*der%mesh%idx%enlarge(3))/2 + 1

      SAFE_ALLOCATE(rh0(1:nx, 1:ny, 1:nz))
      SAFE_ALLOCATE(vh0(1:nx, 1:ny, 1:nz))

      ! The %size gives out half the size of a box. 
      xl = 2*der%mesh%sb%lsize(1) 
      yl = 2*der%mesh%sb%lsize(2) 
      zl = 2*der%mesh%sb%lsize(3)

      dx = der%mesh%spacing(1)
      dy = der%mesh%spacing(2)
      dz = der%mesh%spacing(3)

      sum0 = M_ZERO

      do counter = 1, der%mesh%np
        call  index_to_coords(der%mesh%idx,der%mesh%sb%dim,counter,conversion)
        conversion(1)=conversion(1)+nx_half
        conversion(2)=conversion(2)+ny_half
        conversion(3)=conversion(3)+nz_half
        vh0(conversion(1),conversion(2),conversion(3))=pot(counter)
        rh0(conversion(1),conversion(2),conversion(3))=rho(counter)
      end do

      call poisson_sete_solve(this%sete_solver, icase, rh0, vh0, nx, ny, nz, xl, yl, zl, icalc)

      do counter = 1, der%mesh%np

        call  index_to_coords(der%mesh%idx,der%mesh%sb%dim,counter,conversion)

        conversion(1) = conversion(1) + nx_half
        conversion(2) = conversion(2) + ny_half
        conversion(3) = conversion(3) + nz_half
        
        pot(counter)=vh0(conversion(1), conversion(2), conversion(3))

      end do

      SAFE_DEALLOCATE_A(vh0)
      SAFE_DEALLOCATE_A(rh0)

    end select

    POP_SUB(dpoisson_solve)
    call profiling_out(prof)
  end subroutine dpoisson_solve


  !-----------------------------------------------------------------
  !> This routine checks the Hartree solver selected in the input
  !! file by calculating numerically and analytically the Hartree
  !! potential originated by a Gaussian distribution of charge.
  !! This only makes sense for finite systems.
  subroutine poisson_test(mesh)
    type(mesh_t), intent(inout) :: mesh

    FLOAT, allocatable :: rho(:), vh(:), vh_exact(:), rhop(:), xx(:, :)
    FLOAT :: alpha, beta, rr, delta, norm, rnd, ralpha, range
    integer :: ip, idir, ierr, iunit, nn, n_gaussians

    PUSH_SUB(poisson_test)

    if(mesh%sb%dim == 1) then
      write(message(1),'(a)') 'The Hartree integrator test is not implemented for the one-dimensional case.'
      call write_warning(1)
      POP_SUB(poisson_test)
      return
    endif

    n_gaussians = 4

    SAFE_ALLOCATE(     rho(1:mesh%np))
    SAFE_ALLOCATE(    rhop(1:mesh%np))
    SAFE_ALLOCATE(      vh(1:mesh%np))
    SAFE_ALLOCATE(vh_exact(1:mesh%np))
    SAFE_ALLOCATE(xx(1:mesh%sb%dim, 1:n_gaussians))

    rho = M_ZERO; vh = M_ZERO; vh_exact = M_ZERO

    alpha = CNST(4.0)*mesh%spacing(1)
    beta = M_ONE / ( alpha**mesh%sb%dim * sqrt(M_PI)**mesh%sb%dim )

    write(message(1), '(a)') 'Building the Gaussian distribution of charge...'
    call write_info(1)

    range = CNST(8.0)

    rho = M_ZERO
    do nn = 1, n_gaussians
      norm = M_ZERO
      do while(abs(norm-M_ONE)> CNST(1.0e-4))
        do idir = 1, mesh%sb%dim
          call random_number(rnd)
          xx(idir, nn) = range*rnd 
        end do
        rr = sqrt(sum(xx(:, nn)*xx(:,nn)))
        do ip = 1, mesh%np
          call mesh_r(mesh, ip, rr, origin = xx(:, nn))
          rhop(ip) = beta*exp(-(rr/alpha)**2)
        end do
        norm = dmf_integrate(mesh, rhop)
      end do
      rhop = (-1)**nn * rhop
      rho = rho + rhop
    end do
    write(message(1), '(a,f14.6)') 'Total charge of the Gaussian distribution', dmf_integrate(mesh, rho)
    call write_info(1)

    ! This builds analytically its potential
    vh_exact = M_ZERO
    do nn = 1, n_gaussians
      do ip = 1, mesh%np
        call mesh_r(mesh, ip, rr, origin = xx(:, nn))
        select case(mesh%sb%dim)
        case(3)
          if(rr > r_small) then
            vh_exact(ip) = vh_exact(ip) + (-1)**nn * loct_erf(rr/alpha)/rr
          else
            vh_exact(ip) = vh_exact(ip) + (-1)**nn * (M_TWO/sqrt(M_PI))/alpha
          end if
        case(2)
          ralpha = rr**2/(M_TWO*alpha**2)
          if(ralpha < CNST(100.0)) then
            vh_exact(ip) = vh_exact(ip) + (-1)**nn * beta * (M_PI)**(M_THREE*M_HALF) * alpha * exp(-rr**2/(M_TWO*alpha**2)) * &
              loct_bessel_in(0, rr**2/(M_TWO*alpha**2))
          else
            vh_exact(ip) = vh_exact(ip) + (-1)**nn * beta * (M_PI)**(M_THREE*M_HALF) * alpha * &
                          (M_ONE/sqrt(M_TWO*M_PI*ralpha)) 
          end if
        end select
      end do
    end do

    ! This calculates the numerical potential
    call dpoisson_solve(psolver, vh, rho)

    ! And this compares.
    delta = dmf_nrm2(mesh, vh-vh_exact)

    ! Output
    iunit = io_open("hartree_results", action='write')
    write(iunit, '(a,f14.8)' ) 'Hartree test = ', delta
    call io_close(iunit)
    call doutput_function (io_function_fill_how('AxisX'), ".", "poisson_test_rho", mesh, rho, unit_one, ierr)
    call doutput_function (io_function_fill_how('AxisX'), ".", "poisson_test_exact", mesh, vh_exact, unit_one, ierr)
    call doutput_function (io_function_fill_how('AxisX'), ".", "poisson_test_numerical", mesh, vh, unit_one, ierr)
    call doutput_function (io_function_fill_how('AxisY'), ".", "poisson_test_rho", mesh, rho, unit_one, ierr)
    call doutput_function (io_function_fill_how('AxisY'), ".", "poisson_test_exact", mesh, vh_exact, unit_one, ierr)
    call doutput_function (io_function_fill_how('AxisY'), ".", "poisson_test_numerical", mesh, vh, unit_one, ierr)
    ! not dimensionless, but no need for unit conversion for a test routine

    SAFE_DEALLOCATE_A(rho)
    SAFE_DEALLOCATE_A(rhop)
    SAFE_DEALLOCATE_A(vh)
    SAFE_DEALLOCATE_A(vh_exact)
    SAFE_DEALLOCATE_A(xx)
    POP_SUB(poisson_test)
  end subroutine poisson_test

  ! -----------------------------------------------------------------

  logical pure function poisson_solver_is_iterative(this) result(iterative)
    type(poisson_t), intent(in) :: this

    iterative = this%method == POISSON_CG .or. this%method == POISSON_CG_CORRECTED .or. this%method == POISSON_MULTIGRID
  end function poisson_solver_is_iterative

  ! -----------------------------------------------------------------

  logical pure function poisson_is_multigrid(this) result(is_multigrid)
    type(poisson_t), intent(in) :: this
    
    is_multigrid = (this%method == POISSON_MULTIGRID)
    
  end function poisson_is_multigrid

  ! -----------------------------------------------------------------

  logical pure function poisson_solver_has_free_bc(this) result(free_bc)
    type(poisson_t), intent(in) :: this

    free_bc = &
      this%method /= POISSON_SETE      .and. &
      this%method /= POISSON_FFT_NOCUT .and. & 
      this%method /= POISSON_FFT_CYL   .and. & 
      this%method /= POISSON_FFT_PLA
      
  end function poisson_solver_has_free_bc

  !-----------------------------------------------------------------

  integer pure function poisson_get_solver(this) result (solver)
    type(poisson_t), intent(in) :: this

    solver = this%method
  end function poisson_get_solver

  !-----------------------------------------------------------------
  
  FLOAT function poisson_energy(this) result(energy)
    type(poisson_t),   intent(in) :: this

    energy = M_ZERO
    if(this%method == POISSON_SETE) energy = poisson_sete_energy(this%sete_solver)

  end function poisson_energy

  !-----------------------------------------------------------------

  subroutine poisson_async_init(this, mc)
    type(poisson_t), intent(inout) :: this
    type(multicomm_t), intent(in)  :: mc
      
    PUSH_SUB(poisson_async_init)

#ifdef HAVE_MPI2
    if(multicomm_have_slaves(mc)) then
      
      this%intercomm = mc%slave_intercomm
      call MPI_Comm_remote_size(this%intercomm, this%nslaves, mpi_err)

    end if
#endif
   
    POP_SUB(poisson_async_init)

  end subroutine poisson_async_init

  !-----------------------------------------------------------------
  
  subroutine poisson_async_end(this, mc)
    type(poisson_t), intent(inout) :: this
    type(multicomm_t), intent(in)  :: mc
    
    integer :: islave

    PUSH_SUB(poisson_async_end)

#ifdef HAVE_MPI2
    if(multicomm_have_slaves(mc)) then
      if(.not. multicomm_is_slave(mc)) then
        do islave = 1, this%nslaves
          ! send the finish signal
          call MPI_Send(M_ONE, 1, MPI_FLOAT, islave - 1, CMD_FINISH, this%intercomm, mpi_err) 
        end do
      end if
    end if
#endif

    POP_SUB(poisson_async_end)

  end subroutine poisson_async_end

  !-----------------------------------------------------------------

  subroutine poisson_slave_work(this)
    type(poisson_t), intent(inout) :: this

#ifdef HAVE_MPI2    
    FLOAT, allocatable :: rho(:)
    logical :: done
    integer :: status(MPI_STATUS_SIZE)

    PUSH_SUB(poisson_slave_work)

    SAFE_ALLOCATE(rho(1:this%der%mesh%np))
    done = .false.
    
    do while(.not. done)

      call MPI_Recv(rho(1), this%der%mesh%np, MPI_FLOAT, MPI_ANY_SOURCE, MPI_ANY_TAG, this%intercomm, status(1), mpi_err)
      if(status(MPI_TAG) == CMD_FINISH) done = .true.

    end do

    POP_SUB(poisson_slave_work)
#endif
  end subroutine poisson_slave_work

#include "solver_1d_inc.F90"
#include "solver_2d_inc.F90"
#include "solver_3d_inc.F90"

end module poisson_m

!! Local Variables:
!! mode: f90
!! coding: utf-8
!! End:
