!! Copyright (C) 2002-2011 M. Marques, A. Castro, A. Rubio, G. Bertsch, M. Oliveira
!!
!! This program is free software; you can redistribute it and/or modify
!! it under the terms of the GNU General Public License as published by
!! the Free Software Foundation; either version 2, or (at your option)
!! any later version.
!!
!! This program is distributed in the hope that it will be useful,
!! but WITHOUT ANY WARRANTY; without even the implied warranty of
!! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!! GNU General Public License for more details.
!!
!! You should have received a copy of the GNU General Public License
!! along with this program; if not, write to the Free Software
!! Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
!! 02111-1307, USA.
!!
!! $Id: solver_1d_inc.F90 9854 2013-01-19 23:28:12Z dstrubbe $

subroutine X(poisson1D_solve)(this, pot, rho, theta)
  type(poisson_t), intent(in)  :: this
  R_TYPE,          intent(out) :: pot(:)
  R_TYPE,          intent(in)  :: rho(:)
  FLOAT, optional, intent(in)  :: theta !< Complex scaling angle
  ! Passing theta is not needed for a normal Poisson solver due to linearity,
  ! which makes it possible to solve separately for real/imaginary parts.
  ! But the soft-Coulomb kernel makes it necessary.
  !
  ! Note that we don`t divide by e^(i theta) here, we do it "outside" as with the other Poisson solvers.

  integer             :: ip, jp
  FLOAT               :: xx, yy
  logical             :: cmplxscl
  R_TYPE              :: soft_coulomb_param_squared
#ifdef HAVE_MPI
  R_TYPE              :: tmp
  FLOAT               :: xg(1:MAX_DIM)
  R_TYPE, allocatable :: pvec(:)
#endif

  ASSERT(this%method == -1)

  PUSH_SUB(X(poisson1D_solve))

  soft_coulomb_param_squared = this%poisson_soft_coulomb_param**2
  if(present(theta)) then
    ! This will discard imaginary part for R_TYPE real.
    ! But theta won`t be there unless we already use complex scaling, so only cmplx is relevant.
    soft_coulomb_param_squared = soft_coulomb_param_squared * exp(-M_TWO * M_zI * theta)
  end if

#ifdef HAVE_MPI
  if(this%der%mesh%parallel_in_domains) then
    SAFE_ALLOCATE(pvec(1:this%der%mesh%np))

    pot = M_ZERO
    do ip = 1, this%der%mesh%np_global
      xg = mesh_x_global(this%der%mesh, ip)
      xx = xg(1)
      do jp = 1, this%der%mesh%np
        yy = this%der%mesh%x(jp, 1)
        pvec(jp) = rho(jp)/sqrt(soft_coulomb_param_squared + (xx - yy)**2)
      end do
      tmp = X(mf_integrate)(this%der%mesh, pvec)
      if (this%der%mesh%vp%part(ip).eq.this%der%mesh%vp%partno) then
        pot(vec_global2local(this%der%mesh%vp, ip, this%der%mesh%vp%partno)) = tmp
      end if
    end do

    SAFE_DEALLOCATE_A(pvec)

  else  ! running in serial
#endif
    pot = M_ZERO
    do ip = 1, this%der%mesh%np
      xx = this%der%mesh%x(ip, 1)
      do jp = 1, this%der%mesh%np
        yy = this%der%mesh%x(jp, 1)
        if(this%der%mesh%use_curvilinear) then
          pot(ip) = pot(ip) + rho(jp)/sqrt(soft_coulomb_param_squared + (xx - yy)**2)*this%der%mesh%vol_pp(jp)
        else
          pot(ip) = pot(ip) + rho(jp)/sqrt(soft_coulomb_param_squared + (xx - yy)**2)
        endif
      end do
    end do
    if(.not. this%der%mesh%use_curvilinear) pot(:) = pot(:) * this%der%mesh%volume_element
#ifdef HAVE_MPI
  end if
#endif

  POP_SUB(X(poisson1D_solve))
end subroutine X(poisson1D_solve)

!! Local Variables:
!! mode: f90
!! coding: utf-8
!! End:
