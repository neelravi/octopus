!! Copyright (C) 2002-2006 M. Marques, A. Castro, A. Rubio, G. Bertsch
!!
!! This program is free software; you can redistribute it and/or modify
!! it under the terms of the GNU General Public License as published by
!! the Free Software Foundation; either version 2, or (at your option)
!! any later version.
!!
!! This program is distributed in the hope that it will be useful,
!! but WITHOUT ANY WARRANTY; without even the implied warranty of
!! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!! GNU General Public License for more details.
!!
!! You should have received a copy of the GNU General Public License
!! along with this program; if not, write to the Free Software
!! Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
!! 02111-1307, USA.
!!
!! $Id$

#include "global.h"

module specie_m
  use datasets_m
  use global_m
  use io_m
  use lib_oct_gsl_spline_m
  use lib_oct_m
  use lib_oct_parser_m
  use math_m
  use messages_m
  use mpi_m
  use ps_m
  use string_m
  use units_m

  implicit none

  private
  public ::                   &
    specie_init,              &
    specie_end,               &
    specie_t,                 &
    specie_debug,             &
    specie_filter,            &
    specie_read,              &
    specie_get_local_fourier, &
    specie_get_nlcc,          &
    specie_get_iwf,           &
    specie_is_ps,             &
    specie_is_local,          &
    specie_real_nl_projector, &
    specie_nl_projector


  integer, public, parameter :: &
    SPEC_USDEF  = 123,          & ! user defined function
    SPEC_POINT  = 2,            & ! point charge: jellium sphere of radius 0.5 a.u.
    SPEC_JELLI  = 3,            & ! jellium sphere.
    SPEC_ALL_E  = 124,          & ! All electron atom
    SPEC_PS_PSF = PS_TYPE_PSF,  & ! SIESTA pseudopotential
    SPEC_PS_HGH = PS_TYPE_HGH,  & ! HGH pseudopotential
    SPEC_PS_CPI = PS_TYPE_CPI,  & ! FHI pseudopotential (cpi format)
    SPEC_PS_FHI = PS_TYPE_FHI,  & ! FHI pseudopotential (ABINIT format)
    SPEC_PS_UPF = PS_TYPE_UPF     ! UPF pseudopotential

  type specie_t
    integer :: index              ! just a counter

    character(len=15) :: label    ! Identifier for the species
    integer :: type               ! what type of species
    FLOAT   :: z                  ! charge of the species
    FLOAT   :: z_val              ! valence charge of the species -- the total charge
                                  ! minus the core charge in the case of the pseudopotentials
    FLOAT   :: weight             ! mass, in atomic mass units (!= atomic units of mass)

    logical :: has_density        ! true if the specie has a electric density 
    
    ! for the user defined potential
    character(len=1024) :: user_def
    FLOAT :: omega

    ! jellium stuff
    FLOAT :: jradius

    ! For the pseudopotential
    type(ps_t), pointer :: ps
    logical             :: nlcc       ! true if we have non-local core corrections

    ! If we have an all-electron atom:
    FLOAT :: sigma

    ! the default values for the spacing and atomic radius
    FLOAT :: def_rsize, def_h

    ! The number of initial wave functions
    integer :: niwfs
    integer, pointer :: iwf_l(:, :), iwf_m(:, :), iwf_i(:, :)

    ! For the TM pseudos, the lmax and lloc.
    integer :: lmax, lloc

    ! The filtering parameters.
    FLOAT :: alpha, beta, rcut, beta2

  end type specie_t


contains

  ! ---------------------------------------------------------
  subroutine specie_debug(dir, s)
    character(len=*), intent(in)  :: dir
    type(specie_t),    intent(in) :: s

    character(len=256) :: dirname
    integer :: iunit

    if(.not.mpi_grp_is_root(mpi_world)) then
      call write_debug_newlines(4)
      return
    end if

    call push_sub('specie.specie_debug')

    dirname = trim(dir)//'/'//trim(s%label)

    call io_mkdir(dirname)

    iunit = io_open(trim(dirname)//'/info', action='write')

    write(iunit, '(a,i3)')    'Index  = ', s%index
    write(iunit, '(2a)')      'Label  = ', trim(s%label)
    write(iunit, '(a,i3)')    'Type   = ', s%type
    if (s%type /= SPEC_USDEF ) write(iunit, '(a,f15.2)') 'z      = ', s%z
    write(iunit, '(a,f15.2)') 'z_val  = ', s%z_val
    write(iunit, '(a,f15.2)') 'weight = ', s%weight
    write(iunit, *)           'local  = ', specie_is_local(s)
    write(iunit, '(2a)')      'usdef  = ', trim(s%user_def)
    if (s%type == SPEC_JELLI .or. s%type == SPEC_POINT) then
      write(iunit, '(a,f15.2)') 'jradius= ', s%jradius
    end if
    write(iunit, *)           'nlcc   = ', s%nlcc
    write(iunit, '(a,f15.2)') 'def_rsize = ', s%def_rsize
    write(iunit, '(a,f15.2)') 'def_h = ', s%def_h
    if (s%type /= SPEC_USDEF ) write(iunit, '(a,i3)')    'lmax  = ', s%lmax
    if (s%type /= SPEC_USDEF ) write(iunit, '(a,i3)')    'lloc  = ', s%lloc

    if(specie_is_ps(s)) then
       if(in_debug_mode) call ps_debug(s%ps, trim(dirname))
    end if

    call io_close(iunit)
    call pop_sub()
  end subroutine specie_debug


  ! ---------------------------------------------------------
  subroutine specie_filter(s, gmax)
    type(specie_t), intent(inout) :: s
    FLOAT,             intent(in) :: gmax

    call push_sub('specie.specie_filter')

    call ps_filter(s%ps, gmax, s%alpha, s%beta, s%rcut, s%beta2)
    call ps_getradius(s%ps)
    call ps_derivatives(s%ps)

    call pop_sub()

  end subroutine specie_filter


  ! ---------------------------------------------------------
  subroutine specie_read(s, label)
    type(specie_t), intent(inout) :: s
    character(len=*),  intent(in) :: label

    character(len=256) :: fname
    character(len=10)  :: lab
    integer :: i, row, n_spec_block, n_spec_def, iunit, read_data
    C_POINTER :: blk

    call push_sub('specie.specie_read')

    s%has_density = .false. !there is no density associated
    s%nlcc      = .false. ! without non-local core corrections
    s%def_h     = -M_ONE  ! not defined
    s%def_rsize = -M_ONE  ! not defined
    s%user_def  = ""
    read_data   = 0

#if 0
    !%Variable Species
    !%Type block
    !%Section System::Species
    !%Description
    !% A specie is by definition either an "ion" (nucleus + core electrons) described
    !% through a pseudo-potential, or an user-defined, model potential.
    !%
    !% Note that some common pseudopotentials are distributed with the code in the
    !% directory <i>OCTOPUS-HOME/share/PP/</i>. To use these pseudopotentials you are
    !% not required to define them explicitly in the Species block, as defaults 
    !% are provided by the program (you can override these defaults in any case). 
    !% Additional pseudopotentials can be downloaded from the 
    !% <a href='http://www.tddft.org/programs/octopus/wiki/index.php/Pseudopotentials'>
    !% octopus homepage</a>.
    !%
    !% The format of this block is the following: The first field is
    !% the name of the specie, followed by the atomic mass (in atomic mass
    !% units). The third field defines the type of specie (the valid options
    !% are detailed below), each type needs some extra parameters given in
    !% the following fields of the row.
    !%
    !% In 3D, e.g.
    !%
    !% <tt>%Species
    !% <br>&nbsp;&nbsp;'O'       | 15.9994 | spec_ps_psf  | 8   | 1 | 1
    !% <br>&nbsp;&nbsp;'H'       |  1.0079 | spec_ps_hgh  | 1   | 0 | 0
    !% <br>&nbsp;&nbsp;'jlm'     | 23.2    | spec_jelli   | 8   | 5.0
    !% <br>&nbsp;&nbsp;'pnt'     | 32.3    | spec_point   | 2.0
    !% <br>&nbsp;&nbsp;'udf'     |  0.0    | user_defined | 8   | "1/2*r^2"
    !% <br>&nbsp;&nbsp;'H_all'   |  1.0079 | spec_all_e   | 1   
    !% <br>%</tt>
    !%
    !%
    !%Option user_defined  123
    !% Specie with user defined potential. In this case, the fourth
    !% field is the valence charge and the fifth
    !% field is a string with a mathematical expression that defines the
    !% potential (you can use any of the <i>x</i>, <i>y</i>, <i>z</i>
    !% or <i>r</i> variables).
    !%Option spec_point  2
    !% Point charge, the fourth field is the value of the charge.
    !%Option spec_jelli  3
    !% Jellium sphere, the extra parameters are the charge of the jellium
    !% sphere (an equal value of valence charge is assumed) and the radius of
    !% the sphere.
    !%Option spec_ps_psf  100
    !% Troullier Martins pseudopotential in SIESTA format, the pseudopotential will be
    !% read from an <i>.psf</i> file, either in the working
    !% directory or in the <i>OCTOPUS-HOME/share/octopus/PP/PSF</i> directory.
    !% The following three numbers are the atomic number, the maximum
    !% <i>l</i>-component of the pseudo-potential to consider in the
    !% calculation, and the <i>l</i>-component to consider as local.
    !%Option spec_ps_hgh  101
    !% Hartwigsen-Goedecker-Hutter pseudopotentials, the next field is
    !% the atomic number an the last two numbers are irrelevant, since they
    !% do are not necessary to define the HGH pseudopotentials.
    !%Option spec_ps_cpi  102
    !% Fritz-Haber pseudopotential, the pseudopotential will be
    !% read from an <i>.cpi</i> file, either in the working
    !% directory or in the <i>OCTOPUS-HOME/share/PP/CPI</i> directory.
    !% The following three numbers are the atomic number, the maximum
    !% <i>l</i>-component of the pseudo-potential to consider in the
    !% calculation, and the <i>l</i>-component to consider as local.
    !%Option spec_ps_fhi  103
    !% Fritz-Haber pseudopotential (ABINIT format), the pseudopotential will be
    !% read from an <i>.fhi</i> file, either in the working
    !% directory or in the <i>OCTOPUS-HOME/share/PP/FHI</i> directory.
    !% The following three numbers are the atomic number, the maximum
    !% <i>l</i>-component of the pseudo-potential to consider in the
    !% calculation, and the <i>l</i>-component to consider as local.
    !% Note that you can use the pseudopotentials from ABINT homepage
    !%Option spec_ps_upf  104
    !% UPF format, the pseudopotential will be
    !% read from an <i>.UPF</i> file, either in the working
    !% directory or in the <i>OCTOPUS-HOME/share/PP/UPF</i> directory.
    !% The following three numbers are the atomic number, the maximum
    !% <i>l</i>-component of the pseudo-potential to consider in the
    !% calculation, and the <i>l</i>-component to consider as local.
    !%Option spec_all_e   124
    !% Atom represented with all electrons, the extra parameter is the
    !% atomic number. See the documentation of the variable 
    !% SpecieAllElectronSigma.
    !% 
    !% WARNING: Currently you can not use LCAO with this specie.
    !%End
#endif 

    !%Variable SpecieAllElectronSigma
    !%Type float
    !%Default 0.2
    !%Section System::Species
    !%Description
    !% An all-electron atom is defined by a Gaussian accumulation of positive charge 
    !% (distorted if curvilinear coordinates are used), in the form:
    !%
    !% <math>
    !% q(r) = z * \beta * exp[ - (\vec{r}-\vec{r0})**2 / (sqrt(2) * \delta * \sigma) ]
    !% </math>
    !%
    !% <math>\beta</math> is chosen in order to maintain proper normalization (the integral of
    !% <math>q</math> should sum up to <math>z</math>). <math>\delta</math> is 
    !% the grid spacing (the grid spacing in the first dimension, to be precise). 
    !% <math>\vec{r0}</math> is calculated in such a way that the the first moment of 
    !% <math>q(r)/z</math> is equal to the atomic position.
    !%
    !% For a precise description, see [N. A. Modine, Phys. Rev. B 55, 10289 (1997)]
    !%End
    call loct_parse_float(check_inp('SpecieAllElectronSigma'), CNST(0.2), s%sigma)
    if(s%sigma <= M_ZERO) call input_error('SpecieAllElectronSigma')

    ! First, find out if there is a Species block.
    n_spec_block = 0
    blk = int(0, POINTER_SIZE)
    if(loct_parse_block(check_inp('Species'), blk) == 0) then
      n_spec_block = loct_parse_block_n(blk)
    end if

    ! Find out if the seeked species is in the block
    row = -1
    block: do i = 1, n_spec_block
      call loct_parse_block_string(blk, i-1, 0, lab)
      if(trim(lab)==trim(label)) then
        row = i - 1
        exit block
      end if
    end do block

    ! Read whatever may be read from the block
    if(row>=0) then
      call read_from_block(blk, row, s, read_data)
      call loct_parse_block_end(blk)
    end if

    ! Find out if the species is in the defaults file.
    write(fname, '(2a)') trim(conf%share), "/PP/defaults"
    n_spec_def = max(0, loct_number_of_lines(fname))
    if(n_spec_def > 0) n_spec_def = n_spec_def - 1 ! First line is a comment

    iunit = io_open(fname, action='read', status='old', die=.false.)
    if(iunit > 0) then
      read(iunit,*)

      default_file: do i = 1, n_spec_def
        read(iunit,*) lab
        if(trim(lab) == trim(label)) then
          call read_from_default_file(iunit, read_data, s)
          exit default_file
        end if
      end do default_file

      call io_close(iunit)
    end if

    if(read_data == 0) then
      message(1) = 'Species '//trim(label)//' not found.'
      call write_fatal(1)
    end if

    call pop_sub()
  end subroutine specie_read


  ! ---------------------------------------------------------
  subroutine read_from_default_file(iunit, read_data, s)
    integer,        intent(in)    :: iunit
    integer,        intent(inout) :: read_data
    type(specie_t), intent(inout) :: s

    character(len=10) :: label
    FLOAT :: weight, z, def_h, def_rsize, alpha, beta, rcut, beta2
    integer :: lloc, lmax
    integer :: type

    call push_sub('specie.read_from_default_file')

    backspace(iunit)

    read(iunit,*) label, weight, type, z, lmax, lloc, def_h, def_rsize, alpha, beta, rcut, beta2
    def_h     = def_h     * P_ANG  ! These units are always in Angstrom
    def_rsize = def_rsize * P_ANG
    ASSERT(trim(label) == trim(s%label))

    if(read_data == 0) then ! The Species was not supplied in the block.
      s%weight    = weight
      s%type      = type
    end if
    if(read_data < 4) s%z         = z
    if(read_data < 5) s%lmax      = lmax
    if(read_data < 6) s%lloc      = lloc
    if(read_data < 7) s%def_h     = def_h
    if(read_data < 8) s%def_rsize = def_rsize
    if(read_data < 9) then
      s%alpha     = alpha
      s%beta      = beta
      s%rcut      = rcut
      s%beta2     = beta2
    end if
    read_data = 8

    call pop_sub()
  end subroutine read_from_default_file


  ! ---------------------------------------------------------
  subroutine read_from_block(blk, row, s, read_data)
    C_POINTER,      intent(in) :: blk
    integer,        intent(in) :: row
    type(specie_t), intent(inout) :: s
    integer,        intent(out) :: read_data

    integer :: n, lmax, lloc

    call push_sub('specie.read_from_block')

    read_data = 0

    call loct_parse_block_float (blk, row, 1, s%weight)
    call loct_parse_block_int   (blk, row, 2, s%type)

    select case(s%type)
    case(SPEC_USDEF) ! user defined
      call loct_parse_block_float (blk, row, 3, s%Z_val)
      call loct_parse_block_string(blk, row, 4, s%user_def)
      call conv_to_C_string(s%user_def)
      read_data = 5

    case(SPEC_POINT) ! this is treated as a jellium with radius 0.5
      call loct_parse_block_float(blk, row, 3, s%Z)
      s%jradius = M_HALF
      s%Z_val = 0
      read_data = 4

    case(SPEC_JELLI)
      call loct_parse_block_float(blk, row, 3, s%Z)      ! charge of the jellium sphere
      call loct_parse_block_float(blk, row, 4, s%jradius)! radius of the jellium sphere
      s%jradius = units_inp%length%factor * s%jradius    ! units conversion
      s%Z_val = s%Z
      read_data = 5

    case(SPEC_ALL_E)
      call loct_parse_block_float(blk, row, 3, s%Z)
      s%Z_val = s%Z
      read_data = 4

    case(SPEC_PS_PSF, SPEC_PS_HGH, SPEC_PS_CPI, SPEC_PS_FHI, SPEC_PS_UPF) ! a pseudopotential file
      n = loct_parse_block_cols(blk, row)

      call loct_parse_block_float (blk, row, 3, s%Z)
      lmax = 2 ! default
      if(n>4) then
        call loct_parse_block_int (blk, row, 4, s%lmax)
        read_data = 5
      end if
      lloc = 0 ! default
      if(n>5) then
        call loct_parse_block_int (blk, row, 5, s%lloc)
        read_data = 6
      end if

      if(n>6) then
        call loct_parse_block_float (blk, row, 6, s%def_h)
        s%def_h = s%def_h * units_inp%length%factor
        read_data = 7
      end if

      if(n>7) then
        call loct_parse_block_float (blk, row, 7, s%def_rsize)
        s%def_rsize = s%def_rsize * units_inp%length%factor
        read_data = 8
      end if

      if(n>8) then
        ! For the moment being, let us assume that this is always in atomic units.
        call loct_parse_block_float (blk, row, 8, s%alpha)
        call loct_parse_block_float (blk, row, 9, s%beta)
        call loct_parse_block_float (blk, row, 10, s%rcut)
        call loct_parse_block_float (blk, row, 11, s%beta2)
        read_data = 10
      else
        ! Reasonable defaults
        s%alpha = CNST(0.7); s%beta = CNST(18.0); s%rcut = CNST(2.5); s%beta2 = CNST(0.4)
      end if

    case default
      write(message(1), '(a,i2,a)') "Unknown pseudopotential type: '", s%type, "'"
      call write_fatal(1)
    end select

    call pop_sub()
  end subroutine read_from_block


  ! ---------------------------------------------------------
  subroutine specie_init(s, ispin)
    type(specie_t), intent(inout) :: s
    integer,        intent(in)    :: ispin

    integer :: i, l
    FLOAT   :: pot_re, pot_im

    call push_sub('specie.specie_init')

    ! masses are always in a.u.m, so convert them to a.u.
    s%weight =  units_inp%mass%factor * s%weight

    s%has_density = .false.

    select case(s%type)
    case(SPEC_PS_PSF, SPEC_PS_HGH, SPEC_PS_CPI, SPEC_PS_FHI, SPEC_PS_UPF)
      ! allocate structure
      ALLOCATE(s%ps, 1) 
      call ps_init(s%ps, s%label, s%type, s%Z, s%lmax, s%lloc, ispin)
      call ps_getradius(s%ps)
      call ps_derivatives(s%ps)
      s%z_val = s%ps%z_val
      s%nlcc = (s%ps%icore /= 'nc  ' )
      s%niwfs = 0
      do i = 1, s%ps%conf%p
        l = s%ps%conf%l(i)
        ! The next commented line assumed that we only want the shells 
        ! that had some occupation. I am not sure of what is best, for 
        ! the moment I will assume that we will use all the shells, so 
        ! that the LCAO basis will be larger. 
        ! if(sum(s%ps%conf%occ(i, :)).ne.M_ZERO) s%niwfs = s%niwfs + (2*l+1)
        s%niwfs = s%niwfs + (2*l+1)
      end do

    case(SPEC_USDEF)
      write(message(1),'(a,a,a)')    'Specie "',trim(s%label),'" is an user-defined potential.'
      write(message(2),'(a,a)')      '   Potential = ', trim(s%user_def(1:240)) ! 256 - 16, 16 for "Potential ="
      call write_info(2)
      s%niwfs = 2*s%z_val
      call loct_parse_expression(pot_re, pot_im, CNST(0.01), M_ZERO,   &
        M_ZERO, CNST(0.01), M_ZERO, s%user_def)
      s%omega = sqrt( abs(M_TWO / CNST(1.0e-4) * pot_re ))
      ! To avoid problems with constant potentials.
      if(s%omega <= M_ZERO) s%omega = CNST(0.1) 
    case(SPEC_JELLI, SPEC_POINT)
      write(message(1),'(a,a,a)')    'Specie "',trim(s%label),'" is a jellium sphere / approximated point particle.'
      write(message(2),'(a,f11.6)')  '   Valence charge = ', s%z_val
      write(message(3),'(a,f11.6)')  '   Radius [a.u]   = ', s%jradius
      write(message(4),'(a,f11.6)')  '   Rs [a.u]       = ', s%jradius * s%z_val ** (-M_ONE/M_THREE)
      call write_info(4)
      s%niwfs = 2*s%z_val
      s%omega = CNST(0.1)

    case(SPEC_ALL_E)
      s%niwfs = 2*s%z_val
      s%has_density = .true.
      write(message(1),'(a,a,a)')    'Specie "',trim(s%label),'" is an all electron atom.'
      write(message(2),'(a,f11.6)')  '   Z = ', s%z_val
      write(message(3),'(a)')  '   Potential will be calulated solving poisson equation'
      write(message(3),'(a)')  '   Potential will be calulated solving poisson equation'
      write(message(4),'(a)')  '   for a delta density distribution.'
      call write_info(4)

      write(message(1),'(a)')    'Gradients are not calculated, forces are not correct.'
      call write_warning(1)
      
      write(message(1),'(a)')    'If octopus stops with error, turnoff LCAO (LCAOStart = no).'
      call write_warning(1)
    end select

    ALLOCATE(s%iwf_l(s%niwfs, ispin), s%niwfs*ispin)
    ALLOCATE(s%iwf_m(s%niwfs, ispin), s%niwfs*ispin)
    ALLOCATE(s%iwf_i(s%niwfs, ispin), s%niwfs*ispin)
    call specie_iwf_fix_qn(s, ispin)

    call pop_sub()
  end subroutine specie_init


  ! ---------------------------------------------------------
  subroutine specie_end(ns, s)
    integer,        intent(in) :: ns
    type(specie_t), pointer    :: s(:)

    integer :: i

    call push_sub('specie.specie_end')

    do i = 1, ns
      if (specie_is_ps(s(i))) then 
        if(associated(s(i)%ps)) call ps_end(s(i)%ps)
      end if
    end do

    if(associated(s)) then ! sanity check
      deallocate(s); nullify(s)
    end if

    call pop_sub()
  end subroutine specie_end


  ! ---------------------------------------------------------
  ! returns the localized part of the potential in Fourier space
  ! ---------------------------------------------------------
  FLOAT function specie_get_local_fourier(dim, s, x) result(l)
    integer,        intent(in) :: dim
    type(specie_t), intent(in) :: s
    FLOAT,          intent(in) :: x(MAX_DIM)

    FLOAT :: gmod

    if(dim /= 3 &
         .or. s%type == SPEC_USDEF &
         .or. s%type == SPEC_POINT &
         .or. s%type == SPEC_JELLI &
         .or. s%type == SPEC_ALL_E &
         ) then
      message(1) = 'Periodic arrays of usedef, jelli, point,'
      message(2) = '1D and 2D systems not implemented yet.'
      call write_fatal(2)
      l = M_ZERO
    else
      gmod = sqrt(sum(x(:)**2))
      l = loct_splint(s%ps%vlocal_f, gmod)
    end if

  end function specie_get_local_fourier

  ! ---------------------------------------------------------
  ! This routine returns the non-local projector and its 
  ! derivative build using real spherical harmonics
  subroutine specie_real_nl_projector(s, x_atom, x_grid, l, lm, i, uV, duV)
    type(specie_t),    intent(in)  :: s
    FLOAT,             intent(in)  :: x_atom(1:MAX_DIM)
    FLOAT,             intent(in)  :: x_grid(1:MAX_DIM)
    integer,           intent(in)  :: l, lm, i
    FLOAT,             intent(out) :: uV, duV(1:MAX_DIM)

    FLOAT :: r, uVr0, duvr0, ylm, gylm(MAX_DIM), x(MAX_DIM)
    FLOAT, parameter :: ylmconst = CNST(0.488602511902920) !  = sqr(3/(4*pi))

    x(1:MAX_DIM) = x_grid(1:MAX_DIM) - x_atom(1:MAX_DIM)

    r = sqrt(sum(x(1:MAX_DIM)**2))

    uVr0  = loct_splint(s%ps%kb(l, i), r)
    duVr0 = loct_splint(s%ps%dkb(l, i), r)

    call grylmr(x(1), x(2), x(3), l, lm, ylm, gylm)
    uv = uvr0*ylm
    if(r >= r_small) then
      duv(:) = duvr0 * ylm * x(:)/r + uvr0 * gylm(:)
    else
      if(l == 1) then
        duv = M_ZERO
        if(lm == -1) then
          duv(2) = -ylmconst * duvr0
        else if(lm == 0) then
          duv(3) =  ylmconst * duvr0
        else if(lm == 1) then
          duv(1) = -ylmconst * duvr0
        end if
      else
        duv = M_ZERO
      end if
    end if

  end subroutine specie_real_nl_projector

  ! ---------------------------------------------------------
  ! This routine returns the non-local projector build using 
  ! spherical harmonics
  subroutine specie_nl_projector(s, x_atom, x_grid, l, lm, i, uV)
    type(specie_t),    intent(in)  :: s
    FLOAT,             intent(in)  :: x_atom(1:MAX_DIM)
    FLOAT,             intent(in)  :: x_grid(1:MAX_DIM)
    integer,           intent(in)  :: l, lm, i
    CMPLX,             intent(out) :: uV

    FLOAT :: r, uVr0, x(MAX_DIM)
    CMPLX :: ylm

    x(1:MAX_DIM) = x_grid(1:MAX_DIM) - x_atom(1:MAX_DIM)

    r = sqrt(sum(x(1:MAX_DIM)**2))

    uVr0 = loct_splint(s%ps%kb(l, i), r)

    call ylmr(x(1), x(2), x(3), l, lm, ylm)
    uv = uvr0*ylm

  end subroutine specie_nl_projector

  ! ---------------------------------------------------------
  FLOAT function specie_get_nlcc(s, x) result(l)
    type(specie_t), intent(in) :: s
    FLOAT, intent(in) :: x(MAX_DIM)

    ! only for 3D pseudopotentials, please
    if(s%type==SPEC_PS_PSF.or.s%type==SPEC_PS_HGH.or.s%type==SPEC_PS_CPI.or.s%type==SPEC_PS_FHI.or.s%type==SPEC_PS_UPF) then
      l = loct_splint(s%ps%core, sqrt(sum(x**2)))
    else
      l = M_ZERO
    end if

  end function specie_get_nlcc

  ! ---------------------------------------------------------
  FLOAT function specie_get_iwf(s, j, dim, is, x) result(phi)
    type(specie_t), intent(in) :: s
    integer,           intent(in) :: j
    integer,           intent(in) :: dim
    integer,           intent(in) :: is
    FLOAT,             intent(in) :: x(dim)

    integer :: i, l, m
    FLOAT :: r2

    i = s%iwf_i(j, is)
    l = s%iwf_l(j, is)
    m = s%iwf_m(j, is)
    r2 = sum(x*x)

    if(specie_is_ps(s)) then
      phi =  loct_splint(s%ps%ur(i, is), sqrt(r2)) * loct_ylm(x(1), x(2), x(3), l, m)
    else
      phi=M_ZERO
      select case(dim)
      case(1)
        phi = exp(-s%omega*r2/M_TWO) * hermite(i-1, x(1)*sqrt(s%omega) )
      case(2)
        phi = exp(-s%omega*r2/M_TWO) * hermite(i-1, x(1)*sqrt(s%omega) ) * &
          hermite(l-1, x(2)*sqrt(s%omega) )
      case(3)
        phi = exp(-s%omega*r2/M_TWO) * hermite(i-1, x(1)*sqrt(s%omega) ) * &
          hermite(l-1, x(2)*sqrt(s%omega) ) * &
          hermite(m-1, x(3)*sqrt(s%omega) )
      end select
    end if

  end function specie_get_iwf


  ! ---------------------------------------------------------
  subroutine specie_iwf_fix_qn(s, ispin)
    type(specie_t), intent(inout) :: s
    integer, intent(in) :: ispin

    integer :: is, n, i, l, m, n1, n2, n3

    call push_sub('specie.specie_iwf_fix_qn')

    if(specie_is_ps(s)) then
      do is = 1, ispin
        n = 1
        do i = 1, s%ps%conf%p
          l = s%ps%conf%l(i)
          do m = -l, l
            s%iwf_i(n, is) = i
            s%iwf_l(n, is) = l
            s%iwf_m(n, is) = m
            n = n + 1
          end do
        end do
      end do
    else
      select case(calc_dim)
      case(1)
        do is = 1, ispin
          do i = 1, s%niwfs
            s%iwf_i(i, is) = i
            s%iwf_l(i, is) = 0
            s%iwf_m(i, is) = 0
          end do
        end do
      case(2)
        do is = 1, ispin
          i = 1; n1 = 1; n2 = 1
          do
            s%iwf_i(i, is) = n1
            s%iwf_l(i, is) = n2
            s%iwf_m(i, is) = 0
            i = i + 1; if(i>s%niwfs) exit
            s%iwf_i(i, is) = n1+1
            s%iwf_l(i, is) = n2
            s%iwf_m(i, is) = 0
            i = i + 1; if(i>s%niwfs) exit
            s%iwf_i(i, is) = n1
            s%iwf_l(i, is) = n2+1
            s%iwf_m(i, is) = 0
            i = i + 1; if(i>s%niwfs) exit
            n1 = n1 + 1; n2 = n2 + 1
          end do
        end do
      case(3)
        do is = 1, ispin
          i = 1; n1 = 1; n2 = 1; n3 = 1
          do
            s%iwf_i(i, is) = n1
            s%iwf_l(i, is) = n2
            s%iwf_m(i, is) = n3
            i = i + 1; if(i>s%niwfs) exit
            s%iwf_i(i, is) = n1+1
            s%iwf_l(i, is) = n2
            s%iwf_m(i, is) = n3
            i = i + 1; if(i>s%niwfs) exit
            s%iwf_i(i, is) = n1
            s%iwf_l(i, is) = n2+1
            s%iwf_m(i, is) = 0
            i = i + 1; if(i>s%niwfs) exit
            s%iwf_i(i, is) = n1
            s%iwf_l(i, is) = n2
            s%iwf_m(i, is) = n3+1
            i = i + 1; if(i>s%niwfs) exit
            s%iwf_i(i, is) = n1+1
            s%iwf_l(i, is) = n2+1
            s%iwf_m(i, is) = n3
            i = i + 1; if(i>s%niwfs) exit
            s%iwf_i(i, is) = n1+1
            s%iwf_l(i, is) = n2
            s%iwf_m(i, is) = n3+1
            i = i + 1; if(i>s%niwfs) exit
            s%iwf_i(i, is) = n1
            s%iwf_l(i, is) = n2+1
            s%iwf_m(i, is) = n3+1
            i = i + 1; if(i>s%niwfs) exit
            n1 = n1 + 1; n2 = n2 + 1; n3 = n3 + 1
          end do
        end do
      end select
    end if

    call pop_sub()
  end subroutine specie_iwf_fix_qn

  logical function specie_is_ps(s)
    type(specie_t), intent(in) :: s
    
    specie_is_ps = &
         ( s%type == SPEC_PS_PSF) .or. &
         ( s%type == SPEC_PS_HGH) .or. &
         ( s%type == SPEC_PS_CPI) .or. &
         ( s%type == SPEC_PS_FHI) .or. &
         ( s%type == SPEC_PS_UPF)
    
  end function specie_is_ps

  logical function specie_is_local(s)
    type(specie_t), intent(in) :: s

    specie_is_local = .true.
      
    if( specie_is_ps(s) ) then 
      if ( s%ps%l_max /= 0 ) specie_is_local = .false. 
    end if

  end function specie_is_local

end module specie_m

!! Local Variables:
!! mode: f90
!! coding: utf-8
!! End:
