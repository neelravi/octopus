!! Copyright (C) 2002-2006 M. Marques, A. Castro, A. Rubio, G. Bertsch
!!
!! This program is free software; you can redistribute it and/or modify
!! it under the terms of the GNU General Public License as published by
!! the Free Software Foundation; either version 2, or (at your option)
!! any later version.
!!
!! This program is distributed in the hope that it will be useful,
!! but WITHOUT ANY WARRANTY; without even the implied warranty of
!! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!! GNU General Public License for more details.
!!
!! You should have received a copy of the GNU General Public License
!! along with this program; if not, write to the Free Software
!! Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
!! 02111-1307, USA.
!!
!! $Id: td.F90 3694 2008-02-15 13:37:54Z marques $

#include "global.h"
  
module cpmd_m
  use global_m
  use io_m
  use datasets_m
  use io_function_m
  use lalg_basic_m
  use loct_math_m
  use loct_parser_m
  use units_m
  use math_m
  use messages_m
  use mesh_m
  use external_pot_m
  use geometry_m
  use hamiltonian_m
  use loct_m
  use profiling_m
  use restart_m
  use scf_m
  use states_m
  use states_block_m
  use system_m
  use v_ks_m
  use grid_m

  implicit none

  private
  public ::                 &
    cpmd_t,                 &
    cpmd_init,              &
    cpmd_end,               &
    cpmd_electronic_energy, &
    cpmd_restart_read,      &
    cpmd_restart_write,     &
    dcpmd_propagate,        & 
    zcpmd_propagate

  type cpmd_t
    private
    FLOAT          :: emass
    FLOAT          :: ecorr
    FLOAT, pointer :: doldpsi(:, :, :, :)
    CMPLX, pointer :: zoldpsi(:, :, :, :)
  end type cpmd_t

  type(profile_t), save :: cpmd_prop, cpmd_orth

contains

  subroutine cpmd_init(this, gr, st)
    type(cpmd_t),        intent(out)   :: this
    type(grid_t),        intent(inout) :: gr
    type(states_t),      intent(inout) :: st
    
    integer :: size

    call push_sub('cpmd.cpmd_init')

    !%Variable CPElectronicMass
    !%Type float
    !%Default 1.0
    !%Section Time Dependent::Propagation
    !%Description
    !% The fictious electronic mass used to propagate the electronic
    !% wavefunctions in the Carr-Parrinelo formalism.
    !%End
    
    call loct_parse_float(check_inp('CPElectronicMass'), CNST(1.0), this%emass)

    nullify(this%doldpsi, this%zoldpsi)

    size = gr%m%np_part * st%d%dim * st%lnst * st%d%nik

    if(wfs_are_real(st)) then
      ALLOCATE(this%doldpsi(gr%m%np_part, st%d%dim, st%st_start:st%st_end, st%d%nik), size)
    else
      ALLOCATE(this%zoldpsi(gr%m%np_part, st%d%dim, st%st_start:st%st_end, st%d%nik), size)
    end if

    call pop_sub()

  end subroutine cpmd_init
  
  subroutine cpmd_end(this)
    type(cpmd_t), intent(inout) :: this

    call push_sub('cpmd.cpmd_end')

    if(associated(this%doldpsi)) deallocate(this%doldpsi)
    if(associated(this%zoldpsi)) deallocate(this%zoldpsi)

    call pop_sub()

  end subroutine cpmd_end

  FLOAT function cpmd_electronic_energy(this)
    type(cpmd_t),        intent(in)    :: this

    cpmd_electronic_energy = this%ecorr
    
  end function cpmd_electronic_energy

  subroutine cpmd_restart_write(this, gr, st)
    type(cpmd_t),        intent(in)    :: this
    type(grid_t),        intent(in)    :: gr
    type(states_t),      intent(in)    :: st

    integer :: ik, ist, idim, ii, err
    character(len=80) :: filename

    call push_sub('cpmd.cpmd_restart_write')

    call io_mkdir(trim(tmpdir)//'td/cpmd')

    ii = 0
    do ik = 1, st%d%nik
      do ist = 1, st%nst
        do idim = 1, st%d%dim
          ii = ii + 1
          if (ist < st%st_start .or.  ist > st%st_end) cycle
          write(filename,'(i10.10)') ii
          if(wfs_are_real(st)) then
            call drestart_write_function(trim(tmpdir)//'td/cpmd', filename, gr, this%doldpsi(:, idim, ist, ik), err, gr%m%np)
          else
            call zrestart_write_function(trim(tmpdir)//'td/cpmd', filename, gr, this%zoldpsi(:, idim, ist, ik), err, gr%m%np)
          end if
        end do
      end do
    end do

    call pop_sub()

  end subroutine cpmd_restart_write

  subroutine cpmd_restart_read(this, gr, st, ierr)
    type(cpmd_t),        intent(inout) :: this
    type(grid_t),        intent(in)    :: gr
    type(states_t),      intent(in)    :: st
    integer,             intent(out)   :: ierr

    integer :: ik, ist, idim, ii
    character(len=80) :: filename

    call push_sub('cpmd.cpmd_restart_read')

    ierr = 0

    ii = 0
    do ik = 1, st%d%nik
      do ist = 1, st%nst
        do idim = 1, st%d%dim
          ii = ii + 1
          if (ist < st%st_start .or.  ist > st%st_end) cycle
          write(filename,'(i10.10)') ii
          if(wfs_are_real(st)) then
            call drestart_read_function(trim(tmpdir)//'td/cpmd', filename, gr%m, this%doldpsi(:, idim, ist, ik), ierr)
          else
            call zrestart_read_function(trim(tmpdir)//'td/cpmd', filename, gr%m, this%zoldpsi(:, idim, ist, ik), ierr)
          end if
          if(ierr /= 0) return
        end do
      end do
    end do

    call pop_sub()

  end subroutine cpmd_restart_read

#include "undef.F90"
#include "real.F90"
#include "cpmd_inc.F90"

#include "undef.F90"
#include "complex.F90"
#include "cpmd_inc.F90"


end module cpmd_m

!! Local Variables:
!! mode: f90
!! coding: utf-8
!! End:
