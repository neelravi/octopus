!! Copyright (C) 2002-2006 M. Marques, A. Castro, A. Rubio, G. Bertsch
!!
!! This program is free software; you can redistribute it and/or modify
!! it under the terms of the GNU General Public License as published by
!! the Free Software Foundation; either version 2, or (at your option)
!! any later version.
!!
!! This program is distributed in the hope that it will be useful,
!! but WITHOUT ANY WARRANTY; without even the implied warranty of
!! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!! GNU General Public License for more details.
!!
!! You should have received a copy of the GNU General Public License
!! along with this program; if not, write to the Free Software
!! Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
!! 02111-1307, USA.
!!
!! $Id$


! ---------------------------------------------------------
subroutine X(one_body) (gr, geo, st, h)
  type(grid_t),        intent(inout) :: gr
  type(geometry_t),    intent(in)    :: geo
  type(states_t),      intent(inout) :: st
  type(hamiltonian_t), intent(in) :: h

  integer i, j, iunit, idir, iatom
  R_TYPE :: me, exp_r, exp_g, corr
  R_TYPE, allocatable :: gpsi(:,:), cpsi(:,:)

  call io_assign(iunit)
  iunit = io_open('matrix_elements/1-body', action='write')

  do i = 1, st%nst
    do j = 1, st%nst
      if(j > i) cycle
      
      me = st%eigenval(i,1) - X(mf_integrate) (gr%m, R_CONJ(st%X(psi) (1:NP, 1, i, 1)) * &
           h%Vhxc(1:NP, 1) * st%X(psi) (1:NP, 1, j, 1))

      write(iunit, *) i, j, me
    end do
  end do

  ALLOCATE(gpsi(1:NP_PART, 1:3), NP_PART*3)
  ALLOCATE(cpsi(1:NP_PART, 1), NP_PART)

  
  call io_assign(iunit)
  iunit = io_open('matrix_elements/gauge', action='write')

  do i = 1, st%nst
    do j = 1, st%nst
!      if(j >= i) cycle
      if(st%occ(i, 1) < CNST(0.0001)) cycle
      if(st%occ(j, 1) > CNST(0.0001)) cycle

      call X(f_gradient)(gr%sb, gr%f_der, st%X(psi)(:, 1, j, 1), gpsi(:, :))
       
      do idir = 1, 3
         exp_r = X(mf_integrate) (gr%m, R_CONJ(st%X(psi) (1:NP, 1, i, 1)) * &
              gr%m%x(1:NP, idir) * st%X(psi) (1:NP, 1, j, 1))
         
         exp_g = X(mf_integrate) (gr%m, R_CONJ(st%X(psi) (1:NP, 1, i, 1)) * &
              gpsi(1:NP, idir))
         
         corr = M_ZERO
         do iatom = 1, geo%natoms
           call X(conmut_vnl_r)(gr, geo, h%ep, 1, idir, iatom, st%X(psi)(1:NP, 1, j, 1), &
                cpsi, h%reltype, ik=1)
           corr = corr + X(mf_integrate) (gr%m, R_CONJ(st%X(psi) (1:NP, 1, i, 1)) * &
                cpsi(1:NP, 1))
         end do

         me = (st%eigenval(j,1) - st%eigenval(i,1))*exp_r
         
         write(iunit, *) i, j, idir, me, me - (exp_g + corr)

       end do
       
     end do
   end do
   
  deallocate(gpsi)

  call io_close(iunit)
end subroutine X(one_body)


! ---------------------------------------------------------
subroutine X(two_body) (gr, st)
  type(grid_t),        intent(inout) :: gr
  type(states_t),      intent(in)    :: st

  integer i, j, k, l, iunit
  R_TYPE :: me
  R_TYPE, allocatable :: n(:), v(:)

  call io_assign(iunit)
  iunit = io_open('matrix_elements/2-body', action='write')

  ALLOCATE(n(1:NP), NP)
  ALLOCATE(v(1:NP), NP)

  do i = 1, st%nst
    do j = 1, st%nst
      if(j > i) cycle

      n(:) = R_CONJ(st%X(psi) (:, 1, i, 1)) * st%X(psi) (:, 1, j, 1)
      call X(poisson_solve) (gr, v, n, all_nodes=.false.)

      do k = 1, st%nst
        if(k > i) cycle
        do l = 1, st%nst
          if(l > k) cycle
          if(l > j) cycle

          me = X(mf_integrate) (gr%m, v(1:NP) * &
                st%X(psi) (1:NP, 1, k, 1) *R_CONJ(st%X(psi) (1:NP, 1, l, 1)))

          write(iunit, *) i, j, k, l, me
        end do
      end do
    end do
  end do

  call io_close(iunit)
end subroutine X(two_body)

!! Local Variables:
!! mode: f90
!! coding: utf-8
!! End:
