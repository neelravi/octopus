!! Copyright (C) 2002-2006 M. Marques, A. Castro, A. Rubio, G. Bertsch
!!
!! This program is free software; you can redistribute it and/or modify
!! it under the terms of the GNU General Public License as published by
!! the Free Software Foundation; either version 2, or (at your option)
!! any later version.
!!
!! This program is distributed in the hope that it will be useful,
!! but WITHOUT ANY WARRANTY; without even the implied warranty of
!! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!! GNU General Public License for more details.
!!
!! You should have received a copy of the GNU General Public License
!! along with this program; if not, write to the Free Software
!! Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
!! 02111-1307, USA.
!!
!! $Id$

! This module calculates the derivatives (gradients, laplacians, etc.) 
! of a function. Note that the function whose derivative is to be calculated
! *has* to be defined (1:m%np_part), while the (1:m%np) values of the derivative
! are calculated. This was made to simplify the parallel mode, and has to be
! followed by the rest of the code.

! ---------------------------------------------------------
! This are the workhorse routines that handle the calculation of derivatives
subroutine X(derivatives_batch_start)(op, der, ff, opff, handle, ghost_update, set_bc)
  type(nl_operator_t),      target, intent(in)    :: op
  type(derivatives_t),      target, intent(in)    :: der
  type(batch_t),            target, intent(inout) :: ff
  type(batch_t),            target, intent(inout) :: opff
  type(derivatives_handle_batch_t), intent(out)   :: handle
  logical,                optional, intent(in)    :: ghost_update
  logical,                optional, intent(in)    :: set_bc

  logical :: set_bc_

  call push_sub('derivatives_inc.Xderivatives_batch_start')

  handle%ghost_update = .true.
  if(present(ghost_update)) handle%ghost_update = ghost_update

  handle%op   => op
  handle%der  => der
  handle%ff   => ff
  handle%opff => opff

  set_bc_ = .true.
  if(present(set_bc)) set_bc_ = set_bc
  if(set_bc_) call X(derivatives_batch_set_bc)(der, ff)

#ifdef HAVE_MPI

  if(derivatives_overlap(der) .and. der%mesh%parallel_in_domains .and. handle%ghost_update) then
    call X(ghost_update_batch_start)(der%mesh%vp, ff, der%comm_method, handle%pv_h)
  end if
#endif

  call pop_sub()
end subroutine X(derivatives_batch_start)


! ---------------------------------------------------------
subroutine X(derivatives_batch_finish)(handle)
  type(derivatives_handle_batch_t), intent(inout) :: handle

  call push_sub('derivatives_inc.Xderivatives_batch_finish')

#ifdef HAVE_MPI
  if(derivatives_overlap(handle%der) .and. handle%der%mesh%parallel_in_domains .and. handle%ghost_update) then
    call X(nl_operator_operate_batch)(handle%op, handle%ff, handle%opff, ghost_update=.false., points=OP_INNER)

    call X(ghost_update_batch_finish)(handle%pv_h)

    call X(nl_operator_operate_batch)(handle%op, handle%ff, handle%opff, ghost_update=.false., points=OP_OUTER)
  else
#endif
    
    call X(nl_operator_operate_batch)(handle%op, handle%ff, handle%opff, ghost_update=handle%ghost_update)
    
#ifdef HAVE_MPI
  end if
#endif

  call pop_sub()
end subroutine X(derivatives_batch_finish)


! ---------------------------------------------------------
subroutine X(derivatives_batch_perform)(op, der, ff, opff, ghost_update, set_bc)
  type(nl_operator_t),      target, intent(in)    :: op
  type(derivatives_t),      target, intent(in)    :: der
  type(batch_t),            target, intent(inout) :: ff
  type(batch_t),            target, intent(inout) :: opff
  logical,                optional, intent(in)    :: ghost_update
  logical,                optional, intent(in)    :: set_bc

  type(derivatives_handle_batch_t) :: handle

  call push_sub('derivatives_inc.Xderivatives_batch')

  call X(derivatives_batch_start)(op, der, ff, opff, handle, ghost_update, set_bc)
  call X(derivatives_batch_finish)(handle)

  call pop_sub()

end subroutine X(derivatives_batch_perform)


! ---------------------------------------------------------
! Now the simplified interfaces
subroutine X(derivatives_perform)(op, der, ff, op_ff, ghost_update, set_bc)
  type(nl_operator_t), target, intent(in)    :: op
  type(derivatives_t),         intent(in)    :: der
  R_TYPE,                      intent(inout) :: ff(:)     ! f(m%np_part)
  R_TYPE,                      intent(out)   :: op_ff(:)  ! lapl(m%np)
  logical, optional,           intent(in)    :: ghost_update
  logical, optional,           intent(in)    :: set_bc

  type(batch_t) :: batch_ff, batch_op_ff

  call push_sub('derivatives_inc.Xderivatives_lapl')

  call batch_init     (batch_ff, 1)
  call batch_add_state(batch_ff, ff)

  call batch_init     (batch_op_ff, 1)
  call batch_add_state(batch_op_ff, op_ff)

  call X(derivatives_batch_perform) (op, der, batch_ff, batch_op_ff, ghost_update, set_bc)

  call batch_end(batch_ff)
  call batch_end(batch_op_ff)
        
  call pop_sub()

end subroutine X(derivatives_perform)


! ---------------------------------------------------------
subroutine X(derivatives_lapl)(der, ff, op_ff, ghost_update, set_bc)
  type(derivatives_t),       intent(in)    :: der
  R_TYPE,                    intent(inout) :: ff(:)     ! f(m%np_part)
  R_TYPE,                    intent(out)   :: op_ff(:)  ! lapl(m%np)
  logical, optional,         intent(in)    :: ghost_update
  logical, optional,         intent(in)    :: set_bc

  call push_sub('derivatives_inc.Xderivatives_lapl')

  call X(derivatives_perform)(der%lapl, der, ff, op_ff, ghost_update, set_bc)
        
  call pop_sub()
end subroutine X(derivatives_lapl)


! ---------------------------------------------------------
subroutine X(derivatives_grad)(der, ff, op_ff, ghost_update, set_bc)
  type(derivatives_t), intent(in)    :: der
  R_TYPE,              intent(inout) :: ff(:)        ! f(mesh%np_part)
  R_TYPE,              intent(out)   :: op_ff(:, :)  ! grad(mesh%np, mesh%sb%dim)
  logical, optional,   intent(in)    :: ghost_update
  logical, optional,   intent(in)    :: set_bc

  integer :: idir
  logical :: set_bc_, ghost_update_
  
  call push_sub('derivatives_inc.Xderivatives_grad')

  ASSERT(ubound(op_ff, DIM=2) >= der%dim)

  set_bc_ = .true.
  if(present(set_bc)) set_bc_ = set_bc

  ghost_update_ = .true.
  if(present(ghost_update)) ghost_update_ = ghost_update
    
  do idir = 1, der%dim
    call X(derivatives_perform) (der%grad(idir), der, ff, op_ff(:, idir), &
      ghost_update_, set_bc_)

    set_bc_       = .false. ! there is no need to update again
    ghost_update_ = .false. ! the boundary or ghost points
  end do
    
  call pop_sub()
end subroutine X(derivatives_grad)


! ---------------------------------------------------------
subroutine X(derivatives_div)(der, ff, op_ff, ghost_update, set_bc)
  type(derivatives_t), intent(in)    :: der
  R_TYPE,              intent(inout) :: ff(:,:)   ! f(m%np_part, m%sb%dim)
  R_TYPE,              intent(out)   :: op_ff(:)  ! div(m%np)
  logical, optional,   intent(in)    :: ghost_update
  logical, optional,   intent(in)    :: set_bc

  R_TYPE, allocatable :: tmp(:)
  integer             :: idim, ii

  call push_sub('derivatives_inc.Xderivatives_div')

  ASSERT(ubound(ff, DIM=2) >= der%dim)

  SAFE_ALLOCATE(tmp(1:der%mesh%np))
  forall(ii = 1:der%mesh%np) op_ff(ii) = R_TOTYPE(M_ZERO)

  do idim = 1, der%dim
    call X(derivatives_perform) (der%grad(idim), der, ff(:, idim), tmp, &
      ghost_update, set_bc)

    forall(ii = 1:der%mesh%np) op_ff(ii) = op_ff(ii) + tmp(ii)
  end do

  SAFE_DEALLOCATE_A(tmp)

  call pop_sub()
end subroutine X(derivatives_div)


! ---------------------------------------------------------
subroutine X(derivatives_curl)(der, ff, op_ff, ghost_update, set_bc)
  type(derivatives_t), intent(in)    :: der
  R_TYPE,              intent(inout) :: ff(:,:)    ! f(m%np_part, der%dim) 
  R_TYPE,              intent(out)   :: op_ff(:,:) ! curl(m%np, der%dim) if dim = 2, curl(m%np, 1) if dim = 1.
  logical, optional,   intent(in)    :: ghost_update
  logical, optional,   intent(in)    :: set_bc

  integer, parameter  :: curl_dim(3) = (/-1, 1, 3/)
  R_TYPE, allocatable :: tmp(:)
  integer             :: ii, np

  call push_sub('derivatives_inc.Xderivatives_curl')

  ASSERT(der%dim==2 .or. der%dim==3)
  ASSERT(ubound(ff,    DIM=2) >= der%dim)
  ASSERT(ubound(op_ff, DIM=2) >= curl_dim(der%dim))

  SAFE_ALLOCATE(tmp(1:der%mesh%np_part))

  op_ff(:,:) = R_TOTYPE(M_ZERO)
  np = der%mesh%np

  select case(der%dim)
  case(3)
    call X(derivatives_perform) (der%grad(3), der, ff(:,1), tmp, ghost_update, set_bc)
    forall(ii = 1:np) op_ff(ii, 2) = op_ff(ii, 2) + tmp(ii)
    call X(derivatives_perform) (der%grad(2), der, ff(:,1), tmp, .false., .false.)
    forall(ii = 1:np) op_ff(ii, 3) = op_ff(ii, 3) - tmp(ii)

    call X(derivatives_perform) (der%grad(3), der, ff(:,2), tmp, ghost_update, set_bc)
    forall(ii = 1:np) op_ff(ii, 1) = op_ff(ii, 1) - tmp(ii)
    call X(derivatives_perform) (der%grad(1), der, ff(:,2), tmp, .false., .false.)
    forall(ii = 1:np) op_ff(ii, 3) = op_ff(ii, 3) + tmp(ii)

    call X(derivatives_perform) (der%grad(2), der, ff(:,3), tmp, ghost_update, set_bc)
    forall(ii = 1:np) op_ff(ii, 1) = op_ff(ii, 1) + tmp(ii)
    call X(derivatives_perform) (der%grad(1), der, ff(:,3), tmp, .false., .false.)
    forall(ii = 1:np) op_ff(ii, 2) = op_ff(ii, 2) - tmp(ii)

  case(2)
    call X(derivatives_perform) (der%grad(2), der, ff(:,1), tmp, ghost_update, set_bc)
    forall(ii = 1:np) op_ff(ii, 1) = op_ff(ii, 1) - tmp(ii)
    call X(derivatives_perform) (der%grad(1), der, ff(:,2), tmp, .false., .false.)
    forall(ii = 1:np) op_ff(ii, 1) = op_ff(ii, 1) + tmp(ii)
  end select

  SAFE_DEALLOCATE_A(tmp)
  call pop_sub()
end subroutine X(derivatives_curl)


! ---------------------------------------------------------
! Set all boundary points in f to zero to implement zero
! boundary conditions for the derivatives, in finite system;
! or set according to periodic boundary conditions.
subroutine X(derivatives_set_bc)(der, f)
  type(derivatives_t), intent(in)    :: der
  R_TYPE,              intent(inout) :: f(:)

  integer :: bndry_start, bndry_end
  integer :: p
  integer :: iper, ip, ix, iy, iz, dx, dy, dz
#ifdef HAVE_MPI
  integer :: ipart, nreq
  integer, allocatable :: req(:), statuses(:, :)
#endif

  call push_sub('derivatives_inc.Xset_bc')
  call profiling_in(set_bc_prof, 'SET_BC')
  
  ASSERT(ubound(f, DIM=1) >= der%mesh%np_part)

  p = der%mesh%vp%partno
  
  ! The boundary points are at different locations depending on the presence
  ! of ghost points due to domain parallelization.
  if(der%mesh%parallel_in_domains) then
    bndry_start = der%mesh%vp%np_local(p) + der%mesh%vp%np_ghost(p) + 1
    bndry_end   = der%mesh%vp%np_local(p) + der%mesh%vp%np_ghost(p) + der%mesh%vp%np_bndry(p)
  else
    bndry_start = der%mesh%np+1
    bndry_end   = der%mesh%np_part
  end if
    
  if(der%zero_bc) then
    forall(ip = bndry_start:bndry_end) f(ip) = R_TOTYPE(M_ZERO)
  end if

  if(der%mesh%sb%mr_flag) call multires()

  if(der%periodic_bc) then

#ifdef HAVE_MPI
    if(der%mesh%parallel_in_domains) then
      call profiling_in(set_bc_comm_prof, 'SET_BC_COMMUNICATION')
      ! get the points from other nodes
      SAFE_ALLOCATE(req(1:2*der%mesh%vp%npart))

      nreq = 0

      do ipart = 1, der%mesh%vp%npart
        if(ipart == p .or. der%mesh%nsend(ipart) == 0) cycle
        nreq = nreq + 1
        call MPI_Isend(f(1), 1, der%mesh%X(send_type)(ipart), ipart - 1, 3, der%mesh%vp%comm, req(nreq), mpi_err)
      end do

      do ipart = 1, der%mesh%vp%npart
        if(ipart == p .or. der%mesh%nrecv(ipart) == 0) cycle
        nreq = nreq + 1
        call MPI_Irecv(f(1), 1, der%mesh%X(recv_type)(ipart), ipart - 1, 3, der%mesh%vp%comm, req(nreq), mpi_err)
      end do

      call profiling_count_transfers(sum(der%mesh%nrecv(1:der%mesh%vp%npart) + der%mesh%nrecv(1:der%mesh%vp%npart)), f(1))

      call profiling_out(set_bc_comm_prof)
    end if
#endif

    do iper = 1, der%mesh%nper
      f(der%mesh%per_points(iper)) = f(der%mesh%per_map(iper))
    end do

#ifdef HAVE_MPI
    if(der%mesh%parallel_in_domains) then
      call profiling_in(set_bc_comm_prof)

      SAFE_ALLOCATE(statuses(1:MPI_STATUS_SIZE, 1:nreq))
      call MPI_Waitall(nreq, req(1), statuses(1, 1), mpi_err)
      SAFE_DEALLOCATE_A(statuses)
      SAFE_DEALLOCATE_A(req)

      call profiling_out(set_bc_comm_prof)
    end if
#endif

  end if

  call profiling_out(set_bc_prof)
  call pop_sub()

contains 

  subroutine multires()
    integer :: ii, jj, kk, i_lev
    
    call push_sub('derivatives_inc.Xset_bc.multires')

    do ip = bndry_start, bndry_end
      ix = der%mesh%idx%Lxyz(ip, 1)
      iy = der%mesh%idx%Lxyz(ip, 2)
      iz = der%mesh%idx%Lxyz(ip, 3)

      i_lev = der%mesh%resolution(ix,iy,iz)

      ! resolution is 2**num_radii for outer boundary points, but now we want inner boundary points
      if(i_lev.ne.2**der%mesh%sb%hr_area%num_radii) then

        dx = abs(mod(ix, 2**(i_lev)))
        dy = abs(mod(iy, 2**(i_lev)))
        dz = abs(mod(iz, 2**(i_lev)))

        do ii = 1, der%mesh%sb%hr_area%interp%nn
          do jj = 1, der%mesh%sb%hr_area%interp%nn
            do kk = 1, der%mesh%sb%hr_area%interp%nn
              f(ip) = f(ip) + der%mesh%sb%hr_area%interp%ww(ii)*&
                              der%mesh%sb%hr_area%interp%ww(jj)*&
                              der%mesh%sb%hr_area%interp%ww(kk)*&
                              f(der%mesh%idx%Lxyz_inv(ix + der%mesh%sb%hr_area%interp%posi(ii)*dx, &
                                                      iy + der%mesh%sb%hr_area%interp%posi(jj)*dy, &
                                                      iz + der%mesh%sb%hr_area%interp%posi(kk)*dz))
            end do
          end do
        end do
        
      end if
      
    end do

    call pop_sub()
  end subroutine multires

end subroutine X(derivatives_set_bc)

! ---------------------------------------------------------
! Set all boundary points in f to zero to implement zero
! boundary conditions for the derivatives, in finite system;
! or set according to periodic boundary conditions.
subroutine X(derivatives_batch_set_bc)(der, fb)
  type(derivatives_t), intent(in)    :: der
  type(batch_t),       intent(inout) :: fb

  integer :: ip, ist
  integer :: pp, bndry_start, bndry_end
#ifdef HAVE_MPI
  integer :: ipart, nreq
  integer, allocatable :: req(:), statuses(:, :)
#endif

  call push_sub('derivatives_inc.Xset_bc_batch')

  if(der%mesh%sb%mr_flag) then
    do ist = 1, fb%nst_linear
      call X(derivatives_set_bc)(der, fb%states_linear(ist)%X(psi)(:))
    end do
    
  else

    call profiling_in(set_bc_prof, 'SET_BC')
    pp = der%mesh%vp%partno

    ! The boundary points are at different locations depending on the presence
    ! of ghost points due to domain parallelization.
    if(der%mesh%parallel_in_domains) then
      bndry_start = der%mesh%vp%np_local(pp) + der%mesh%vp%np_ghost(pp) + 1
      bndry_end   = der%mesh%vp%np_local(pp) + der%mesh%vp%np_ghost(pp) + der%mesh%vp%np_bndry(pp)
    else
      bndry_start = der%mesh%np + 1
      bndry_end   = der%mesh%np_part
    end if

    if(der%zero_bc) then
      forall (ist=1:fb%nst_linear, ip=bndry_start:bndry_end)
        fb%states_linear(ist)%X(psi)(ip) = R_TOTYPE(M_ZERO)
      end forall
    end if

    if(der%periodic_bc) then

#ifdef HAVE_MPI
      if(der%mesh%parallel_in_domains) then
        call profiling_in(set_bc_comm_prof, 'SET_BC_COMMUNICATION')

        ! get the points that are copies from other nodes
        SAFE_ALLOCATE(req(1:2*der%mesh%vp%npart*fb%nst_linear))

        nreq = 0

        do ist = 1, fb%nst_linear
          do ipart = 1, der%mesh%vp%npart
            if(ipart == der%mesh%vp%partno .or. der%mesh%nrecv(ipart) == 0) cycle
            nreq = nreq + 1
            call MPI_Irecv(fb%states_linear(ist)%X(psi)(1), 1, der%mesh%X(recv_type)(ipart), ipart - 1, 3, &
                   der%mesh%vp%comm, req(nreq), mpi_err)
          end do
        end do

        do ist = 1, fb%nst_linear
          do ipart = 1, der%mesh%vp%npart
            if(ipart == der%mesh%vp%partno .or. der%mesh%nsend(ipart) == 0) cycle
            nreq = nreq + 1
            call MPI_Isend(fb%states_linear(ist)%X(psi)(1), 1, der%mesh%X(send_type)(ipart), ipart - 1, 3, &
              der%mesh%vp%comm, req(nreq), mpi_err)
          end do
        end do
        
        call profiling_count_transfers(&
        sum(der%mesh%nsend(1:der%mesh%vp%npart) + der%mesh%nrecv(1:der%mesh%vp%npart))*fb%nst_linear, &
             R_TOTYPE(M_ONE))

        call profiling_out(set_bc_comm_prof)
      end if
#endif

      forall (ist = 1:fb%nst_linear, ip = 1:der%mesh%nper)
        fb%states_linear(ist)%X(psi)(der%mesh%per_points(ip)) = fb%states_linear(ist)%X(psi)(der%mesh%per_map(ip))
      end forall

#ifdef HAVE_MPI
      if(der%mesh%parallel_in_domains) then
        call profiling_in(set_bc_comm_prof)

        SAFE_ALLOCATE(statuses(1:MPI_STATUS_SIZE, 1:nreq))
        call MPI_Waitall(nreq, req(1), statuses(1, 1), mpi_err)
        SAFE_DEALLOCATE_A(statuses)
        SAFE_DEALLOCATE_A(req)

        call profiling_out(set_bc_comm_prof)
      end if
#endif

    end if

    call profiling_out(set_bc_prof)

  end if

  call pop_sub()

end subroutine X(derivatives_batch_set_bc)

! ---------------------------------------------------------
! The action of the angular momentum operator (three spatial components).
! In case of real functions, it does not include the -i prefactor
! (L = -i r ^ nabla).
! ---------------------------------------------------------
subroutine X(f_angular_momentum)(sb, mesh, der, f, lf, ghost_update, set_bc)
  type(simul_box_t),   intent(in)    :: sb
  type(mesh_t),        intent(in)    :: mesh
  type(derivatives_t), intent(inout) :: der
  R_TYPE,              intent(inout) :: f(:)     ! f(m%np_part)
  R_TYPE,              intent(out)   :: lf(:, :)  ! lf(m%np, 3) in 3D, lf(m%np, 1) in 2D
  logical, optional,   intent(in)    :: ghost_update
  logical, optional,   intent(in)    :: set_bc

  R_TYPE, allocatable :: gf(:, :)
  FLOAT :: x1, x2, x3
  R_TYPE :: factor
  integer :: ip

  call push_sub('derivatives_inc.Xf_angular_momentum')

  ASSERT(sb%dim.ne.1)

  SAFE_ALLOCATE(gf(1:mesh%np, 1:sb%dim))

  if (present(ghost_update) .and. present(set_bc)) &
    call X(derivatives_grad)(der, f, gf, ghost_update, set_bc)
  if (present(ghost_update) .and. .not. present(set_bc)) &
    call X(derivatives_grad)(der, f, gf, ghost_update = ghost_update)
  if (.not. present(ghost_update) .and. present(set_bc)) &
    call X(derivatives_grad)(der, f, gf, set_bc = set_bc)
  if (.not. present(ghost_update) .and. .not. present(set_bc)) &
    call X(derivatives_grad)(der, f, gf)

#if defined(R_TCOMPLEX)
  factor = -M_ZI
#else
  factor = M_ONE
#endif

  select case(sb%dim)
  case(3)
    do ip = 1, mesh%np
      x1 = mesh%x(ip, 1)
      x2 = mesh%x(ip, 2)
      x3 = mesh%x(ip, 3)
      lf(ip, 1) = factor*(x2*gf(ip, 3) - x3*gf(ip, 2))
      lf(ip, 2) = factor*(x3*gf(ip, 1) - x1*gf(ip, 3))
      lf(ip, 3) = factor*(x1*gf(ip, 2) - x2*gf(ip, 1))
    end do

  case(2)
    do ip = 1, mesh%np
      x1 = mesh%x(ip, 1)
      x2 = mesh%x(ip, 2)
      lf(ip, 1) = factor*(x1*gf(ip, 2) - x2*gf(ip, 1))
    end do

  end select

  SAFE_DEALLOCATE_A(gf)
  call pop_sub()
end subroutine X(f_angular_momentum)

! ---------------------------------------------------------
! Square of the angular momentum L. This has to be very much improved if
! accuracy is needed.
! ---------------------------------------------------------
subroutine X(f_l2)(sb, m, der, f, l2f, ghost_update)
  type(simul_box_t),   intent(in)    :: sb
  type(mesh_t),        intent(in)    :: m
  type(derivatives_t), intent(inout) :: der
  R_TYPE,              intent(inout) :: f(:)   ! f(1:m%np_part)
  R_TYPE,              intent(out)   :: l2f(:)
  logical, optional,   intent(in)    :: ghost_update

  R_TYPE, allocatable :: gf(:, :), ggf(:, :, :)
  integer :: j

  call push_sub('derivatives_inc.Xf_l2')

  ASSERT(sb%dim == 2 .or. sb%dim == 3)

  l2f = R_TOTYPE(M_ZERO)

  select case(sb%dim)
  case(3)
    SAFE_ALLOCATE( gf(1:m%np_part, 1:3))
    SAFE_ALLOCATE(ggf(1:m%np_part, 1:3, 1:3))

    if (present(ghost_update)) then
       call X(f_angular_momentum)(sb, m, der, f, gf, ghost_update)
    else
       call X(f_angular_momentum)(sb, m, der, f, gf)
    endif

    do j = 1, 3
      if (present(ghost_update)) then
        call X(f_angular_momentum)(sb, m, der, gf(:,j), ggf(:,:,j), ghost_update)
      else
        call X(f_angular_momentum)(sb, m, der, gf(:,j), ggf(:,:,j))
      endif

    end do

    do j = 1, sb%dim
      l2f(1:m%np) = l2f(1:m%np) + ggf(1:m%np, j, j)
    end do

  case(2)
    SAFE_ALLOCATE( gf(1:m%np_part, 1:1))
    SAFE_ALLOCATE(ggf(1:m%np_part, 1:1, 1:1))

    if (present(ghost_update)) then
      call X(f_angular_momentum)(sb, m, der, f, gf, ghost_update)
      call X(f_angular_momentum)(sb, m, der, gf(:, 1), ggf(:, :, 1), ghost_update)
    else
      call X(f_angular_momentum)(sb, m, der, f, gf)
      call X(f_angular_momentum)(sb, m, der, gf(:, 1), ggf(:, :, 1))
    endif

    l2f(1:m%np) = ggf(1:m%np, 1, 1)
  end select


  ! In case of real functions, since the angular momentum calculations
  ! lack a (-i) prefactor, we must add a (-1) factor
#if defined(R_TREAL)
  l2f = - l2f
#endif

  SAFE_DEALLOCATE_A(gf)
  SAFE_DEALLOCATE_A(ggf)
  call pop_sub()
end subroutine X(f_l2)

!! Local Variables:
!! mode: f90
!! coding: utf-8
!! End:
