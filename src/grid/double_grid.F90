!! Copyright (C) 2002-2006 X. Andrade
!!
!! This program is free software; you can redistribute it and/or modify
!! it under the terms of the GNU General Public License as published by
!! the Free Software Foundation; either version 2, or (at your option)
!! any later version.
!!
!! This program is distributed in the hope that it will be useful,
!! but WITHOUT ANY WARRANTY; without even the implied warranty of
!! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!! GNU General Public License for more details.
!!
!! You should have received a copy of the GNU General Public License
!! along with this program; if not, write to the Free Software
!! Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
!! 02111-1307, USA.
!!
!! $Id: specie.F90 2711 2007-02-13 17:36:18Z xavier $

#include "global.h"

module double_grid_m

  use curvlinear_m
  use datasets_m
  use geometry_m
  use global_m
  use math_m
  use mesh_m
  use messages_m
  use functions_m
  use mesh_function_m
  use lib_oct_m
  use lib_oct_gsl_spline_m
  use lib_oct_parser_m
  use profiling_m
  use simul_box_m
  use specie_m
  use submesh_m

  implicit none
  
  private
  
  public ::              &
       double_grid_t,    &
       double_grid_init, &
       double_grid_end,  &
       double_grid_apply_local,     &
       double_grid_apply_non_local, &
       double_grid_get_rmax,        &
       double_grid_get_hmax,        &
       double_grid_enlarge

  type double_grid_t
     private
     integer :: order
     integer :: npoints
     integer :: spacing_divisor
     integer :: interpolation_min
     integer :: interpolation_max
     integer :: nn
     logical :: use_double_grid
     FLOAT, pointer :: co(:)
  end type double_grid_t

  type(profile_t), save :: double_grid_local_prof, double_grid_nonlocal_prof

contains
  
  subroutine double_grid_init(this, sb)
    type(double_grid_t), intent(out) :: this
    type(simul_box_t),   intent(in)  :: sb

    this%spacing_divisor = 3
    this%nn = (this%spacing_divisor - 1)/2
   
    !%Variable DoubleGrid
    !%Type logical
    !%Default no
    !%Section Mesh
    !%Description
    !% Enables or disables the use of a double grid technique to
    !% increase the precision of the application of the
    !% pseudopotentials.
    !%End
    if (sb%dim == 3) then 
      call loct_parse_logical(check_inp('DoubleGrid'), .false., this%use_double_grid)
    else
      this%use_double_grid = .false.
    end if

    !%Variable DoubleGridOrder
    !%Type integer 
    !%Default 9
    !%Section Mesh
    !%Description
    !% Order of the interpolation used for the Double Grid. Must be
    !% and odd number. Low order interpolation schemes are not
    !% recommended. The default is to use 9th order interpolation.
    !%End
    call loct_parse_int(check_inp('DoubleGridOrder'), 9, this%order)
    
    ASSERT(mod(this%order,2) == 1)
    
    this%interpolation_min = -this%order/2
    this%interpolation_max =  this%order/2 + 1
    this%npoints = this%interpolation_max - this%interpolation_min + 1
    
    ALLOCATE(this%co(this%interpolation_min:this%interpolation_max), this%npoints)

    call calc_coefficients

    contains

      subroutine calc_coefficients
        integer, allocatable :: points(:)
        integer :: ii, jj

        ALLOCATE(points(this%interpolation_min:this%interpolation_max), this%npoints)
        
        do ii = this%interpolation_min,  this%interpolation_max
          points(ii) = 3 * ii - 1
        end do

        do ii = this%interpolation_min,  this%interpolation_max
          
          this%co(ii) = M_ONE
          do jj = this%interpolation_min,  this%interpolation_max
            if(ii /= jj) then
              this%co(ii) = this%co(ii) * (dble(points(jj))/dble(points(jj) - points(ii)))
            end if
          end do

        end do
        
        deallocate(points)
        
      end subroutine calc_coefficients

  end subroutine double_grid_init

  subroutine double_grid_end(this)
    type(double_grid_t), intent(inout) :: this
 
    deallocate(this%co)

  end subroutine double_grid_end
  
  FLOAT function double_grid_get_hmax(this, mesh) result(hmax)
    type(double_grid_t), intent(in) :: this
    type(mesh_t),        intent(in) :: mesh

    hmax = maxval(mesh%h(1:MAX_DIM))
      
    if(this%use_double_grid)  hmax = hmax / this%spacing_divisor
    
  end function double_grid_get_hmax

  FLOAT function double_grid_get_rmax(this, s, m) result(rmax)
    type(double_grid_t),     intent(in) :: this
    type(specie_t),          intent(in) :: s
    type(mesh_t),            intent(in) :: m
    
    rmax = loct_spline_cutoff_radius(s%ps%vl, s%ps%projectors_sphere_threshold)
    if(this%use_double_grid) then 
      rmax = rmax + this%interpolation_max * maxval(m%h(1:3))
    end if

  end function double_grid_get_rmax

  integer function double_grid_enlarge(this)
    type(double_grid_t),     intent(in) :: this
    
    double_grid_enlarge = 0 
    if(this%use_double_grid) double_grid_enlarge = this%interpolation_max * this%nn

  end function double_grid_enlarge

#define profiler double_grid_local_prof
#define profiler_label "DOUBLE_GRID_LOCAL"
#define double_grid_apply double_grid_apply_local
#define calc_pot(vv) vv = loct_splint(s%ps%vl, r)

#include "double_grid_apply.F90"

#undef calc_pot
#undef double_grid_apply
#undef profiler
#undef profiler_label

#define profiler double_grid_nonlocal_prof
#define profiler_label "DOUBLE_GRID_NL"
#define double_grid_apply double_grid_apply_non_local
#define calc_pot(vv) call specie_real_nl_projector(s, x, l, lm, ic, vv, tmp)

#include "double_grid_apply.F90"

end module double_grid_m

!! Local Variables:
!! mode: f90
!! coding: utf-8
!! End:
