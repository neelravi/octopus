!! Copyright (C) 2002 M. Marques, A. Castro, A. Rubio, G. Bertsch
!!
!! This program is free software; you can redistribute it and/or modify
!! it under the terms of the GNU General Public License as published by
!! the Free Software Foundation; either version 2, or (at your option)
!! any later version.
!!
!! This program is distributed in the hope that it will be useful,
!! but WITHOUT ANY WARRANTY; without even the implied warranty of
!! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!! GNU General Public License for more details.
!!
!! You should have received a copy of the GNU General Public License
!! along with this program; if not, write to the Free Software
!! Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
!! 02111-1307, USA.

subroutine td_rti(h, sys, td, t)
  type(system_type), intent(inout) :: sys
  type(hamiltonian_type), intent(inout) :: h
  type(td_type), intent(inout) :: td
  real(r8), intent(in) :: t

  integer :: is  
  sub_name = 'td_rti'; call push_sub()

  call dcopy(sys%st%nspin*sys%m%np, td%v_old(1, 1, 2), 1, td%v_old(1, 1, 3), 1)
  call dcopy(sys%st%nspin*sys%m%np, td%v_old(1, 1, 1), 1, td%v_old(1, 1, 2), 1)
  do is = 1, sys%st%nspin
     td%v_old(:, is, 1) = h%vhartree(:) + h%vxc(:, is)
  enddo
  
  select case(td%evolution_method)
  case(OLD_REVERSAL)
    call td_rti1
  case(REVERSAL)
    call td_rti2
  case(APP_REVERSAL)
    call td_rti3
  case(EXPONENTIAL_MIDPOINT)
    call td_rti4
  end select

  call pop_sub(); return
contains

  ! Warning: this subroutine should only be used with LDA/GGA functionals
  subroutine td_rti1
    integer is, ik, ist
    real(r8), allocatable :: aux(:,:)
    complex(r8), allocatable :: zpsi1(:,:,:,:)
    
    sub_name = 'td_rti1'; call push_sub()

    allocate(aux(sys%m%np, sys%st%nspin))

    call xpolate_pot(td%dt/2._r8, td%dt, sys%m%np, sys%st%nspin, &
                     td%v_old(:, :, 3), td%v_old(:, :, 2), td%v_old(:, :, 1), aux)
    
    h%VHartree = 0._r8; h%Vxc = aux
    allocate(zpsi1(0:sys%m%np, sys%st%dim, sys%st%st_start:sys%st%st_end, sys%st%nik))
    zpsi1 = sys%st%zpsi
    do ik = 1, sys%st%nik
      do ist = sys%st%st_start, sys%st%st_end
        call td_dtexp(h, sys, td, ik, sys%st%zpsi(:,:, ist, ik), td%dt, t-td%dt)
      end do
    end do
    sys%st%zpsi = zpsi1
    deallocate(zpsi1)
    
    call zcalcdens(sys%st, sys%m%np, aux, .true.)
    sys%st%rho = (sys%st%rho +  aux) / 2.0_r8
    deallocate(aux)
    
    call zhamiltonian_setup(h, sys)
    
    do ik = 1, sys%st%nik
      do ist = sys%st%st_start, sys%st%st_end
        call td_dtexp(h, sys, td, ik, sys%st%zpsi(:,:, ist, ik), td%dt, t-td%dt)
      end do
    end do
    
    call pop_sub(); return
  end subroutine td_rti1

  subroutine td_rti2
    real(r8), allocatable :: vhxc_t1(:,:), vhxc_t2(:,:)
    complex(r8), allocatable :: zpsi1(:,:,:,:)
    integer is, ik, ist

    sub_name = 'td_rti2'; call push_sub()

    allocate(zpsi1(0:sys%m%np, sys%st%dim, sys%st%st_start:sys%st%st_end, sys%st%nik))
    zpsi1 = sys%st%zpsi ! store zpsi
    
    allocate(vhxc_t1(sys%m%np, sys%st%nspin))
    do is = 1, sys%st%nspin ! store Vhxc
      Vhxc_t1(:, is) = h%VHartree(:) + h%Vxc(:, is)
    end do
    
    ! propagate dt with H(t-dt)
    do ik = 1, sys%st%nik
      do ist = sys%st%st_start, sys%st%st_end
        call td_dtexp(h, sys, td, ik, sys%st%zpsi(:,:, ist, ik), td%dt, t-td%dt)
      end do
    end do
    
    call zcalcdens(sys%st, sys%m%np, sys%st%rho, .true.)
    call zhamiltonian_setup(h, sys)
    
    sys%st%zpsi = zpsi1
    deallocate(zpsi1)
    
    ! store Vhxc at t
    allocate(vhxc_t2(sys%m%np, sys%st%nspin))
    do is = 1, sys%st%nspin
      Vhxc_t2(:, is) = h%VHartree(:) + h%Vxc(:, is)
    end do
    
    ! propagate dt/2 with H(t-dt)
    h%Vhartree = 0._r8
    h%Vxc = Vhxc_t1
    do ik = 1, sys%st%nik
      do ist = sys%st%st_start, sys%st%st_end
        call td_dtexp(h, sys, td, ik, sys%st%zpsi(:,:, ist, ik), td%dt/2._r8, t-td%dt)
      end do
    end do
    
    deallocate(vhxc_t1)
    
    ! propagate dt/2 with H(t)
    h%Vxc = Vhxc_t2
    do ik = 1, sys%st%nik
      do ist = sys%st%st_start, sys%st%st_end
        call td_dtexp(h, sys, td, ik, sys%st%zpsi(:,:, ist, ik), td%dt/2._r8, t)
      end do
    end do
    
    deallocate(vhxc_t2)

    call pop_sub(); return
  end subroutine td_rti2

  subroutine td_rti3
    integer is, ik, ist
    real(r8), allocatable :: aux(:,:)

    sub_name = 'td_rti3'; call push_sub()

    allocate(aux(sys%m%np, sys%st%nspin))

    call xpolate_pot(td%dt/2._r8, td%dt, sys%m%np, sys%st%nspin, &
                     td%v_old(:, :, 3), td%v_old(:, :, 2), td%v_old(:, :, 1), aux)
    
    ! propagate dt/2 with H(t-dt)
    h%Vhartree = 0._r8
    h%vxc = td%v_old(:, :, 2)
    do ik = 1, sys%st%nik
      do ist = sys%st%st_start, sys%st%st_end
        call td_dtexp(h, sys, td, ik, sys%st%zpsi(:,:, ist, ik), td%dt/2._r8, t-td%dt)
      end do
    end do
    
    h%vxc = aux
    do ik = 1, sys%st%nik
      do ist = sys%st%st_start, sys%st%st_end
        call td_dtexp(h, sys, td, ik, sys%st%zpsi(:,:, ist, ik), td%dt/2._r8, t)
      end do
    end do
 
    deallocate(aux)   
    call pop_sub(); return
  end subroutine td_rti3

  subroutine td_rti4
    integer :: ist, ik

    sub_name = 'td_rti4'; call push_sub()

    call xpolate_pot(td%dt/2._r8, td%dt, sys%m%np, sys%st%nspin, &
                     td%v_old(:, :, 3), td%v_old(:, :, 2), td%v_old(:, :, 1), h%vxc)

    h%vhartree = 0._r8
    do ik = 1, sys%st%nik
      do ist = sys%st%st_start, sys%st%st_end
        call td_dtexp(h, sys, td, ik, sys%st%zpsi(:,:, ist, ik), td%dt, t + td%dt/2._r8)
      end do
    end do

    call pop_sub(); return
  end subroutine td_rti4

  subroutine xpolate_pot(t, dt, np, dim, pot2, pot1, pot0, pot)
    real(r8), intent(in)  :: t, dt
    integer, intent(in)   :: np, dim
    real(r8), intent(in)  :: pot0(np, dim), pot1(np, dim), pot2(np, dim)
    real(r8), intent(out) :: pot(np, dim)

    !pot = pot0 + t/dt       * (3._r8/2._r8*pot0 + 1._r8/2._r8*pot2 - 2.0_r8*pot1) + &
    !             t**2/dt**2 * (1._r8/2._r8*pot0 + 1._r8/2._r8*pot2 -        pot1)
    call dcopy(np*dim,                                                    pot0, 1, pot, 1)
    call daxpy(np*dim, (t/dt)*(3._r8/2._r8) + (t**2/dt**2)*(1._r8/2._r8), pot0, 1, pot, 1)
    call daxpy(np*dim, (t/dt)*(-2._r8)      + (t**2/dt**2)*(-1._r8),      pot1, 1, pot, 1)
    call daxpy(np*dim, (t/dt)*(1._r8/2._r8) + (t**2/dt**2)*(1._r8/2._r8), pot2, 1, pot, 1)

  end subroutine xpolate_pot


end subroutine td_rti
