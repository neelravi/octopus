!! Copyright (C) 2002-2006 M. Marques, A. Castro, A. Rubio, G. Bertsch
!!
!! This program is free software; you can redistribute it and/or modify
!! it under the terms of the GNU General Public License as published by
!! the Free Software Foundation; either version 2, or (at your option)
!! any later version.
!!
!! This program is distributed in the hope that it will be useful,
!! but WITHOUT ANY WARRANTY; without even the implied warranty of
!! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!! GNU General Public License for more details.
!!
!! You should have received a copy of the GNU General Public License
!! along with this program; if not, write to the Free Software
!! Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
!! 02111-1307, USA.
!!
!! $Id$

! ---------------------------------------------------------
subroutine xc_get_vxc(gr, xcs, rho, ispin, vxc, ex, ec, ip, qtot)
  type(grid_t),       intent(inout) :: gr
  type(xc_t), target, intent(in)    :: xcs
  FLOAT,              intent(in)    :: rho(:, :)
  integer,            intent(in)    :: ispin
  FLOAT,              intent(inout) :: vxc(:,:), ex, ec
  FLOAT,              intent(in)    :: ip, qtot

  FLOAT :: l_dens(MAX_SPIN), l_dedd(MAX_SPIN), l_sigma(3), l_vsigma(3)!, l_tau(MAX_SPIN) , l_dedtau(MAX_SPIN)
  FLOAT, allocatable :: dens(:,:), dedd(:,:), ex_per_vol(:), ec_per_vol(:)
  FLOAT, allocatable :: gdens(:,:,:), dedgd(:,:,:)
  FLOAT, allocatable :: tau(:,:), dedtau(:,:)

  integer :: jj, ixc, spin_channels
  FLOAT   :: e, r
  logical :: gga, mgga

  type(xc_functl_t), pointer :: functl(:)

  call push_sub('xc_vxc.xc_get_vxc')

  if(ispin == UNPOLARIZED) then
    functl => xcs%functl(:, 1)
  else
    functl => xcs%functl(:, 2)
  end if

  ! is there anything to do ?
  jj = XC_FAMILY_LDA + XC_FAMILY_GGA + XC_FAMILY_HYB_GGA + XC_FAMILY_MGGA
  if(iand(xcs%family, jj) == 0) then
    call pop_sub()
    return
  end if

  call profiling_in(C_PROFILING_XC_LOCAL)

  ! initialize a couple of handy variables
  gga           = iand(xcs%family, XC_FAMILY_GGA + XC_FAMILY_HYB_GGA).ne.0
  mgga          = iand(xcs%family, XC_FAMILY_MGGA).ne.0

  ! This is a bit ugly (why functl(1) and not functl(2)?, but for the moment it works.
  spin_channels = functl(1)%spin_channels

  call  lda_init()
  if(gga.or.mgga) call  gga_init()
  if(       mgga) call mgga_init()

  !$omp parallel do private(l_dens, l_sigma, ixc, l_dedd, r, e, l_vsigma)
  space_loop: do jj = 1, NP

    ! make a local copy with the correct memory order
    l_dens(1:spin_channels) = dens (jj, 1:spin_channels)
    if(gga.or.mgga) then
      l_sigma(1) = sum(gdens(jj, :,1)*gdens(jj, :,1))
      if(ispin == SPIN_POLARIZED) then
        l_sigma(2) = sum(gdens(jj, :,1)*gdens(jj, :,2))
        l_sigma(3) = sum(gdens(jj, :,2)*gdens(jj, :,2))
      end if
    end if
!    if(mgga) l_tau  (:)   = tau  (jj, :)

    ! Calculate the potential/gradient density in local reference frame.
    functl_loop: do ixc = 1, 2

      select case(functl(ixc)%family)
      case(XC_FAMILY_LDA)
        call xc_f90_lda_vxc(functl(ixc)%conf, l_dens(1), e, l_dedd(1))

      case(XC_FAMILY_GGA)
        if(functl(ixc)%id == XC_GGA_XC_LB) then
          call mesh_r(gr%m, jj, r)
          call xc_f90_gga_lb_modified(functl(ixc)%conf, l_dens(1), l_sigma(1), &
            r, l_dedd(1))
          e = M_ZERO; l_vsigma = M_ZERO
        else
          call xc_f90_gga(functl(ixc)%conf, l_dens(1), l_sigma(1), &
            e, l_dedd(1), l_vsigma(1))
        end if

      case(XC_FAMILY_HYB_GGA)
        call xc_f90_hyb_gga(functl(ixc)%conf, l_dens(1), l_sigma(1), &
          e, l_dedd(1), l_vsigma(1))

      case(XC_FAMILY_MGGA)
        message(1) = 'Meta-GGAs are currently disabled.'
        call write_fatal(1)
        ! call xc_f90_mgga(functl(ixc)%conf, l_dens(1), l_gdens(1,1), l_tau(1), &
        !   e, l_dedd(1), l_dedgd(1,1), l_dedtau(1))

      case default
        cycle
      end select

      if(functl(ixc)%type == XC_EXCHANGE) then
        ex_per_vol(jj) = ex_per_vol(jj) + sum(l_dens(1:spin_channels)) * e
      else
        ec_per_vol(jj) = ec_per_vol(jj) + sum(l_dens(1:spin_channels)) * e
      end if

      ! store results
      dedd(jj,1:spin_channels) = dedd(jj,1:spin_channels) + l_dedd(1:spin_channels)

      if(gga.or.mgga) then
        dedgd(jj,:,1) = dedgd(jj,:,1) + M_TWO*l_vsigma(1)*gdens(jj,:,1)
        if(ispin == SPIN_POLARIZED) then
          dedgd(jj,:,1) = dedgd(jj,:,1) + l_vsigma(2)*gdens(jj,:,2)
          dedgd(jj,:,2) = dedgd(jj,:,2) + M_TWO*l_vsigma(3)*gdens(jj,:,2) + l_vsigma(2)*gdens(jj,:,1)
        end if
      end if

      if(functl(ixc)%family == XC_FAMILY_MGGA) then
        ! dedtau(jj,:)   = dedtau(jj,:)   + l_dedtau(:)
      end if

    end do functl_loop
  end do space_loop

  ! this has to be done in inverse order
  if(       mgga) call mgga_process()
  if(gga.or.mgga) call  gga_process()
  call  lda_process()

  ! integrate eneries per unit volume
  ex = dmf_integrate(gr%m, ex_per_vol)
  ec = dmf_integrate(gr%m, ec_per_vol)

  ! clean up allocated memory
  call  lda_end()
  if(gga.or.mgga) call  gga_end()
  if(       mgga) call mgga_end()

  call pop_sub()
  call profiling_out(C_PROFILING_XC_LOCAL)

contains

  ! ---------------------------------------------------------
  ! Takes care of the initialization of the LDA part of the functionals
  !   *) allocates density and dedd, and their local variants
  !   *) calculates the density taking into account nlcc and non-collinear spin
  subroutine lda_init()
    integer :: ii
    FLOAT   :: d(2), dtot, dpol

    ! allocate some general arrays
    ALLOCATE(dens(NP_PART, spin_channels), NP_PART*spin_channels)
    ALLOCATE(dedd(NP_PART, spin_channels), NP_PART*spin_channels)
    ALLOCATE(ex_per_vol(NP), NP)
    ALLOCATE(ec_per_vol(NP), NP)

    dens       = M_ZERO
    dedd       = M_ZERO
    ex_per_vol = M_ZERO
    ec_per_vol = M_ZERO

    !$omp parallel do private(d, dtot, dpol)
    do ii = 1, NP
      d(1:spin_channels) = rho(ii, 1:spin_channels)

      select case(ispin)
      case(UNPOLARIZED)
        dens(ii, 1) = max(d(1), M_ZERO)
      case(SPIN_POLARIZED)
        dens(ii, 1) = max(d(1), M_ZERO)
        dens(ii, 2) = max(d(2), M_ZERO)
      case(SPINORS)
        dtot = d(1) + d(2)
        dpol = sqrt((d(1) - d(2))**2 + &
          M_FOUR*(rho(ii, 3)**2 + rho(ii, 4)**2))
        dens(ii, 1) = max(M_HALF*(dtot + dpol), M_ZERO)
        dens(ii, 2) = max(M_HALF*(dtot - dpol), M_ZERO)
      end select
    end do
    !$omp end parallel do

  end subroutine lda_init


  ! ---------------------------------------------------------
  ! deallocates variables allocated in lda_init
  subroutine lda_end()
    deallocate(dens, dedd, ex_per_vol, ec_per_vol)
  end subroutine lda_end


  ! ---------------------------------------------------------
  ! calculates the LDA part of vxc, taking into account non-collinear spin
  subroutine lda_process()
    integer :: i, j
    FLOAT :: d(2), dpol, vpol

    if(ispin == SPINORS) then
      ! rotate back (do not need the rotation matrix for this).
      do i = 1, NP
        d(1:spin_channels) = rho(i, 1:spin_channels)

        dpol = sqrt((d(1) - d(2))**2 + &
          M_FOUR*(rho(i, 3)**2 + rho(i, 4)**2))
        vpol = (dedd(i, 1) - dedd(i, 2))*(d(1) - d(2))/(dpol + tiny)

        vxc(i, 1) = vxc(i, 1) + M_HALF*(dedd(i, 1) + dedd(i, 2) + vpol)
        vxc(i, 2) = vxc(i, 2) + M_HALF*(dedd(i, 1) + dedd(i, 2) - vpol)
        vxc(i, 3) = vxc(i, 3) + (dedd(i, 1) - dedd(i, 2))*rho(i, 3)/(dpol + tiny)
        vxc(i, 4) = vxc(i, 4) + (dedd(i, 1) - dedd(i, 2))*rho(i, 4)/(dpol + tiny)
      end do
    elseif(ispin == SPIN_POLARIZED) then
      ! Intel ifort 10.0 (Build 20070613) on Xeon produces a segfault on this line
      !vxc(1:NP, 1:2) = vxc(1:NP, 1:2) + dedd(1:NP, 1:2)
      ! but not when it is substituted by:
      do j = 1, 2
        do i = 1, NP
          vxc(i, j) = vxc(i, j) + dedd(i, j)
        end do
      end do
    else
      !vxc(1:NP, 1) = vxc(1:NP, 1) + dedd(1:NP, 1)
      do i = 1, NP
        vxc(i, 1) = vxc(i, 1) + dedd(i, 1)
      end do
    end if

  end subroutine lda_process


  ! ---------------------------------------------------------
  ! initialize GGAs
  !   *) allocates gradient of the density (gdens), dedgd, and its local variants
  !   *) calculates the gradient of the density
  subroutine gga_init()
    integer :: ii
    ! allocate variables
    ALLOCATE(gdens(NP,      3, spin_channels), NP     *3*spin_channels)
    ALLOCATE(dedgd(NP_PART, 3, spin_channels), NP_PART*3*spin_channels)
    gdens = M_ZERO
    dedgd = M_ZERO

    ! get gradient of the density
    do ii = 1, spin_channels
      call df_gradient(gr%sb, gr%f_der, dens(:, ii), gdens(:,:, ii))
    end do

    do ii = 1, 2
      if(functl(ii)%id == XC_GGA_XC_LB) then
        call xc_f90_gga_lb_set_params(functl(ii)%conf, &
          functl(ii)%LB94_modified, functl(ii)%LB94_threshold, ip, qtot)
      end if
    end do

  end subroutine gga_init


  ! ---------------------------------------------------------
  ! cleans up memory allocated in gga_init
  subroutine gga_end()
    deallocate(gdens, dedgd)
  end subroutine gga_end


  ! ---------------------------------------------------------
  ! calculates the GGA contribution to vxc
  subroutine gga_process()
    integer :: i, is
    FLOAT, allocatable :: gf(:,:)

    ! subtract the divergence of the functional derivative of Exc with respect to
    ! the gradient of the density.
    ALLOCATE(gf(NP, 1), NP*1)
    do is = 1, spin_channels
      call df_divergence(gr%sb, gr%f_der, dedgd(:,:,is), gf(:,1))
      call lalg_axpy(NP, -M_ONE, gf(:,1), dedd(:, is))
    end do
    deallocate(gf)

    ! If LB94, we can calculate an approximation to the energy from
    ! Levy-Perdew relation PRA 32, 2010 (1985)
    if(functl(1)%id == XC_GGA_XC_LB) then
      ALLOCATE(gf(NP, 3), NP*3)

      do is = 1, spin_channels
        call df_gradient(gr%sb, gr%f_der, dedd(:, is), gf(:,:))
        do i = 1, NP
          ex_per_vol(i) = ex_per_vol(i) - dens(i, is) * sum(gr%m%x(i,:)*gf(i,:))
        end do
      end do

      deallocate(gf)
    end if

  end subroutine gga_process


  ! ---------------------------------------------------------
  ! initialize meta-GGAs
  !   *) allocate the kinetic energy density, dedtau, and local variants
  !   *) calculates tau either from a GEA or from the orbitals
  subroutine mgga_init()
    integer :: i, is
    FLOAT   :: f, d
    FLOAT, allocatable :: n2dens(:)

    ALLOCATE(tau   (NP,      spin_channels), NP     *spin_channels)
    ALLOCATE(dedtau(NP_PART, spin_channels), NP_PART*spin_channels)
    tau    = M_ZERO
    dedtau = M_ZERO

    ASSERT(xcs%mGGA_implementation==1.or.xcs%mGGA_implementation==2)

    ! calculate tau
    select case(xcs%mGGA_implementation)
    case (1)  ! GEA implementation
      ALLOCATE(n2dens(NP), NP)
      f = CNST(3.0)/CNST(10.0) * (M_SIX*M_PI*M_PI)**M_TWOTHIRD

      do is = 1, spin_channels
        call df_laplacian(gr%sb, gr%f_der, dens(:,is), n2dens(:))

        do i = 1, NP
          d          = max(dens(i, is), CNST(1e-14))
          tau(i, is) = f * d**(M_FIVE/M_THREE) + &
            sum(gdens(i, :, is)**2)/(CNST(72.0)*d) + &
            n2dens(i)/M_SIX
        end do
      end do

      deallocate(n2dens)

    case(2) ! OEP implementation
      message(1) = 'Error: mgga_init: not yet implemented'
      call write_fatal(1)
    end select

  end subroutine mgga_init


  ! ---------------------------------------------------------
  ! clean up memory allocates in mgga_init
  subroutine mgga_end()
    deallocate(tau, dedtau)
  end subroutine mgga_end


  ! ---------------------------------------------------------
  ! calculate the mgga contribution to vxc
  subroutine mgga_process()
    integer :: i, is
    FLOAT   :: d, f
    FLOAT, allocatable :: gf(:)

    ASSERT(xcs%mGGA_implementation==1.or.xcs%mGGA_implementation==2)

    ! calculate tau
    select case(xcs%mGGA_implementation)
    case (1) ! GEA implementation
      f = CNST(3.0)/CNST(10.0) * (M_SIX*M_PI*M_PI)**M_TWOTHIRD
      ALLOCATE(gf(NP), NP)

      do is = 1, spin_channels
        call df_laplacian(gr%sb, gr%f_der, dedtau(:,is), gf(:))

        do i = 1, NP
          d = max(dens(i, is), CNST(1e-14))
          dedd(i, is) = dedd(i, is) + dedtau(i, is) * &
            (f*d**M_TWOTHIRD - sum(gdens(i, :, is)**2)/(CNST(72.0)*d*d))

          ! add the laplacian of the functional derivative of Exc with respect to tau
          dedd(i, is) = dedd(i, is) + dedtau(i, is) * &
            gf(i)/M_SIX

          dedgd(i, :, is) = dedgd(i, :, is) + dedtau(i, is) * &
            M_TWO*gdens(i, :, is)/(CNST(72.0)*d)
        end do
      end do

      deallocate(gf)

    case (2) ! OEP implementation
      message(1) = 'Error: mgga_process: not yet implemented'
      call write_fatal(1)
    end select
  end subroutine mgga_process

end subroutine xc_get_vxc

!! Local Variables:
!! mode: f90
!! coding: utf-8
!! End:
