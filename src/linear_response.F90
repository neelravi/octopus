!! Copyright (C) 2004 E.S. Kadantsev, M. Marques
!!
!! This program is free software; you can redistribute it and/or modify
!! it under the terms of the GNU General Public License as published by
!! the Free Software Foundation; either version 2, or (at your option)
!! any later version.
!!
!! This program is distributed in the hope that it will be useful,
!! but WITHOUT ANY WARRANTY; without even the implied warranty of
!! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!! GNU General Public License for more details.
!!
!! You should have received a copy of the GNU General Public License
!! along with this program; if not, write to the Free Software
!! Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
!! 02111-1307, USA.
!!
!! -*- coding: utf-8 mode: f90 -*-
!! $Id$

#include "global.h"

module linear_response_m
  use datasets_m
  use global_m
  use grid_m
  use hamiltonian_m
  use lib_basic_alg_m
  use lib_oct_m
  use lib_oct_parser_m
  use mesh_m
  use messages_m
  use output_m
  use preconditioners_m
  use states_m
  use units_m
  use xc_m

  implicit none

  private

  !the minimum value for a state to be considered occupied
  FLOAT, public, parameter :: lr_min_occ=CNST(1e-5)
  
  public :: &
       lr_t, &
       lr_init, &
       lr_copy, & 
       dlr_alloc_psi, &
       zlr_alloc_psi, & 
       dlr_output, & 
       zlr_output, & 
       dlr_orth_vector, & 
       zlr_orth_vector, & 
       dlr_build_dl_rho, & 
       zlr_build_dl_rho, &
       dlr_orth_response, &
       zlr_orth_response, &
       lr_dealloc, &
       lr_build_fxc, &
       lr_alloc_fHxc

  type lr_t
    !the number of lr wfs
    integer :: nst
     
    ! the real quantities
    FLOAT, pointer :: ddl_rho(:,:)     ! response of the density
    FLOAT, pointer :: ddl_psi(:,:,:,:) ! linear change of the real KS orbitals
    FLOAT, pointer :: ddl_Vhar(:)      ! linear change of the Hartree potential
    
    ! and the complex version
    CMPLX, pointer :: zdl_rho(:,:)     ! response of the density
    CMPLX, pointer :: zdl_psi(:,:,:,:) ! linear change of the complex KS orbitals
    CMPLX, pointer :: zdl_Vhar(:)      ! linear change of the Hartree potential

    FLOAT, pointer :: dl_Vxc(:,:,:)    ! linear change of the xc potential (fxc)
    
    !other observables
    CMPLX, pointer :: dl_j(:,:,:)     ! response of the current
    FLOAT, pointer :: ddl_de(:,:)     ! unnormalized elf
    FLOAT, pointer :: ddl_elf(:,:)    ! normalized elf
    CMPLX, pointer :: zdl_de(:,:)     ! unnormalized elf
    CMPLX, pointer :: zdl_elf(:,:)    ! normalized elf
    
  end type lr_t

contains

  ! ---------------------------------------------------------
  subroutine lr_init(lr)
    type(lr_t),        intent(out)   :: lr
    call push_sub('linear_response.lr_init')

    nullify(lr%ddl_rho, lr%ddl_psi, lr%ddl_Vhar, lr%dl_Vxc)
    nullify(lr%zdl_rho, lr%zdl_psi, lr%zdl_Vhar, lr%dl_Vxc)
    
    nullify(lr%dl_j, lr%ddl_de, lr%zdl_de, lr%ddl_elf, lr%zdl_elf)
    
  end subroutine lr_init

  ! ---------------------------------------------------------
  integer function dlr_alloc_psi(st, m, lr) result(r)
    type(states_t), intent(in) :: st
    type(mesh_t),   intent(in) :: m
    type(lr_t),  intent(inout) :: lr

    call push_sub('linear_response.dlr_alloc_psi')

    r = 1
    if(associated(lr%ddl_psi)) then 
      call pop_sub()
      return ! do nothing
    end if

    ALLOCATE(lr%ddl_psi(m%np_part, st%d%dim, st%nst, st%d%nspin),
         m%np_part*st%d%dim*st%nst*st%d%nspin)

    if(associated(lr%zdl_psi)) then
      r = 2
      lr%ddl_psi = real(lr%zdl_psi, REAL_PRECISION)
      deallocate(lr%zdl_psi)
      nullify(lr%zdl_psi)
    else
      r = -1
      lr%ddl_psi = M_ZERO
    end if

    call pop_sub()

  end function dlr_alloc_psi


  ! ---------------------------------------------------------
  integer function zlr_alloc_psi(st, m, lr) result(r)
    type(states_t), intent(in) :: st
    type(mesh_t),   intent(in) :: m
    type(lr_t),  intent(inout) :: lr

    call push_sub('linear_response.zlr_alloc_psi')

    r = 1
    if(associated(lr%zdl_psi)) then 
      call pop_sub()
      return ! do nothing
    end if

    ALLOCATE(lr%zdl_psi(m%np_part, st%d%dim, st%nst, st%d%nspin),
         m%np_part*st%d%dim*st%nst*st%d%nspin)

    if(associated(lr%ddl_psi)) then
      r = 2
      lr%zdl_psi = lr%ddl_psi
      deallocate(lr%ddl_psi)
      nullify(lr%ddl_psi)
    else
      r = -1
      lr%zdl_psi = M_z0
    end if

    call pop_sub()

  end function zlr_alloc_psi


  ! ---------------------------------------------------------
  subroutine lr_dealloc(lr)
    type(lr_t), intent(inout) :: lr

    if(associated(lr%ddl_rho)) then
      deallocate(lr%ddl_rho, lr%ddl_Vhar, lr%dl_Vxc)
      nullify   (lr%ddl_rho, lr%ddl_Vhar, lr%dl_Vxc)
    end if

    if(associated(lr%zdl_rho)) then
      deallocate(lr%zdl_rho, lr%zdl_Vhar, lr%dl_Vxc)
      nullify   (lr%zdl_rho, lr%zdl_Vhar, lr%dl_Vxc)
    end if

    if(associated(lr%ddl_psi)) then
      deallocate(lr%ddl_psi)
      nullify   (lr%ddl_psi)
    end if

    if(associated(lr%zdl_psi)) then
      deallocate(lr%zdl_psi)
      nullify   (lr%zdl_psi)
    end if

    if(associated(lr%dl_j)) deallocate(lr%dl_j)
    if(associated(lr%ddl_de)) deallocate(lr%ddl_de)
    if(associated(lr%ddl_elf)) deallocate(lr%ddl_elf)
    if(associated(lr%zdl_de)) deallocate(lr%zdl_de)
    if(associated(lr%zdl_elf)) deallocate(lr%zdl_elf)

  end subroutine lr_dealloc


  ! ---------------------------------------------------------
  subroutine lr_build_fxc(m, st, xcs, fxc)
    type(mesh_t),   intent(in)  :: m
    type(states_t), intent(in)  :: st
    type(xc_t),     intent(in)  :: xcs
    FLOAT,          intent(inout) :: fxc(:,:,:)

    FLOAT, allocatable :: rho(:, :)
    integer :: is

    call push_sub('linear_response.lr_build_fxc')

    ALLOCATE(rho(m%np, st%d%nspin), m%np*st%d%nspin)
    if(associated(st%rho_core)) then
      do is = 1, st%d%spin_channels
        rho(1:m%np, is) = st%rho(1:m%np, is) + st%rho_core(1:m%np)/st%d%spin_channels
      end do
    else
      rho(1:m%np, 1:st%d%nspin) = st%rho(1:m%np, 1:st%d%nspin)
    end if
    fxc = M_ZERO
    call xc_get_fxc(xcs, m, rho, st%d%ispin, fxc)
    deallocate(rho)

    call pop_sub()
  end subroutine lr_build_fxc


  ! ---------------------------------------------------------
  subroutine lr_alloc_fHxc(st, m, lr)
    type(states_t), intent(in)  :: st
    type(mesh_t),   intent(in)  :: m
    type(lr_t),     intent(inout) :: lr

    call push_sub('linear_response.lr_alloc_fHxc')

    ! allocate variables
    if (st%d%wfs_type == M_REAL) then
      ALLOCATE(lr%ddl_rho(m%np, st%d%nspin), m%np*st%d%nspin)
      ALLOCATE(lr%ddl_Vhar(m%np), m%np)
    else
      ALLOCATE(lr%zdl_rho(m%np, st%d%nspin), m%np*st%d%nspin)
      ALLOCATE(lr%zdl_Vhar(m%np), m%np)
    end if
    ALLOCATE(lr%dl_Vxc(m%np, st%d%nspin, st%d%nspin), m%np*st%d%nspin*st%d%nspin)

    call pop_sub()

  end subroutine lr_alloc_fHxc

  subroutine lr_copy(st, m, src, dest)
    type(states_t), intent(in) :: st
    type(mesh_t),   intent(in) :: m
    type(lr_t),     intent(in) :: src
    type(lr_t),     intent(inout) :: dest

    integer :: ik, idim, ist

    do ik = 1, st%d%nspin
      
      if( wfs_are_complex(st)) then
        call lalg_copy(m%np, src%zdl_rho(:, ik), dest%zdl_rho(:, ik))
      else
        call lalg_copy(m%np, src%ddl_rho(:, ik), dest%ddl_rho(:, ik))
      end if
    
      do ist = 1, st%nst
        do idim = 1, st%d%dim
          if( wfs_are_complex(st)) then
            call lalg_copy(m%np_part, src%zdl_psi(:, idim, ist, ik), dest%zdl_psi(:, idim, ist, ik))
          else 
            call lalg_copy(m%np_part, src%ddl_psi(:, idim, ist, ik), dest%ddl_psi(:, idim, ist, ik))
          end if
        end do
      end do

   end do

  end subroutine lr_copy

#include "undef.F90"
#include "real.F90"

#include "linear_response_inc.F90"
#include "linear_response_out.F90" 

#include "undef.F90"
#include "complex.F90"

#include "linear_response_inc.F90"
#include "linear_response_out.F90" 

end module linear_response_m
