!! Copyright (C) 2004 Xavier Andrade, Eugene S. Kadantsev (ekadants@mjs1.phy.queensu.ca)
!!
!! This program is free software; you can redistribute it and/or modify
!! it under the terms of the GNU General Public License as published by
!! the Free Software Foundation; either version 2, or (at your option)
!! any later version.
!!
!! This program is distributed in the hope that it will be useful,
!! but WITHOUT ANY WARRANTY; without even the implied warranty of
!! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!! GNU General Public License for more details.
!!
!! You should have received a copy of the GNU General Public License
!! along with this program; if not, write to the Free Software
!! Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
!! 02111-1307, USA.
!!
!! $Id: em_resp.F90 2548 2006-11-06 21:42:27Z xavier $


! ---------------------------------------------------------
! WARNING: This subroutine is clearly broken after the changes
! to include temperature in linear response
subroutine X(lr_calc_elf)(st, gr, lr, lr_m)
  type(states_t),   intent(inout) :: st
  type(grid_t),     intent(inout) :: gr
  type(lr_t),       intent(inout) :: lr
  type(lr_t), optional, intent(inout) :: lr_m !when this argument is present, we are doing dynamical response

  integer :: i, is, ist, idim, ik

  R_TYPE, allocatable :: gpsi(:,:), gdl_psi(:,:), gdl_psi_m(:,:)
  FLOAT,  allocatable :: rho(:), grho(:,:)
  R_TYPE, allocatable :: dl_rho(:), gdl_rho(:,:)
  FLOAT,  allocatable :: elf(:,:), de(:,:), current(:, :, :)
  FLOAT :: dl_d0, d0
  FLOAT :: f, s

  FLOAT, parameter :: dmin = CNST(1e-10)
  FLOAT :: ik_weight

  call push_sub('em_resp_inc.Xcalc_lr_elf')

  ASSERT(.false.)

  SAFE_ALLOCATE(   gpsi(1:gr%mesh%np, 1:gr%mesh%sb%dim))
  SAFE_ALLOCATE(gdl_psi(1:gr%mesh%np, 1:gr%mesh%sb%dim))

  if(present(lr_m)) then
    SAFE_ALLOCATE(gdl_psi_m(1:gr%mesh%np, 1:gr%mesh%sb%dim))
  end if

  SAFE_ALLOCATE(   grho(1:gr%mesh%np, 1:gr%mesh%sb%dim))
  SAFE_ALLOCATE(gdl_rho(1:gr%mesh%np, 1:gr%mesh%sb%dim))

  SAFE_ALLOCATE(   rho(1:gr%mesh%np_part))
  SAFE_ALLOCATE(dl_rho(1:gr%mesh%np_part))

  SAFE_ALLOCATE(   elf(1:gr%mesh%np, 1:st%d%nspin))
  SAFE_ALLOCATE(    de(1:gr%mesh%np, 1:st%d%nspin))
  SAFE_ALLOCATE(current(1:gr%mesh%np_part, 1:gr%mesh%sb%dim, 1:st%d%nspin))

  if( .not. associated(lr%X(dl_de)) ) then
    SAFE_ALLOCATE(lr%X(dl_de)(1:gr%mesh%np, 1:st%d%nspin)) 
  end if
  if( .not. associated(lr%X(dl_elf))) then
    SAFE_ALLOCATE(lr%X(dl_elf)(1:gr%mesh%np, 1:st%d%nspin))
  end if

  !calculate the gs elf
  call elf_calc(st, gr, elf, de)

  !calculate current and its variation
  if(st%wfs_type == M_CMPLX) then 
    call calc_physical_current(gr, st, current)
    if(present(lr_m)) then 
      call lr_calc_current(st, gr, lr, lr_m)
    else
      call lr_calc_current(st, gr, lr)
    end if
  end if

  ! single or double occupancy
  if(st%d%nspin == 1) then
    s = M_TWO
  else
    s = M_ONE
  end if

  lr%X(dl_de) = M_ZERO

  do is = 1, st%d%nspin
    rho  = M_ZERO
    grho = M_ZERO

    dl_rho  = M_ZERO
    gdl_rho = M_ZERO

    !first we calculate the densities and their gradients; this could
    !be done directly, but it is less precise numerically
    do ik = is, st%d%nik, st%d%nspin
      do ist = 1, st%nst
        ik_weight = st%d%kweights(ik)*st%occ(ist, ik)/s

        do idim = 1, st%d%dim

          call X(derivatives_grad)(gr%der, st%X(psi)   (:, idim, ist, is), gpsi)
          call X(derivatives_grad)(gr%der, lr%X(dl_psi)(:, idim, ist, is), gdl_psi)

          ! sum over states to obtain the spin-density
          rho(1:gr%mesh%np) = rho(1:gr%mesh%np) + ik_weight*abs(st%X(psi)(1:gr%mesh%np, idim, ist, is))**2

          !the gradient of the density
          do i = 1, gr%mesh%sb%dim
            grho(1:gr%mesh%np, i) = grho(1:gr%mesh%np, i) + &
                 ik_weight*M_TWO*R_REAL(R_CONJ(st%X(psi)(1:gr%mesh%np, idim, ist, is))*gpsi(1:gr%mesh%np, i))
          end do

          !the variation of the density and its gradient

          if(present(lr_m)) then 

            call X(derivatives_grad)(gr%der, lr_m%X(dl_psi)(:, idim, ist, is), gdl_psi_m)

            dl_rho(1:gr%mesh%np) = dl_rho(1:gr%mesh%np) + ik_weight * ( &
                 R_CONJ(st%X(psi)(1:gr%mesh%np, idim, ist, is)) * lr%X(dl_psi)(1:gr%mesh%np, idim, ist, is)+ & 
                 st%X(psi)(1:gr%mesh%np, idim, ist, is) * R_CONJ(lr_m%X(dl_psi)(1:gr%mesh%np, idim, ist, is)) )

            do i=1, gr%mesh%sb%dim

              gdl_rho(1:gr%mesh%np,i) = gdl_rho(1:gr%mesh%np,i) + ik_weight * ( &
                   R_CONJ(st%X(psi)(1:gr%mesh%np, idim, ist, is)) * gdl_psi(1:gr%mesh%np,i) +      &
                   R_CONJ(gpsi(1:gr%mesh%np,i))* lr%X(dl_psi)(1:gr%mesh%np, idim, ist, is)  +      &
                   st%X(psi)(1:gr%mesh%np, idim, ist, is) * R_CONJ(gdl_psi_m(1:gr%mesh%np,i)) +      &
                   gpsi(1:gr%mesh%np,i) * R_CONJ(lr_m%X(dl_psi)(1:gr%mesh%np, idim, ist, is))  )

            end do

          else
            
            dl_rho(1:gr%mesh%np) = dl_rho(1:gr%mesh%np) + ik_weight*M_TWO* &
                 R_REAL(R_CONJ(st%X(psi)(1:gr%mesh%np, idim, ist, is))*lr%X(dl_psi)(1:gr%mesh%np, idim, ist, is))

            do i=1,gr%mesh%sb%dim
              gdl_rho(1:gr%mesh%np,i) = gdl_rho(1:gr%mesh%np,i) + ik_weight * M_TWO * ( &
                   R_CONJ(st%X(psi)(1:gr%mesh%np, idim, ist, is)) * gdl_psi(1:gr%mesh%np, i) + &
                   gpsi(1:gr%mesh%np, i)*R_CONJ(lr%X(dl_psi)(1:gr%mesh%np, idim, ist, is))  )
            end do

          end if

        end do !idim
      end do !ist
    end do !ik

    !now we start to calculate the elf

    !first the term that depends on the orbitals
    !this is the only term that is different for the dynamical case
    do ik = is, st%d%nik, st%d%nspin
      do ist = 1, st%nst
        ik_weight = st%d%kweights(ik)*st%occ(ist, ik)/s
        do idim = 1, st%d%dim

          call X(derivatives_grad)(gr%der, st%X(psi)   (:, idim, ist, is), gpsi)
          call X(derivatives_grad)(gr%der, lr%X(dl_psi)(:, idim, ist, is), gdl_psi)

          if(present(lr_m)) then 

            call X(derivatives_grad)(gr%der, lr_m%X(dl_psi)(:, idim, ist, is), gdl_psi_m)

            do i = 1, gr%mesh%np
              lr%X(dl_de)(i, is) = lr%X(dl_de)(i, is) + &
                   dl_rho(i)*ik_weight*sum(R_ABS(gpsi(i, 1:gr%mesh%sb%dim))**2) + &
                   rho(i)*ik_weight*&
                   sum(R_CONJ(gpsi(i,1:gr%mesh%sb%dim))*gdl_psi(i, 1:gr%mesh%sb%dim) + &
                   gpsi(i,1:gr%mesh%sb%dim)*R_CONJ(gdl_psi_m(i, 1:gr%mesh%sb%dim)))
            end do

          else 

            do i = 1, gr%mesh%np
              lr%X(dl_de)(i, is) = lr%X(dl_de)(i, is) + &
                   dl_rho(i)*ik_weight*sum(R_ABS(gpsi(i, 1:gr%mesh%sb%dim))**2) + &
                   rho(i)*ik_weight*M_TWO*(sum(R_CONJ(gpsi(i, 1:gr%mesh%sb%dim))*gdl_psi(i, 1:gr%mesh%sb%dim)))
            end do

          end if

        end do
      end do
    end do

    !the density term
    do i = 1, gr%mesh%np
      if(abs(st%rho(i, is)) >= dmin) then
        lr%X(dl_de)(i, is) = lr%X(dl_de)(i, is) - M_HALF*sum(grho(i, 1:gr%mesh%sb%dim)*gdl_rho(i, 1:gr%mesh%sb%dim))
      end if
    end do

    !the current term
    if(st%wfs_type == M_CMPLX) then       
      do i = 1, gr%mesh%np
        if(abs(st%rho(i, is)) >= dmin) then
          lr%X(dl_de)(i, is) = lr%X(dl_de)(i, is) + M_TWO*sum(current(i, 1:gr%mesh%sb%dim, is)*lr%dl_j(i, 1:gr%mesh%sb%dim, is))
        end if
      end do
    end if

    !now the normalization 
    f = M_THREE/M_FIVE*(M_SIX*M_PI**2)**M_TWOTHIRD
    do i = 1, gr%mesh%np

      if(abs(st%rho(i, is)) >= dmin) then
        d0    = f*rho(i)**(M_EIGHT/M_THREE)
        dl_d0 = M_EIGHT/M_THREE*f*dl_rho(i)*rho(i)**(M_FIVE/M_THREE)

        lr%X(dl_elf)(i, is) = &
             M_TWO*d0*dl_d0/(d0**2 + de(i, is)**2)*(M_ONE - elf(i,is)) &
             - M_TWO*de(i, is)*lr%X(dl_de)(i, is)/(d0**2 + de(i, is)**2)*elf(i, is)
      else
        lr%X(dl_elf)(i, is) = M_ZERO
      end if

    end do

  end do

  SAFE_DEALLOCATE_A(current)
  SAFE_DEALLOCATE_A(gpsi)
  SAFE_DEALLOCATE_A(gdl_psi)
  if(present(lr_m)) then
    SAFE_DEALLOCATE_A(gdl_psi_m)
  end if

  SAFE_DEALLOCATE_A(rho)
  SAFE_DEALLOCATE_A(dl_rho)

  SAFE_DEALLOCATE_A(grho)
  SAFE_DEALLOCATE_A(gdl_rho)

  SAFE_DEALLOCATE_A(elf)
  SAFE_DEALLOCATE_A(de)

  call pop_sub()

end subroutine X(lr_calc_elf)



! ---------------------------------------------------------
subroutine X(calc_polarizability_periodic)(sys, em_lr, kdotp_lr, nsigma, zpol, ndir)
  type(system_t),         intent(inout) :: sys
  type(lr_t),             intent(inout) :: em_lr(:,:)
  type(lr_t),             intent(inout) :: kdotp_lr(:)
  integer,                intent(in)    :: nsigma
  CMPLX,                  intent(out)   :: zpol(1:MAX_DIM, 1:MAX_DIM)
  integer, optional,      intent(in)    :: ndir

  integer :: dir1, dir2, ndir_, ist, ik, idim
  CMPLX :: term, subterm
  type(mesh_t), pointer :: m
  m => sys%gr%mesh

  call push_sub('em_resp_calc.Xcalc_polarizability_periodic')

  ndir_ = sys%gr%mesh%sb%dim
  if(present(ndir)) ndir_ = ndir

  ! alpha_ij(w) = -e sum(m occ, k) [(<u_mk(0)|-id/dk_i)|u_mkj(1)(w)> + <u_mkj(1)(-w)|(-id/dk_i|u_mk(0)>)]
  ! Smearing is not implemented here yet?

  do dir1 = 1, ndir_
    do dir2 = 1, sys%gr%sb%dim

      zpol(dir1, dir2) = M_ZERO

      do ik = 1, sys%st%d%nik
        term = M_ZERO
        do ist = 1, sys%st%nst
          do idim = 1, sys%st%d%dim
            subterm = - X(mf_dotp)(m, kdotp_lr(dir1)%X(dl_psi)(1:m%np, idim, ist, ik), &
              em_lr(dir2, 1)%X(dl_psi)(1:m%np, idim, ist, ik))
            term = term + subterm

            if(nsigma == 1) then
              term = term + conjg(subterm)
            else
              term = term - X(mf_dotp)(m, em_lr(dir2, 2)%X(dl_psi)(1:m%np, idim, ist, ik), & 
                kdotp_lr(dir1)%X(dl_psi)(1:m%np, idim, ist, ik))
            end if
          enddo
        enddo

        zpol(dir1, dir2) = zpol(dir1, dir2) + &
          term * sys%st%d%kweights(ik) * sys%st%smear%el_per_state
      enddo
    enddo
  enddo

  call pop_sub()

end subroutine X(calc_polarizability_periodic)


! ---------------------------------------------------------
subroutine X(calc_polarizability_finite)(sys, hm, lr, nsigma, perturbation, zpol, ndir)
  type(system_t),         intent(inout) :: sys
  type(hamiltonian_t),    intent(inout) :: hm
  type(lr_t),             intent(inout) :: lr(:,:)
  integer,                intent(in)    :: nsigma
  type(pert_t),           intent(inout) :: perturbation
  CMPLX,                  intent(out)   :: zpol(1:MAX_DIM, 1:MAX_DIM)
  integer, optional,      intent(in)    :: ndir

  integer :: dir1, dir2, ndir_

  call push_sub('em_resp_calc_inc.Xcalc_polarizability_finite')

  ndir_ = sys%gr%mesh%sb%dim
  if(present(ndir)) ndir_ = ndir

  ! alpha_ij(w) = - sum(m occ) [<psi_m(0)|r_i|psi_mj(1)(w)> + <psi_mj(1)(-w)|r_i|psi_m(0)>]
  ! minus sign is from electronic charge -e

  do dir1 = 1, ndir_
    do dir2 = 1, sys%gr%sb%dim
      call pert_setup_dir(perturbation, dir1)
      zpol(dir1, dir2) = -X(pert_expectation_value)(perturbation, sys%gr, sys%geo, hm, &
        sys%st, sys%st%X(psi), lr(dir2, 1)%X(dl_psi))

      if(nsigma == 1) then
        zpol(dir1, dir2) = zpol(dir1, dir2) + R_CONJ(zpol(dir1, dir2))
      else
        zpol(dir1, dir2) = zpol(dir1, dir2) &
          - X(pert_expectation_value)(perturbation, sys%gr, sys%geo, hm, &
          sys%st, lr(dir2, 2)%X(dl_psi), sys%st%X(psi))
      end if
    end do
  end do

  call pop_sub()

end subroutine X(calc_polarizability_finite)


! ---------------------------------------------------------
subroutine X(lr_calc_susceptibility)(sys, hm, lr, nsigma, perturbation, chi_para, chi_dia)
  type(system_t),         intent(inout) :: sys
  type(hamiltonian_t),    intent(inout) :: hm
  type(lr_t),             intent(inout) :: lr(:,:)
  integer,                intent(in)    :: nsigma
  type(pert_t),           intent(inout) :: perturbation
  CMPLX,                  intent(out)   :: chi_para(:,:), chi_dia(:,:)

  integer :: dir1, dir2
  R_TYPE  :: trace

  call push_sub('em_resp_calc_inc.Xlr_calc_susceptibility')

  chi_para = M_ZERO
  chi_dia  = M_ZERO

  do dir1 = 1, sys%gr%sb%dim
    do dir2 = 1, sys%gr%sb%dim

      call pert_setup_dir(perturbation, dir1, dir2)

      trace = X(pert_expectation_value)(perturbation, sys%gr, sys%geo, hm, sys%st, sys%st%X(psi), lr(dir2, 1)%X(dl_psi))
      
      if (nsigma == 1) then 
        trace = trace + R_CONJ(trace)
      else
        trace = trace + &
          X(pert_expectation_value)(perturbation, sys%gr, sys%geo, hm, sys%st, lr(dir2, 2)%X(dl_psi), sys%st%X(psi))
      end if
     
      ! first the paramagnetic term 
      chi_para(dir1, dir2) = chi_para(dir1, dir2) + trace

      chi_dia(dir1, dir2) = chi_dia(dir1, dir2) + &
        X(pert_expectation_value)(perturbation, sys%gr, sys%geo, hm, sys%st, sys%st%X(psi), sys%st%X(psi), pert_order=2)

    end do
  end do


  ! We now add the minus sign from the definition of the susceptibility (chi = -d^2 E /d B^2)
  chi_para(:,:) = -chi_para(:,:)/P_C**2
  chi_dia (:,:) = -chi_dia (:,:)/P_C**2

#if defined(R_TREAL)
  ! When using real wave-functions there is an extra factor of (-i)*(-i) = -1
  chi_para(:,:) = -chi_para(:,:)
#endif

  call pop_sub()

end subroutine X(lr_calc_susceptibility)

! ---------------------------------------------------------
subroutine X(lr_calc_beta) (sh, sys, hm, em_lr, dipole, beta, kdotp_lr, kdotp_em_lr)
! Note: correctness for spinors is unclear
! See (16) in X Andrade et al., J. Chem. Phys. 126, 184106 (2006) for finite systems
! and (10) in A Dal Corso et al., Phys. Rev. B 15, 15638 (1996) for periodic systems
! Supply only em_lr for finite systems, and both kdotp_lr and kdotp_em_lr for periodic
! em_lr(dir, sigma, omega) = electric perturbation of ground-state wavefunctions
! kdotp_lr(dir) = kdotp perturbation of ground-state wavefunctions
! kdotp_em_lr(dir1, dir2, sigma, omega) = kdotp perturbation of electric-perturbed wfns
  type(sternheimer_t),     intent(inout) :: sh
  type(system_t), target,  intent(inout) :: sys
  type(hamiltonian_t),     intent(inout) :: hm
  type(lr_t),              intent(inout) :: em_lr(:,:,:)
  type(pert_t),            intent(inout) :: dipole
  CMPLX,                   intent(out)   :: beta(1:MAX_DIM, 1:MAX_DIM, 1:MAX_DIM)
  type(lr_t),    optional, intent(in)    :: kdotp_lr(:)
  type(lr_t),    optional, intent(in)    :: kdotp_em_lr(:,:,:,:)

  type(states_t), pointer :: st
  type(mesh_t),   pointer :: mesh

  integer :: ifreq, jfreq, isigma, idim, ispin, np, ndim, idir, ist, ik
  integer :: ii, jj, kk, iperm, op_sigma, idim2, ist2, ip
  integer :: perm(1:3), u(1:3), w(1:3), ijk(1:3)

  R_TYPE, allocatable :: hvar(:, :, :, :, :, :)
  R_TYPE, allocatable :: tmp(:), ppsi(:, :), me010(:, :, :, :, :)
  type(matrix_t), allocatable :: me11(:, :, :, :, :, :)
  FLOAT,  allocatable :: rho(:,:), kxc(:, :, :, :)
  R_TYPE, allocatable :: hpol_density(:)

  call push_sub('em_resp_inc.Xlr_calc_beta')

  call profiling_in(beta_prof, "CALC_BETA")

  np   =  sys%gr%mesh%np
  ndim =  sys%gr%sb%dim
  st   => sys%st
  mesh => sys%gr%mesh

  ASSERT(present(kdotp_lr) .eqv. present(kdotp_em_lr))
  ! either both are absent for finite, or both present for periodic

  !calculate kxc, the derivative of fxc
  SAFE_ALLOCATE(kxc(1:np, 1:st%d%nspin, 1:st%d%nspin, 1:st%d%nspin))
  kxc = M_ZERO

  SAFE_ALLOCATE(rho(1:np, 1:st%d%nspin))
  call states_total_density(st, mesh, rho)

  call xc_get_kxc(sys%ks%xc, mesh, rho, st%d%ispin, kxc)
  SAFE_DEALLOCATE_A(rho)

  SAFE_ALLOCATE(tmp(1:np))
  SAFE_ALLOCATE(ppsi(1:np, 1:st%d%dim))
  SAFE_ALLOCATE(hvar(1:np, 1:st%d%nspin, 1:2, 1:st%d%dim, 1:ndim, 1:3))
  SAFE_ALLOCATE(hpol_density(1:np))
  SAFE_ALLOCATE(me010(1:st%nst, 1:st%nst, 1:MAX_DIM, 1:3, st%d%kpt%start:st%d%kpt%end))
  SAFE_ALLOCATE(me11(1:MAX_DIM, 1:MAX_DIM, 1:3, 1:3, 1:2, st%d%kpt%start:st%d%kpt%end))

  do ifreq = 1, 3
    do idir = 1, ndim
      do idim = 1, st%d%dim
        call X(sternheimer_calc_hvar)(sh, sys, hm, em_lr(idir, :, ifreq), 2, hvar(:, :, :, idim, idir, ifreq))
      end do !idim
    end do !idir
  end do !ifreq

  call get_matrix_elements()

  beta(1:MAX_DIM, 1:MAX_DIM, 1:MAX_DIM) = M_ZERO

  do ii = 1, ndim
    do jj = 1, ndim
      do kk = 1, ndim

        ijk = (/ ii, jj, kk /)

        do isigma = 1, 2

          op_sigma = 2 
          if(isigma == 2) op_sigma = 1

          do iperm = 1, 6

            call get_permutation(iperm, perm)

            u(1:3) = ijk(perm(1:3))
            w(1:3) = perm(1:3)

            do ik = st%d%kpt%start, st%d%kpt%end
              do ist = 1, st%nst
                do idim = 1, st%d%dim

                  ispin = states_dim_get_spin_index(sys%st%d, ik)

                  if (present(kdotp_em_lr)) then
                    tmp(1:np) = - M_zI * kdotp_em_lr(u(2), u(3), isigma, w(3))%X(dl_psi)(1:np, idim, ist, ik)
                  else
                    call pert_setup_dir(dipole, u(2))
                    call X(pert_apply) &
                      (dipole, sys%gr, sys%geo, hm, ik, em_lr(u(3), isigma, w(3))%X(dl_psi)(:, idim, ist, ik), tmp)
                  endif

                  do ip = 1, np
                    tmp(ip) = tmp(ip) + R_REAL(hvar(ip, ispin, isigma, idim, u(2), w(2))) &
                      * em_lr(u(3), isigma, w(3))%X(dl_psi)(ip, idim, ist, ik)
                  enddo

                  beta(ii, jj, kk) = beta(ii, jj, kk) &
                    - M_HALF * st%d%kweights(ik) * st%smear%el_per_state &
                    * X(mf_dotp)(mesh, em_lr(u(1), op_sigma, w(1))%X(dl_psi)(1:np, idim, ist, ik), tmp(1:np))
                  
                  do ist2 = 1, st%nst
                    beta(ii, jj, kk) = beta(ii, jj, kk) + & 
                      M_HALF * st%d%kweights(ik)*st%smear%el_per_state*me010(ist2, ist, u(2), w(2), ik)*&
                      me11(u(1), u(3), w(1), w(3), isigma, ik)%X(matrix)(ist, ist2)
                  end do ! ist2

                end do !idim
              end do !ist
            end do !ik

            if(sternheimer_add_fxc(sh)) then 
              do ip = 1, np 
                hpol_density(ip) = kxc(ip, 1, 1, 1) & 
                  * sum(em_lr(u(1), isigma, w(1))%X(dl_rho)(ip, 1:st%d%nspin)) & 
                  * sum(em_lr(u(2), isigma, w(2))%X(dl_rho)(ip, 1:st%d%nspin)) & 
                  * sum(em_lr(u(3), isigma, w(3))%X(dl_rho)(ip, 1:st%d%nspin)) & 
                  / CNST(6.0) 
              end do
            end if

            beta(ii, jj, kk) = beta(ii, jj, kk) - M_HALF * X(mf_integrate)(mesh, hpol_density)


          end do ! iperm

        end do !isigma

      end do !kk
    end do !jj
  end do !ii

  do ik = st%d%kpt%start, st%d%kpt%end
    do ii = 1, MAX_DIM
      do jj = 1, MAX_DIM
        do ifreq = 1, 3
          do jfreq = 1, 3
            do isigma = 1,2
              SAFE_DEALLOCATE_P(me11(ii, jj, ifreq, jfreq, isigma, ik)%X(matrix))
            end do
          end do
        end do
      end do
    end do
  end do

  SAFE_DEALLOCATE_A(hpol_density)
  SAFE_DEALLOCATE_A(tmp)
  SAFE_DEALLOCATE_A(hvar)
  SAFE_DEALLOCATE_A(me010)
  SAFE_DEALLOCATE_A(me11)
  SAFE_DEALLOCATE_A(hvar)

  call pop_sub()

  call profiling_out(beta_prof)

contains

  subroutine get_permutation(i, p)
    integer, intent(in)  :: i
    integer, intent(out) :: p(1:3)

    ASSERT( i>=1 .and. i <= 6)

    select case(i)

    case(1) ; p(1)=1 ; p(2)=2 ; p(3)=3
    case(2) ; p(1)=2 ; p(2)=3 ; p(3)=1
    case(3) ; p(1)=3 ; p(2)=1 ; p(3)=2
    case(4) ; p(1)=3 ; p(2)=2 ; p(3)=1
    case(5) ; p(1)=1 ; p(2)=3 ; p(3)=2
    case(6) ; p(1)=2 ; p(2)=1 ; p(3)=3

    end select

  end subroutine get_permutation

  subroutine get_matrix_elements()

    do ik = st%d%kpt%start, st%d%kpt%end
      ispin = states_dim_get_spin_index(sys%st%d, ik)
      do ist = 1, st%nst
        do ist2 = 1, st%nst
          do ii = 1, MAX_DIM
            do ifreq = 1, 3

              if (present(kdotp_lr)) then
                forall (idim = 1:st%d%dim, ip = 1:np) ppsi(ip, idim) = - M_zI * kdotp_lr(ii)%X(dl_psi)(ip, idim, ist, ik)
              else
                do idim = 1, st%d%dim
                  call pert_setup_dir(dipole, ii)
                  call X(pert_apply)(dipole, sys%gr, sys%geo, hm, ik, st%X(psi)(:, idim, ist, ik), ppsi(:, idim))
                end do
              endif

              isigma = 1
              forall (idim = 1:st%d%dim, ip = 1:np)
                ppsi(ip, idim) = ppsi(ip, idim) + R_REAL(hvar(ip, ispin, isigma, idim, ii, ifreq))*st%X(psi)(ip, idim, ist, ik)
              end forall

              me010(ist2, ist, ii, ifreq, ik) = X(mf_dotp)(mesh, st%d%dim, st%X(psi)(:, :, ist2, ik), ppsi)

            end do
          end do
        end do
      end do
    end do

    do ik = st%d%kpt%start, st%d%kpt%end
      do ii = 1, MAX_DIM
        do jj = 1, MAX_DIM
          do ifreq = 1, 3
            do jfreq = 1, 3
              do isigma = 1, 2
                op_sigma = 2 
                if(isigma == 2) op_sigma = 1

                SAFE_ALLOCATE(me11(ii, jj, ifreq, jfreq, isigma, ik)%X(matrix)(1:st%nst, 1:st%nst))
                call states_blockt_mul(mesh, st, st%st_start, st%st_end, st%st_start, st%st_end, &
                  em_lr(ii, op_sigma, ifreq)%X(dl_psi)(:, :, :, ik), &
                  em_lr(jj, isigma, jfreq)%X(dl_psi)(:, :, :, ik), &
                  me11(ii, jj, ifreq, jfreq, isigma, ik)%X(matrix))

              end do
            end do
          end do
        end do
      end do
    end do

  end subroutine get_matrix_elements

end subroutine X(lr_calc_beta)

! ---------------------------------------------------------
subroutine X(lr_calc_2np1) (sh, hm, st, geo, gr, lr1, lr2, lr3, pert1, pert2, pert3, val)
  type(sternheimer_t),     intent(inout) :: sh
  type(hamiltonian_t),     intent(inout) :: hm
  type(states_t),          intent(in)    :: st
  type(geometry_t),        intent(in)    :: geo
  type(grid_t),            intent(inout) :: gr
  type(lr_t),              intent(in)    :: lr1
  type(lr_t),              intent(in)    :: lr2
  type(lr_t),              intent(in)    :: lr3
  type(pert_t),            intent(in)    :: pert1
  type(pert_t),            intent(in)    :: pert2
  type(pert_t),            intent(in)    :: pert3
  R_TYPE,                  intent(out)   :: val

  integer :: ik, ist, jst, idim
  R_TYPE :: term
  R_TYPE, allocatable :: tmp(:, :), me23(:, :)

  SAFE_ALLOCATE(tmp(1:gr%mesh%np, 1:st%d%dim))
  SAFE_ALLOCATE(me23(1:st%nst, 1:st%nst))

  val = R_TOTYPE(M_ZERO)
  idim = 1

  do ik = st%d%kpt%start, st%d%kpt%end

    !precalculate these matrix elements (it could be done in a better way)
    do ist = 1, st%nst
      do jst = 1, st%nst
        me23(ist, jst) = X(mf_dotp)(gr%mesh, st%d%dim, lr2%X(dl_psi)(:, :, ist, ik), lr3%X(dl_psi)(:, :, jst, ik))
      end do
    end do

    do ist = 1, st%nst
      
      call X(pert_apply)(pert2, gr, geo, hm, ik, lr3%X(dl_psi)(:, idim, ist, ik), tmp(:, idim))
      term = X(mf_dotp)(gr%mesh, st%d%dim, lr1%X(dl_psi)(:, :, ist, ik), tmp)
      
      do jst = 1, st%nst
        call X(pert_apply)(pert1, gr, geo, hm, ik, st%X(psi)(:, idim, jst, ik), tmp(:, idim))
        term = term - X(mf_dotp)(gr%mesh, st%d%dim, st%X(psi)(:, :, jst, ik), tmp)*me23(jst, ist)
      end do

      val = val + st%d%kweights(ik)*st%smear%el_per_state*term
      
    end do
  end do
  
  SAFE_DEALLOCATE_A(tmp)
  SAFE_DEALLOCATE_A(me23)

end subroutine X(lr_calc_2np1)

!! Local Variables:
!! mode: f90
!! coding: utf-8
!! End:
