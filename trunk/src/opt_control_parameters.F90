!! Copyright (C) 2002-2006 M. Marques, A. Castro, A. Rubio, G. Bertsch
!!
!! This program is free software; you can redistribute it and/or modify
!! it under the terms of the GNU General Public License as published by
!! the Free Software Foundation; either version 2, or (at your option)
!! any later version.
!!
!! This program is distributed in the hope that it will be useful,
!! but WITHOUT ANY WARRANTY; without even the implied warranty of
!! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!! GNU General Public License for more details.
!!
!! You should have received a copy of the GNU General Public License
!! along with this program; if not, write to the Free Software
!! Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
!! 02111-1307, USA.
!!
!! $Id: opt_control.F90 2870 2007-04-28 06:26:47Z acastro $


  ! ---------------------------------------------------------
  subroutine parameters_init(cp, no_parameters, td)
    type(oct_control_parameters_t), intent(inout) :: cp
    integer, intent(in) :: no_parameters   
    type(td_t),   intent(in) :: td
    integer :: j

    call push_sub('opt_control_parameters.parameters_init')

    cp%no_parameters = no_parameters
    cp%dt = td%dt
    cp%ntiter = 2*td%max_iter
    ALLOCATE(cp%f(cp%no_parameters), cp%no_parameters)
    do j = 1, cp%no_parameters
      call tdf_init_numerical(cp%f(j), cp%ntiter, M_HALF*cp%dt)
    end do

    call oct_read_laserpol(cp%laser_pol)

  end subroutine parameters_init


  ! ---------------------------------------------------------
  subroutine parameters_set(cp, gr, ep)
    type(oct_control_parameters_t), intent(inout) :: cp
    type(grid_t), intent(in) :: gr
    type(epot_t), intent(in) :: ep

    call push_sub('opt_control_parameters.parameters_set')

    ! WARNING: This assumes only one laser, and only one control parameter. Obviously 
    ! this has to be improved.
    cp%f(1) = ep%lasers(1)%f
    if(cp%no_parameters > 1) then
      write(message(1),'(a)') 'Internal Error at parameters_set.'
      call write_fatal(1)
    end if

    call pop_sub()
  end subroutine parameters_set


  ! ---------------------------------------------------------
  subroutine parameters_to_h(cp, gr, td, ep)
    type(oct_control_parameters_t), intent(in) :: cp
    type(grid_t), intent(in) :: gr
    type(td_t),   intent(in) :: td
    type(epot_t), intent(inout) :: ep

    call push_sub('opt_control_paramters.parameters_to_h')

    ep%lasers(1)%f = cp%f(1)
    ep%lasers(1)%pol(:) = cp%laser_pol(:)

    call pop_sub()
  end subroutine parameters_to_h


  ! ---------------------------------------------------------
  subroutine parameters_end(cp)
    type(oct_control_parameters_t), intent(inout) :: cp
    integer :: j

    call push_sub('opt_control_parameters.parameters_end')

    do j = 1, cp%no_parameters
      call tdf_end(cp%f(j))
    end do

    call pop_sub()
  end subroutine parameters_end


  ! ---------------------------------------------------------
  subroutine parameters_write(filename, cp)
    character(len=*), intent(in) :: filename
    type(oct_control_parameters_t), intent(in) :: cp

    integer :: i, iunit
    FLOAT :: t

    call push_sub('opt_control_parameters.parameters_write')

    iunit = io_open(filename, action='write')
    do i = 1, cp%ntiter + 1
      t = (i-1)*cp%dt*M_HALF
      write(iunit, '(3es20.8e3)') t, tdf(cp%f(1), t)
    end do
    call io_close(iunit)

    call pop_sub()
  end subroutine parameters_write


  ! ---------------------------------------------------------
  subroutine update_field(oct, penalty, iter, cp, gr, td, psi, chi)
    type(oct_t), intent(in)    :: oct
    type(oct_penalty_t), intent(in) :: penalty
    integer, intent(in)        :: iter
    type(oct_control_parameters_t), intent(inout) :: cp
    type(grid_t), intent(in)   :: gr
    type(td_t), intent(in)     :: td
    type(states_t), intent(in) :: psi
    type(states_t), intent(in) :: chi
    
    CMPLX :: d1
    CMPLX :: d2(NDIM)
    integer :: ik, p, dim, i, pol
    FLOAT :: value

    CMPLX, allocatable :: rpsi(:, :)
    
    call push_sub('opt_control.update_field')
    
    ALLOCATE(rpsi(gr%m%np_part, psi%d%dim), gr%m%np_part*psi%d%dim)

    ! TODO This should be a product between Slater determinants.
    d2 = M_z0 
    do ik = 1, psi%d%nik
      do p  = psi%st_start, psi%st_end
        do pol = 1, NDIM
          do dim = 1, psi%d%dim
            rpsi(:, dim) = psi%zpsi(:, dim, p, ik)*cp%laser_pol(pol)*gr%m%x(:, pol)
          end do
          d2(pol) = zstates_dotp(gr%m, psi%d%dim, chi%zpsi(:, :, p, ik), rpsi)
        end do
      end do
    end do
    deallocate(rpsi)

    d1 = M_z1
    if(oct%algorithm_type .eq. oct_algorithm_zbr98) d1 = zstates_mpdotp(gr%m, psi, chi)

    value = aimag(d1*d2(1))/penalty%tdpenalty(1, 2*iter)
    call tdf_set_numerical(cp%f(1), 2*iter+1, value)
    i = int(sign(M_ONE, td%dt))
    if(iter==0.or.iter==td%max_iter) then
      call tdf_set_numerical(cp%f(1), 2*iter+i+1, value)
      call tdf_set_numerical(cp%f(1), 2*iter+2*i+1, value)
    else
      value = M_HALF*(M_THREE*tdf(cp%f(1), 2*iter+1) - tdf(cp%f(1), 2*iter-2*i+1))
      call tdf_set_numerical(cp%f(1), 2*iter+i+1, value)
      value = M_HALF*( M_FOUR*tdf(cp%f(1), 2*iter+1) - M_TWO*tdf(cp%f(1), 2*iter-2*i+1))
      call tdf_set_numerical(cp%f(1), 2*iter+2*i+1, value)
    end if

    call pop_sub()
  end subroutine update_field


  ! ---------------------------------------------------------
  ! WARNING: This is probably useless. The number of degrees of freedom should be 
  ! the number of initial laser fields found in the input file. The polarizations
  ! should be read from there. If no laser fields are found in the input file, then
  ! some number of degrees of freedon (1) should be used, and some default
  ! polarization (x). 
  subroutine oct_read_laserpol(laserpol)
    ! read the parameters for the laser polarization
    CMPLX,   intent(out) :: laserpol(MAX_DIM)
  
    integer   :: i, no_blk, no_c, dof
    C_POINTER :: blk

    call push_sub('opt_control_parameters.oct_read_laserpol') 

    !%Variable OCTPolarization
    !%Type block
    !%Section Optimal Control
    !%Description
    !% Define how many degress of freedom the laser has and how it is polarized.
    !% The different examples below will explain this.
    !% First of all, the syntax of each line is:
    !%
    !% <tt>%OCTPolarization
    !% <br>&nbsp;&nbsp;dof | pol1 | pol2 | pol3 
    !% <br>%</tt>
    !% The variable defines the degress of freedom which is either 1 or 2.
    !% pol1, pol2, pol3 define the polarization.
    !%
    !% Some examples:
    !%
    !% <tt>%OCTPolarization
    !% <br>&nbsp;&nbsp;1 | 1 | 0 | 0 
    !% <br>%</tt> 
    !% Here we try to optimize the x-polarized laser.
    !%
    !% <tt>%OCTPolarization
    !% <br>&nbsp;&nbsp;1 | 1 | 1 | 0 
    !% <br>%</tt> 
    !% The polarization is linear and lies in the x-y plane, only one laser field is optimized.
    !%
    !% <tt>%OCTPolarization
    !% <br>&nbsp;&nbsp;2 | 1 | 1 | 0 
    !% <br>%</tt> 
    !% The polarization lies in the x-y plane, but this time two components of the laser
    !% field are optimized. This may lead to linear, circular, or ellipitically polarized 
    !% fields, dependening on the problem.
    !%
    !% <tt>%OCTPolarization
    !% <br>&nbsp;&nbsp;1 | 1 | i | 0 
    !% <br>%</tt> 
    !% If we know that the answer must be a circular polarized field we can also fix it 
    !% and optimize only one component.  
    !%End
    if(loct_parse_block(check_inp('OCTPolarization'),blk)==0) then
      no_blk = loct_parse_block_n(blk)
      ! TODO: for each orbital            
      do i=1, no_blk
        no_c = loct_parse_block_cols(blk, i-1)
        call loct_parse_block_int(blk,i-1, 0, dof)
        if((dof.gt.3).OR.(dof.lt.1) ) then
          message(1) = "OCTPolarization: Choose degrees of freedom between 1 and 3."
          call write_fatal(1)
          
        end if
        call loct_parse_block_cmplx(blk,i-1, 1, laserpol(1))
        call loct_parse_block_cmplx(blk,i-1, 2, laserpol(2))
        call loct_parse_block_cmplx(blk,i-1, 3, laserpol(3))
        
      end do
      call loct_parse_block_end(blk)
    else
      message(1) = 'Input: No Polarization defined and degrees of freedom defined'
      message(2) = 'Input: Using default: x-polarized.'
      laserpol(1) = m_z1
      laserpol(2) = m_z0
      laserpol(3) = m_z0
      dof  = 1
      call write_info(2)
    end if

    call pop_sub()
  end subroutine oct_read_laserpol


