!! Copyright (C) 2002-2006 M. Marques, A. Castro, A. Rubio, G. Bertsch
!!
!! This program is free software; you can redistribute it and/or modify
!! it under the terms of the GNU General Public License as published by
!! the Free Software Foundation; either version 2, or (at your option)
!! any later version.
!!
!! This program is distributed in the hope that it will be useful,
!! but WITHOUT ANY WARRANTY; without even the implied warranty of
!! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!! GNU General Public License for more details.
!!
!! You should have received a copy of the GNU General Public License
!! along with this program; if not, write to the Free Software
!! Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
!! 02111-1307, USA.
!!
!! $Id$

#include "global.h"

module run_prog_m
  use calc_mode_m
  use casida_m
  use datasets_m
  use external_pot_m
  use fft_m
  use geom_opt_m
  use global_m
  use ground_state_m
  use hamiltonian_m
  use loct_parser_m
  use messages_m
  use mpi_debug_m
  use multicomm_m
  use one_shot_m
  use opt_control_m
  use phonons_fd_m
  use phonons_lr_m
  use pol_lr_m
  use kdotp_m
  use gcm_m
  use pulpo_m
  use raman_m
  use restart_m
  use static_pol_m
  use system_m
  use timedep_m
  use units_m
  use unocc_m
  use varinfo_m
  use vdw_m

  implicit none

  private
  public ::                      &
    run_init,                    &
    run,                         &
    run_end

  type(system_t)      :: sys
  type(hamiltonian_t) :: h

  integer, parameter :: LR = 1, FD = 2


contains

  ! ---------------------------------------------------------
  subroutine run()
    logical :: fromScratch

    call push_sub('run.run')

    !%Variable FromScratch
    !%Type logical
    !%Default 0
    !%Section Execution
    !%Description
    !% When this variable is yes, octopus will always perform a
    !% calculation from the beginning, without looking for restart
    !% information.
    !%End

    call loct_parse_logical(check_inp('fromScratch'), .false., fromScratch)

    select case(calc_mode())
    case(CM_GS)
      call ground_state_run(sys, h, fromScratch)
    case(CM_UNOCC)
      call unocc_run(sys, h, fromScratch)
    case(CM_TD)
      call td_run(sys, h, fromScratch)
    case(CM_LR_POL)
      select case(get_resp_method())
      case(FD)
        call static_pol_run(sys, h, fromScratch)
      case(LR)
        call pol_lr_run(sys, h, fromScratch)
      end select
    case(CM_VDW)
      call vdW_run(sys, h, fromScratch)
    case(CM_GEOM_OPT)
      call geom_opt_run(sys, h, fromScratch)
    case(CM_PHONONS_LR)
      select case(get_resp_method())
      case(FD)
        call phonons_run(sys, h)
      case(LR)
        call phonons_lr_run(sys, h, fromscratch)
      end select
    case(CM_OPT_CONTROL)
      call opt_control_run(sys, h)
    case(CM_CASIDA)
      call casida_run(sys, h, fromScratch)
    case(CM_RAMAN)
      call raman_run(sys, h, fromscratch)
    case(CM_ONE_SHOT)
      call one_shot_run(sys, h)
    case(CM_KDOTP)
      call kdotp_lr_run(sys, h, fromScratch)
!!!!NEW
    case(CM_GCM)
      call gcm_run(sys, h)
!!!!ENDOFNEW
    case(CM_PULPO_A_FEIRA)
      call pulpo_print()
    end select

    call pop_sub()
    
  end subroutine run
  

  integer function get_resp_method()
    
    !%Variable ResponseMethod
    !%Type integer
    !%Default gs
    !%Section Linear Response
    !%Description
    !% Some response properties can be calculated either via
    !% Sternheimer linear response or by using finite
    !% differences. You can use this variable to select how you want
    !% them to be calculated, it applies to em_resp and vib_modes
    !% calculation modes. By default the Sternheimer linear response
    !% technique is used.
    !%Option sternheimer 1
    !% The linear response is obtained by solving a self-consistent
    !% Sternheimer equation for the variation of the orbitals. This
    !% is the recomended method.
    !%Option finite_differences 2
    !% Properties are calculated as a finite differences derivative of
    !% the energy obtained by several ground state calculations. This
    !% method is slow and limited only to static response, is kept
    !% mainly because is simple and for useful for testing purposes.
    !%End
    
    call loct_parse_int(check_inp('ResponseMethod'), LR, get_resp_method)

    if(.not.varinfo_valid_option('ResponseMethod', get_resp_method)) then
      call input_error('ResponseMethod')
    end if

  end function get_resp_method
  
  ! ---------------------------------------------------------
  subroutine run_init()

    call messages_print_stress(stdout, "Calculation Mode")
    call messages_print_var_option(stdout, "CalculationMode", calc_mode())
    call messages_print_stress(stdout)

    ! initialize ffts
    call fft_all_init()

    if(.not. calc_mode_is(CM_PULPO_A_FEIRA)) then
      call units_init()
      call system_init(sys)
      call hamiltonian_init(h, sys%gr, sys%geo, sys%st, sys%ks%theory_level, sys%ks%xc_family)
      call epot_generate(h%ep, sys%gr, sys%geo, sys%st)
      call restart_init()
    end if
  end subroutine run_init


  ! ---------------------------------------------------------
  subroutine run_end()
    if(.not. calc_mode_is(CM_PULPO_A_FEIRA)) then
       call hamiltonian_end(h, sys%gr, sys%geo)
       call system_end(sys)
    end if

    call fft_all_end()

#ifdef HAVE_MPI
    call mpi_debug_statistics()
#endif

  end subroutine run_end

end module run_prog_m

!! Local Variables:
!! mode: f90
!! coding: utf-8
!! End:
