!! Copyright (C) 2002-2006 X. Andrade
!!
!! This program is free software; you can redistribute it and/or modify
!! it under the terms of the GNU General Public License as published by
!! the Free Software Foundation; either version 2, or (at your option)
!! any later version.
!!
!! This program is distributed in the hope that it will be useful,
!! but WITHOUT ANY WARRANTY; without even the implied warranty of
!! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!! GNU General Public License for more details.
!!
!! You should have received a copy of the GNU General Public License
!! along with this program; if not, write to the Free Software
!! Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
!! 02111-1307, USA.
!!
!! -*- coding: utf-8 mode: f90 -*-
!! $Id: specie.F90 2711 2007-02-13 17:36:18Z xavier $

#include "global.h"

module double_grid_m

  use datasets_m
  use global_m
  use mesh_m
  use messages_m
  use lib_oct_m
  use lib_oct_gsl_spline_m
  use lib_oct_parser_m
  use specie_m

  implicit none
  
  private
  
  public ::              &
       double_grid_t,    &
       double_grid_init, &
       double_grid_end,  &
       double_grid_apply, &
       double_grid_apply_local,     &
       dg_add_localization_density, &
       dg_get_potential_correction, &
       dg_filter_potential,         &
       dg_get_hmax

  type double_grid_t

     FLOAT   :: h_fine(MAX_DIM)
     integer :: nn
     integer :: loc_function
     type(loct_spline_t) :: rho_corr
     logical :: use_double_grid
     logical :: filter

  end type double_grid_t

  FLOAT,   parameter :: rmax = CNST(30.0), sigma = CNST(0.625)
  integer, parameter :: nn = 3000
  
  integer, parameter :: nw = 125
  integer :: idx_fine(nw, 3), idx_coarse(nw, 3)
  FLOAT :: weight(nw)

  integer, parameter :: &
       F_NONE = 0,      &
       F_GAUSSIAN = 1 
  
  
contains
  
  subroutine double_grid_init(this, m, dim)
    type(double_grid_t), intent(out) :: this
    type(mesh_t),        intent(in)  :: m
    integer,             intent(in)  :: dim

    this%nn = 1
    this%h_fine(1:MAX_DIM) = m%h(1:MAX_DIM)/M_THREE
   
    !%Variable DoubleGrid
    !%Type logical
    !%Default no
    !%Section Mesh
    !%Description
    !% Enables or disables the use of a double grid technique to
    !% increase the precision of the application of the
    !% pseudopotentials.
    !%End

    call loct_parse_logical(check_inp('DoubleGrid'), .false., this%use_double_grid)

    !%Variable DoubleGridFilter
    !%Type logical
    !%Default no
    !%Section Mesh
    !%Description
    !% When this is enabled a filter is applied to the pseudopotential.
    !%End

    call loct_parse_logical(check_inp('DoubleGridFilter'), .false., this%filter)

    !%Variable LocalizationDensity
    !%Type integer
    !%Default none
    !%Section Mesh
    !%Description
    !% When a localization density is used, the local part of the
    !% pseudopotential is separated in a localized part and a long
    !% range term. The long range term is calculated by solving the
    !% poisson equation of a localized charge density. This separation
    !% is required to apply to use the double grid technique or to
    !% apply a filter to the local part of the pseudopotential. This
    !% variable selects which kind of function is used for the
    !% localized charge density. By default this technique is not
    !% used.
    !%Option none 0 
    !% The potential is not separated. This is the default.
    !%Option gaussian 3
    !% A gaussian charge distribution is used, the correspoding
    !% potential is erf(r)/r.
    !%End

    if ( this%use_double_grid .or. this%filter ) then 
      call loct_parse_int(check_inp('LocalizationDensity'), F_GAUSSIAN, this%loc_function)
    else
      call loct_parse_int(check_inp('LocalizationDensity'), F_NONE, this%loc_function)
    end if

    call functions_init()
    
    call init_data()

    contains

      subroutine functions_init()
        
        integer :: ii
        FLOAT :: dr, r(1:nn), rho(1:nn)

        dr = rmax/(nn-M_ONE)
        
        r(1) = M_ZERO
        do ii = 1, nn
          rho(ii) = M_ONE/(sigma*sqrt(M_TWO*M_PI))**3*exp(-M_HALF*r(ii)**2/sigma**2)
          if ( ii /= nn ) r(ii+1) = r(ii) + dr
        end do

        call loct_spline_init(this%rho_corr)
        
        call loct_spline_fit(nn, r, rho, this%rho_corr)

      end subroutine functions_init

  end subroutine double_grid_init

  subroutine double_grid_end(this)
    type(double_grid_t), intent(inout) :: this
 
        call loct_spline_end(this%rho_corr)

  end subroutine double_grid_end

  subroutine double_grid_apply_local(this, s, m, x_atom, vl)
    type(double_grid_t), intent(in)  :: this
    type(specie_t),      intent(in)  :: s
    type(mesh_t),        intent(in)  :: m
    FLOAT,               intent(in)  :: x_atom(1:MAX_DIM)
    FLOAT,               intent(out) :: vl(:)
    
    FLOAT, allocatable :: vv(:,:,:)

    type(loct_spline_t), pointer  :: ps_spline
    FLOAT :: r
    integer :: ip, idx_ip(1:3)

    integer :: ii, jj, kk, idx(1:3)

    if(dg_add_localization_density(this)) then 
      ps_spline => s%ps%vll
    else
      ps_spline => s%ps%vl
    end if

    if (.not. this%use_double_grid) then 
      
      do ip = 1, m%np
        r = sqrt(sum( (m%x(ip, :) - x_atom(:))**2 ))
        vl(ip) = loct_splint(ps_spline, r)
      end do
      
    else

      ALLOCATE(vv(-this%nn:this%nn, -this%nn:this%nn, -this%nn:this%nn), (2*this%nn+1)**3 )
      
      vl(1:m%np) = M_ZERO
      
      do ip = 1, m%np
        
        do ii = -this%nn, this%nn
          do jj = -this%nn, this%nn
            do kk = -this%nn, this%nn
              
              idx = (/ii, jj, kk/)
              
              r = sqrt(sum( (m%x(ip, 1:3) + this%h_fine(1:3)*idx(1:3) - x_atom(1:3))**2 ))
              vv(ii, jj, kk) = loct_splint(ps_spline, r)
              
            end do
          end do
        end do
        
        do ii = 1, nw
          idx_ip(1:3) = m%Lxyz(ip, 1:3) + idx_coarse(ii, 1:3)
          jj = m%Lxyz_inv( idx_ip(1), idx_ip(2), idx_ip(3) )
          if ( jj <= m%np ) then 
            vl(jj) = vl(jj) + weight(ii) * vv( idx_fine(ii, 1), idx_fine(ii, 2), idx_fine(ii, 3) )
          end if
        end do
        
      end do
      
      deallocate(vv)

      vl(1:m%np) = vl(1:m%np) / CNST(27.0)

    end if

  end subroutine double_grid_apply_local

  FLOAT function double_grid_apply(this, ps_spline, x_atom, x_grid) result(ww)
    type(double_grid_t), intent(in) :: this
    type(loct_spline_t), intent(in)  :: ps_spline
    FLOAT,               intent(in)  :: x_atom(1:MAX_DIM)
    FLOAT,               intent(in)  :: x_grid(1:MAX_DIM)
    
    FLOAT :: r
   
    r = sqrt(sum( (x_grid(:)-x_atom(:))**2 ))
    ww = loct_splint(ps_spline, r)

  end function double_grid_apply

  logical function dg_add_localization_density(this) 
    type(double_grid_t), intent(in) :: this
    
    dg_add_localization_density = ( this%loc_function /= F_NONE )
    
  end function dg_add_localization_density


  subroutine dg_get_potential_correction(this, vlc)
    type(double_grid_t), intent(in) :: this
    type(loct_spline_t), intent(out) :: vlc

    FLOAT :: dr, r(1:nn), pot(1:nn)
    integer :: ii
    
    dr = rmax/(nn-M_ONE)
        
    r(1) = M_ZERO

    pot(1) = M_TWO/(sqrt(M_TWO*M_PI)*sigma)

    do ii = 1, nn
      if ( ii /= 1 ) pot(ii) = loct_erf(r(ii)/(sigma*sqrt(M_TWO)))/r(ii)
      if ( ii /= nn ) r(ii+1) = r(ii) + dr
    end do
    
    call loct_spline_fit(nn, r, pot, vlc)

  end subroutine dg_get_potential_correction

  logical function dg_filter_potential(this)
    type(double_grid_t), intent(in) :: this

    dg_filter_potential = this%filter
  end function dg_filter_potential

  FLOAT function dg_get_hmax(this, mesh) result(hmax)
    type(double_grid_t), intent(in) :: this
    type(mesh_t),        intent(in) :: mesh

    if(this%use_double_grid) then 
      hmax = maxval(this%h_fine(1:MAX_DIM))
    else
      hmax = maxval(mesh%h(1:MAX_DIM))
    end if
    
  end function dg_get_hmax

#include "double_grid_data.F90"

end module double_grid_m
