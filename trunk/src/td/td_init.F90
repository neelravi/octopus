!! Copyright (C) 2002-2006 M. Marques, A. Castro, A. Rubio, G. Bertsch
!!
!! This program is free software; you can redistribute it and/or modify
!! it under the terms of the GNU General Public License as published by
!! the Free Software Foundation; either version 2, or (at your option)
!! any later version.
!!
!! This program is distributed in the hope that it will be useful,
!! but WITHOUT ANY WARRANTY; without even the implied warranty of
!! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!! GNU General Public License for more details.
!!
!! You should have received a copy of the GNU General Public License
!! along with this program; if not, write to the Free Software
!! Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
!! 02111-1307, USA.
!!
!! $Id$

! ---------------------------------------------------------
subroutine td_init(sys, h, td)
  type(system_t),        intent(inout) :: sys
  type(hamiltonian_t),   intent(inout) :: h
  type(td_t),            intent(out)   :: td

  integer :: dummy

  call push_sub('td_init.td_init')

  td%iter = 0

  !%Variable TDTimeStep
  !%Type float
  !%Default 0.07 a.u.
  !%Section Time Dependent::Propagation
  !%Description
  !% Time-step for the time propagation. The default is 0.07
  !% [hbar/Hartree].
  !%End
  call loct_parse_float(check_inp('TDTimeStep'), CNST(0.07)/units_inp%time%factor, td%dt)
  td%dt = td%dt * units_inp%time%factor
  if (td%dt <= M_ZERO) then
    write(message(1),'(a,f14.6,a)') "Input: '", td%dt, "' is not a valid TDTimeStep"
    message(2) = '(0 < TDTimeStep)'
    call write_fatal(2)
  end if

  !%Variable TDIonicTimeScale
  !%Type float
  !%Default 1.0
  !%Section Time Dependent::Propagation
  !%Description
  !% This variable defines the factor between the time scale of ionic
  !% and electronic movement. It allows to perform reasonably fast
  !% Born-Oppenheimer molecular dynamics simulations based on
  !% Ehrenfest dynamics. The value of this variable is equivalent to
  !% the role of <math>\mu</math> in Car-Parrinello. If increased it
  !% will allow to accelerate linearly the time step of the ion
  !% dynamics, but more the system will deviate from the
  !% Born-Oppenheimer surface. The default is 1, which means that both
  !% time scales are the same. Note that a value different than 1
  !% implies that the electron will not follow a physical behaviour.
  !%
  !% According to our test values around 10 are reasonable, but it
  !% will depend on your system, mainly on the width of the gap.
  !%
  !% Important: The electronic time step will be the value of
  !% TDTimeStep divided by this variable, so if you have determined an
  !% optimal electronic time step (that we can call dte), it is
  !% recommended that you define your time step as:
  !%
  !% TDTimeStep = dte * TDIonicTimeScale
  !%
  !% so you will always use the optimal electronic time step.
  !%
  !% For more details see: http://arxiv.org/abs/0710.3321
  !%
  !%End

  call loct_parse_float(check_inp('TDIonicTimeScale'), CNST(1.0), td%mu)

  !%Variable TDMaximumIter
  !%Type integer
  !%Default 1500
  !%Section Time Dependent::Propagation
  !%Description
  !% Number of time propagation steps that will be performed. By default 1500.
  !%
  !% Tip: If you would like to specify the real time of the
  !% propagation, rather than the number of steps, just use something
  !% like:
  !%
  !% TDMaximumIter = 1000.0 / TDTimeStep
  !%
  !%End 

  call loct_parse_int(check_inp('TDMaximumIter'), 1500, td%max_iter)

  if(td%max_iter < 1) then
    write(message(1), '(a,i6,a)') "Input: '", td%max_iter, "' is not a valid TDMaximumIter"
    message(2) = '(TDMaximumIter <= 1)'
    call write_fatal(2)
  end if
  
  ! Initialize the kick (if optical spectrum calculations are to be performed)
  call kick_init(td%kick, sys%st%d%nspin)

  ! now the photoelectron stuff
#if !defined(DISABLE_PES)
  call loct_parse_int(check_inp('AbsorbingBoundaries'), 0, dummy)
  call PES_init(td%PESv, sys%gr%m, sys%gr%sb, sys%st, dummy, sys%outp%iter)
#endif

  !%Variable MoveIons
  !%Type integer
  !%Default static_ions
  !%Section Time Dependent::Propagation
  !%Description
  !% This variable specifies how to treat the dynamic of the ions
  !% during a time-dependent run. By default they will remain static.
  !%Option static_ions 0
  !% Do not move the ions.
  !%Option vel_verlet 1
  !% Newtonian dynamics using the velocity Verlet integrator.
  !%End

  call loct_parse_int(check_inp('MoveIons'), STATIC_IONS, td%move_ions)
  if(.not.varinfo_valid_option('MoveIons', td%move_ions)) call input_error('MoveIons')
  call messages_print_var_option(stdout, 'MoveIons', td%move_ions)


  !%Variable TDDynamics
  !%Type integer
  !%Default 1
  !%Section Time Dependent::Propagation
  !%Description
  !% Type of dynamics to follow during a time propagation. By default
  !% is Ehrenfest TDDFT.
  !%Option ehrenfest 1
  !% Ehrenfest dynamics.
  !%Option bo 2
  !% Born Openheimer (Experimental).
  !%Option cp 3
  !% Carr-Parrinelo molecular dynamics.
  !%End

  if (td%move_ions /= 0 ) then
    call loct_parse_int(check_inp('TDDynamics'), EHRENFEST, td%dynamics)
    if(.not.varinfo_valid_option('TDDynamics', td%dynamics)) call input_error('TDDynamics')
    call messages_print_var_option(stdout, 'TDDynamics', td%dynamics)
  else
    td%dynamics =  EHRENFEST
  end if
 

  !%Variable RecalculateGSDuringEvolution
  !%Type logical
  !%Default no
  !%Section Time Dependent::Propagation
  !%Description
  !% In order to calculate some information about the system along the
  !% evolution (e.g. projection onto the ground-state KS determinant,
  !% projection of the TDKS spin-orbitals onto the grond-state KS
  !% spin-orbitals), the ground-state KS orbitals are needed. If the
  !% ionic potential changes -- that is, the ions move --, one may want
  !% to recalculate the ground state. You may do this by setting this
  !% variable.
  !%
  !% The recalculation is not done every time step, but only every
  !% OutputEvery time steps.
  !%End
  call loct_parse_logical(check_inp("RecalculateGSDuringEvolution"), .false., td%recalculate_gs)

  call td_rti_init(sys%gr, sys%st, td%tr, td%dt, td%max_iter)
  if(td%dynamics == BO)  call scf_init(sys%gr, sys%geo, td%scf, sys%st, h)
  if(td%dynamics == CP) call cpmd_init(td%cp_propagator, sys%gr, sys%st)

  if(h%ep%no_lasers>0.and.mpi_grp_is_root(mpi_world)) then
    call messages_print_stress(stdout, "Time-dependent external fields")
    call laser_write_info(h%ep%no_lasers, h%ep%lasers, sys%gr%sb, td%dt, td%max_iter, stdout)
    call messages_print_stress(stdout)
  end if

  call pop_sub()
end subroutine td_init


! ---------------------------------------------------------
subroutine td_end(td)
  type(td_t), intent(inout) :: td

  call push_sub('td_init.td_end')

#if !defined(DISABLE_PES)
  call PES_end(td%PESv)
#endif
  call td_rti_end(td%tr)  ! clean the evolution method

  if(td%dynamics == BO) call scf_end(td%scf)
  
  call pop_sub()
end subroutine td_end

!! Local Variables:
!! mode: f90
!! coding: utf-8
!! End:
