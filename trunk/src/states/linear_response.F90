!! Copyright (C) 2004 E.S. Kadantsev, M. Marques
!!
!! This program is free software; you can redistribute it and/or modify
!! it under the terms of the GNU General Public License as published by
!! the Free Software Foundation; either version 2, or (at your option)
!! any later version.
!!
!! This program is distributed in the hope that it will be useful,
!! but WITHOUT ANY WARRANTY; without even the implied warranty of
!! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!! GNU General Public License for more details.
!!
!! You should have received a copy of the GNU General Public License
!! along with this program; if not, write to the Free Software
!! Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
!! 02111-1307, USA.
!!
!! $Id$

#include "global.h"

module linear_response_m
  use datasets_m
  use global_m
  use grid_m
  use lalg_basic_m
  use loct_m
  use lib_oct_parser_m
  use mesh_m
  use messages_m
  use output_m
  use states_m
  use units_m

  implicit none

  private

  !the minimum value for a state to be considered occupied
  FLOAT, public, parameter :: lr_min_occ=CNST(1e-5)
  
  public :: &
       lr_t, &
       lr_init, &
       lr_allocate, &
       lr_copy, & 
       dlr_output, & 
       zlr_output, & 
       dlr_orth_vector, & 
       zlr_orth_vector, & 
       dlr_build_dl_rho, & 
       zlr_build_dl_rho, &
       dlr_orth_response, &
       zlr_orth_response, &
       lr_dealloc, &
       lr_is_allocated


  type lr_t
    !the number of lr wfs
    integer :: nst
    logical :: is_allocated
     
    ! the real quantities
    FLOAT, pointer :: ddl_rho(:,:)     ! response of the density
    FLOAT, pointer :: ddl_psi(:,:,:,:) ! linear change of the real KS orbitals
    
    ! and the complex version
    CMPLX, pointer :: zdl_rho(:,:)     ! response of the density
    CMPLX, pointer :: zdl_psi(:,:,:,:) ! linear change of the complex KS orbitals

    !other observables
    CMPLX, pointer :: dl_j(:,:,:)     ! response of the current
    FLOAT, pointer :: ddl_de(:,:)     ! unnormalized elf
    FLOAT, pointer :: ddl_elf(:,:)    ! normalized elf
    CMPLX, pointer :: zdl_de(:,:)     ! unnormalized elf
    CMPLX, pointer :: zdl_elf(:,:)    ! normalized elf
    
  end type lr_t

contains

  ! ---------------------------------------------------------
  subroutine lr_init(lr)
    type(lr_t),     intent(out)   :: lr

    call push_sub('linear_response.lr_init')

    nullify(lr%ddl_rho, lr%ddl_psi)
    nullify(lr%zdl_rho, lr%zdl_psi)
    nullify(lr%dl_j, lr%ddl_de, lr%zdl_de, lr%ddl_elf, lr%zdl_elf)

    lr%is_allocated = .false.

    call pop_sub()

  end subroutine lr_init

  subroutine lr_allocate(lr, st, m)
    type(lr_t),     intent(inout) :: lr
    type(states_t), intent(in)    :: st
    type(mesh_t),   intent(in)    :: m

    integer :: size

    call push_sub('linear_response.lr_init')

    size = m%np_part*st%d%dim*st%nst*st%d%nspin

    if (wfs_are_complex(st)) then 

      ALLOCATE(lr%zdl_psi(m%np_part, st%d%dim, st%nst, st%d%nspin), size)
      ALLOCATE(lr%zdl_rho(m%np, st%d%nspin), m%np*st%d%nspin)
      
      lr%zdl_psi = M_ZERO
      lr%zdl_rho = M_ZERO
      
    else

      ALLOCATE(lr%ddl_psi(m%np_part, st%d%dim, st%nst, st%d%nspin), size)
      ALLOCATE(lr%ddl_rho(m%np, st%d%nspin), m%np*st%d%nspin)

      lr%ddl_psi = M_ZERO
      lr%ddl_rho = M_ZERO

    end if

    lr%is_allocated = .true.
    
    call pop_sub()

  end subroutine lr_allocate

  ! ---------------------------------------------------------
  subroutine lr_dealloc(lr)
    type(lr_t), intent(inout) :: lr

    if(associated(lr%ddl_rho)) then
      deallocate(lr%ddl_rho)
      nullify   (lr%ddl_rho)
    end if

    if(associated(lr%zdl_rho)) then
      deallocate(lr%zdl_rho)
      nullify   (lr%zdl_rho)
    end if

    if(associated(lr%ddl_psi)) then
      deallocate(lr%ddl_psi)
      nullify   (lr%ddl_psi)
    end if

    if(associated(lr%zdl_psi)) then
      deallocate(lr%zdl_psi)
      nullify   (lr%zdl_psi)
    end if

    if(associated(lr%dl_j)) deallocate(lr%dl_j)
    if(associated(lr%ddl_de)) deallocate(lr%ddl_de)
    if(associated(lr%ddl_elf)) deallocate(lr%ddl_elf)
    if(associated(lr%zdl_de)) deallocate(lr%zdl_de)
    if(associated(lr%zdl_elf)) deallocate(lr%zdl_elf)

  end subroutine lr_dealloc

  subroutine lr_copy(st, m, src, dest)
    type(states_t), intent(in) :: st
    type(mesh_t),   intent(in) :: m
    type(lr_t),     intent(in) :: src
    type(lr_t),     intent(inout) :: dest

    integer :: ik, idim, ist

    do ik = 1, st%d%nspin
      
      if( wfs_are_complex(st)) then
        call lalg_copy(m%np, src%zdl_rho(:, ik), dest%zdl_rho(:, ik))
      else
        call lalg_copy(m%np, src%ddl_rho(:, ik), dest%ddl_rho(:, ik))
      end if
    
      do ist = 1, st%nst
        do idim = 1, st%d%dim
          if( wfs_are_complex(st)) then
            call lalg_copy(m%np_part, src%zdl_psi(:, idim, ist, ik), dest%zdl_psi(:, idim, ist, ik))
          else 
            call lalg_copy(m%np_part, src%ddl_psi(:, idim, ist, ik), dest%ddl_psi(:, idim, ist, ik))
          end if
        end do
      end do

   end do

  end subroutine lr_copy

  logical function lr_is_allocated(this) 
    type(lr_t),     intent(in) :: this
    lr_is_allocated = this%is_allocated
  end function lr_is_allocated

#include "undef.F90"
#include "real.F90"

#include "linear_response_inc.F90"
#include "linear_response_out.F90" 

#include "undef.F90"
#include "complex.F90"

#include "linear_response_inc.F90"
#include "linear_response_out.F90" 

end module linear_response_m

!! Local Variables:
!! mode: f90
!! coding: utf-8
!! End:
