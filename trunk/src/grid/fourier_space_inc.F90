!! Copyright (C) 2002-2006 M. Marques, A. Castro, A. Rubio, G. Bertsch
!!
!! This program is free software; you can redistribute it and/or modify
!! it under the terms of the GNU General Public License as published by
!! the Free Software Foundation; either version 2, or (at your option)
!! any later version.
!!
!! This program is distributed in the hope that it will be useful,
!! but WITHOUT ANY WARRANTY; without even the implied warranty of
!! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!! GNU General Public License for more details.
!!
!! You should have received a copy of the GNU General Public License
!! along with this program; if not, write to the Free Software
!! Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
!! 02111-1307, USA.
!!
!! $Id$

subroutine X(cf_alloc_FS)(cf)
  type(X(cf_t)), intent(inout) :: cf

  PUSH_SUB(X(cf_alloc_FS))

  ASSERT(.not.associated(cf%FS))
  ASSERT(associated(cf%fft))

  SAFE_ALLOCATE(cf%FS(1:cf%nx, 1:cf%n(2), 1:cf%n(3)))

  POP_SUB(X(cf_alloc_FS))
end subroutine X(cf_alloc_FS)


! ---------------------------------------------------------
subroutine X(cf_free_FS)(cf)
  type(X(cf_t)), intent(inout) :: cf

  PUSH_SUB(X(cf_free_FS))

  ASSERT(associated(cf%FS))
  SAFE_DEALLOCATE_P(cf%FS)
  nullify(cf%FS)

  POP_SUB(X(cf_free_FS))
end subroutine X(cf_free_FS)

! ---------------------------------------------------------
! initializes the ffts. As the dimension of the fft may be adjusted, this
! routine has to be called before allocating anything
subroutine X(cf_fft_init)(cf, sb)
  type(X(cf_t)),     intent(inout) :: cf
  type(simul_box_t), intent(in)    :: sb

  PUSH_SUB(X(cf_fft_init))

  ASSERT(.not.associated(cf%fft))
  ASSERT(.not.associated(cf%RS))
  ASSERT(.not.associated(cf%FS))

  SAFE_ALLOCATE(cf%fft)
#ifdef R_TREAL
  call fft_init(cf%n, sb%dim, fft_real, cf%fft, optimize = .not.simul_box_is_periodic(sb))
  cf%nx = cf%n(1)/2 + 1
#else
  call fft_init(cf%n, sb%dim, fft_complex, cf%fft, optimize = .not.simul_box_is_periodic(sb))
  cf%nx = cf%n(1)
#endif

  POP_SUB(X(cf_fft_init))
end subroutine X(cf_fft_init)

! ---------------------------------------------------------
! The next routines convert the function between real space and Fourier space
! Note that the dimensions of the function in FS are different depending on whether
! f is real or complex, because the FFT representation is different (FFTW scheme).
subroutine X(cf_RS2FS)(cf)
  type(X(cf_t)), intent(inout)  :: cf

  ASSERT(associated(cf%RS))
  if(.not.associated(cf%FS)) call X(cf_alloc_FS)(cf)

  call X(fft_forward)(cf%fft, cf%RS, cf%FS)

end subroutine X(cf_RS2FS)


! ---------------------------------------------------------
subroutine X(cf_FS2RS)(cf)
  type(X(cf_t)), intent(inout)  :: cf

  ASSERT(associated(cf%FS))
  if(.not.associated(cf%RS)) call X(cf_alloc_RS)(cf)

  call X(fft_backward)(cf%fft, cf%FS, cf%RS)

end subroutine X(cf_FS2RS)


! ---------------------------------------------------------
subroutine X(fourier_space_op_init)(this, cube, op)
  type(fourier_space_op_t), intent(out) :: this
  type(X(cf_t)),            intent(in)  :: cube
  R_TYPE,                   intent(in)  :: op(:, :, :)

  integer :: ii, jj, kk

  SAFE_ALLOCATE(this%X(op)(1:cube%nx, 1:cube%n(2), 1:cube%n(3)))

  do kk = 1, cube%n(3)
    do jj = 1, cube%n(2)
      do ii = 1, cube%nx
        this%X(op)(ii, jj, kk) = op(ii, jj, kk)
      end do
    end do
  end do

end subroutine X(fourier_space_op_init)


! ---------------------------------------------------------
subroutine X(fourier_space_op_end)(this)
  type(fourier_space_op_t), intent(inout) :: this

  SAFE_DEALLOCATE_P(this%X(op))

end subroutine X(fourier_space_op_end)


! ---------------------------------------------------------
subroutine X(fourier_space_op_apply)(this, cube)
  type(fourier_space_op_t), intent(in)     :: this
  type(X(cf_t)),            intent(inout)  :: cube
  
  integer :: ii, jj, kk
  
  call X(cf_alloc_FS)(cube)
  call X(cf_RS2FS)(cube)

  do kk = 1, cube%n(3)
    do jj = 1, cube%n(2)
      do ii = 1, cube%nx
        cube%FS(ii, jj, kk) = cube%FS(ii, jj, kk)*this%X(op)(ii, jj, kk)
      end do
    end do
  end do

  call X(cf_FS2RS)(cube)
  call X(cf_free_FS)(cube)

end subroutine X(fourier_space_op_apply)

! ---------------------------------------------------------
! The next two subroutines convert a function in Fourier space
! between the normal mesh and the cube
! Note that the function in the mesh should be defined
! globally, not just in a partition (when running in
! parallel in real-space domains).
! ---------------------------------------------------------
subroutine X(mesh_to_fourier) (mesh, mf, cf)
  type(mesh_t),  intent(in)    :: mesh
  CMPLX,         intent(in)    :: mf(:)   ! mf(mesh%np_global)
  type(X(cf_t)), intent(inout) :: cf

  integer :: ip, ix, iy, iz

  ASSERT(associated(cf%FS))

  cf%FS = M_z0

  do ip = 1, mesh%np_global
    ix = pad_feq(mesh%idx%Lxyz(ip, 1), cf%n(1), .false.)
    if(ix > cf%nx) cycle ! negative frequencies are redundant
    iy = pad_feq(mesh%idx%Lxyz(ip, 2), cf%n(2), .false.)
    iz = pad_feq(mesh%idx%Lxyz(ip, 3), cf%n(3), .false.)

    cf%FS(ix, iy, iz) = mf(ip)
  end do
end subroutine X(mesh_to_fourier)


! ---------------------------------------------------------
subroutine X(fourier_to_mesh) (mesh, cf, mf)
  type(mesh_t),  intent(in)  :: mesh
  type(X(cf_t)), intent(in)  :: cf
  CMPLX,         intent(out) :: mf(:) ! mf(mesh%np_global)

  integer :: ip, ix, iy, iz

  ASSERT(associated(cf%FS))

  do ip = 1, mesh%np_global
    ix = pad_feq(mesh%idx%Lxyz(ip, 1), cf%n(1), .false.)
    iy = pad_feq(mesh%idx%Lxyz(ip, 2), cf%n(2), .false.)
    iz = pad_feq(mesh%idx%Lxyz(ip, 3), cf%n(3), .false.)

#ifdef R_TREAL
    if(ix > cf%nx) then
      ix = pad_feq(-mesh%idx%Lxyz(ip, 1), cf%n(1), .false.)
      mf(ip) = conjg(cf%FS(ix, iy, iz))
    else
      mf(ip) = cf%FS(ix, iy, iz)
    end if
#else
    mf(ip) = cf%FS(ix, iy, iz)
#endif
  end do

end subroutine X(fourier_to_mesh)

!! Local Variables:
!! mode: f90
!! coding: utf-8
!! End:
