!! Copyright (C) 2002-2006 M. Marques, A. Castro, A. Rubio, G. Bertsch
!!
!! This program is free software; you can redistribute it and/or modify
!! it under the terms of the GNU General Public License as published by
!! the Free Software Foundation; either version 2, or (at your option)
!! any later version.
!!
!! This program is distributed in the hope that it will be useful,
!! but WITHOUT ANY WARRANTY; without even the implied warranty of
!! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!! GNU General Public License for more details.
!!
!! You should have received a copy of the GNU General Public License
!! along with this program; if not, write to the Free Software
!! Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
!! 02111-1307, USA.
!!
!! $Id$

#include "global.h"

module derivatives_m
  use datasets_m
  use global_m
  use lalg_adv_m
  use lalg_basic_m
  use loct_parser_m
  use math_m
  use mesh_m
  use messages_m
  use mpi_m
  use nl_operator_m
  use par_vec_m
  use profiling_m
  use simul_box_m
  use stencil_cube_m
  use stencil_star_m
  use stencil_starplus_m
  use stencil_variational_m
  use varinfo_m

  implicit none

  private
  public ::                 &
    derivatives_t,          &
    derivatives_init,       &
    derivatives_end,        &
    derivatives_build,      &
    dderivatives_lapl,      &
    dderivatives_lapl_start,&
    dderivatives_lapl_finish,&
    dderivatives_lapl_keep_going,&
    derivatives_lapl_diag,  &
    dderivatives_laplt,     &
    dderivatives_grad,      &
    dderivatives_oper,      &
    dderivatives_div,       &
    dderivatives_curl,      &
    dset_bc,                &
    zderivatives_lapl,      &
    zderivatives_lapl_start,&
    zderivatives_lapl_finish,&
    zderivatives_lapl_keep_going,&
    zderivatives_laplt,     &
    zderivatives_grad,      &
    zderivatives_oper,      &
    zderivatives_div,       &
    zderivatives_curl,      &
    zset_bc,                &
    stencil_extent,         &
    der_handle_t,           &
    der_handle_init,        &
    der_handle_end,         &
    df_angular_momentum,    &
    zf_angular_momentum,    &
    df_l2, zf_l2

  integer, parameter ::     &
    DER_BC_ZERO_F    = 0,   &  ! function is zero at the boundaries
    DER_BC_ZERO_DF   = 1,   &  ! first derivative of the function is zero
    DER_BC_PERIOD    = 2       ! boundary is periodic

  integer, parameter ::     &
    DER_STAR         = 1,   &
    DER_VARIATIONAL  = 2,   &
    DER_CUBE         = 3,   &
    DER_STARPLUS     = 4

  type derivatives_t
    type(mesh_t), pointer :: m             ! pointer to the underlying mesh
    integer               :: dim           ! dimensionality of the space (sb%dim)
    integer               :: order         ! order of the discretization (value depends on stencil)
    integer               :: stencil_type  ! type of discretization

    integer               :: boundaries(MAX_DIM) ! bounday conditions
    logical               :: zero_bc
    logical               :: periodic_bc

    ! If the so-called variational discretization is used, this controls a
    FLOAT :: lapl_cutoff   ! possible filter on the Laplacian.


    type(nl_operator_t), pointer :: op(:)  ! op(1:conf%dim) => gradient
    ! op(conf%dim+1) => laplacian
    type(nl_operator_t), pointer :: lapl   ! these are just shortcuts for op
    type(nl_operator_t), pointer :: grad(:)

    type(nl_operator_t) :: laplt ! The transpose of the Laplacian.
    integer             :: n_ghost(3)   ! ghost points to add in each dimension
#if defined(HAVE_MPI)
    integer             :: comm_method 
#endif
  end type derivatives_t

  type der_handle_t
    private
#ifdef HAVE_MPI
    type(pv_handle_t) :: pv_h
    logical :: parallel_in_domains
#endif
    FLOAT, pointer :: df(:)
    CMPLX, pointer :: zf(:)
    FLOAT, pointer :: dlapl(:)
    CMPLX, pointer :: zlapl(:)
    logical :: ghost_update
    logical :: dealloc_f
  end type der_handle_t

contains

  ! ---------------------------------------------------------
  subroutine derivatives_init(der, sb, use_curvilinear)
    type(derivatives_t), intent(out) :: der
    type(simul_box_t),   intent(in)  :: sb
    logical,             intent(in)  :: use_curvilinear

    integer :: i

    call push_sub('derivatives.derivatives_init')

    ! copy this value to my structure
    der%dim = sb%dim

    !%Variable DerivativesStencil
    !%Type integer
    !%Default stencil_star
    !%Section Mesh::Derivatives
    !%Description
    !% Decides what kind of stencil is used, i.e. what points, around
    !% each point in the mesh, are the neighboring points used in the
    !% expression of the differential operator.
    !%
    !% If curvilinear coordinates are to be used, then only the "stencil_starplus"
    !% or the "stencil_cube" may be used. We only recommend the "stencil_starplus",
    !% the cube typically needing way too much memory resources.
    !%Option stencil_star 1
    !% A star around each point (i.e., only points in the axis).
    !%Option stencil_variational 2
    !% Same as the star, but with coefficients built in a different way.
    !%Option stencil_cube 3
    !% A cube of points around each point.
    !%Option stencil_starplus 4
    !% The star, plus a number of off-axis points.
    !%End
    if(use_curvilinear) then
      call loct_parse_int(check_inp('DerivativesStencil'), DER_STARPLUS, der%stencil_type)
    else
      call loct_parse_int(check_inp('DerivativesStencil'), DER_STAR, der%stencil_type)
    endif
    if(.not.varinfo_valid_option('DerivativesStencil', der%stencil_type)) call input_error('DerivativesStencil')
    call messages_print_var_option(stdout, "DerivativesStencil", der%stencil_type)

    if(use_curvilinear  .and.  der%stencil_type < DER_CUBE) call input_error('DerivativesStencil')
    if(der%stencil_type == DER_VARIATIONAL) then
      call loct_parse_float(check_inp('DerivativesLaplacianFilter'), M_ONE, der%lapl_cutoff)
    end if

    !%Variable DerivativesOrder
    !%Type integer
    !%Default 4
    !%Section Mesh::Derivatives
    !%Description
    !% This variable gives the dicretization order for the approximation of
    !% the differential operators. This means, basically, that
    !% <tt>DerivativesOrder</tt> points are used in each positive/negative
    !% spatial direction, e. g. <tt>DerivativesOrder = 1</tt> would give
    !% the well-known three-point-formula in 1D.
    !% The number of points actually used for the Laplacian
    !% depends on the stencil used:
    !%
    !% <tt>stencil_star</tt>: 2*<tt>DerivativesOrder</tt>*dim+1
    !%
    !% <tt>stencil_cube</tt>: (2*<tt>DerivativesOrder</tt>+1)^dim
    !%
    !% <tt>stencil_starplus</tt>: 2*<tt>DerivativesOrder</tt>+1+n with n being 12
    !% in 2D and 44 in 3D.
    !%End
    call loct_parse_int(check_inp('DerivativesOrder'), 4, der%order)

#ifdef HAVE_MPI
    !%Variable ParallelizationOfDerivatives
    !%Type integer
    !%Default non_blocking
    !%Section Execution::Parallelization
    !%Description
    !% This option selects how the communication required for the
    !% synchronization is performed. The default is non_blocking.
    !%Option blocking 1
    !% Blocking communication.
    !%Option non_blocking 2
    !% Communication is based on non blocking point to point communication.
    !%Option non_blocking_collective 3
    !% Non-blocking collective communication (requires libnbc).
    !%End
    
    if ( der%dim > 1) then
      call loct_parse_int(check_inp('ParallelizationOfDerivatives'), NON_BLOCKING, der%comm_method)
    else
      der%comm_method = BLOCKING
    end if
    
    if(.not. varinfo_valid_option('ParallelizationOfDerivatives', der%comm_method)) then
      call input_error('ParallelizationOfDerivatives')
    end if

#ifndef HAVE_LIBNBC
    if(der%comm_method == NON_BLOCKING_COLLECTIVE) then
      message(1) = "Error: libnbc is not available. Check the ParallelizationOfDerivatives variable."
      call write_fatal(1)
    end if
#endif

    call obsolete_variable('OverlapDerivatives', 'ParallelizationOfDerivatives')
#endif

    ! construct lapl and grad structures
    ALLOCATE(der%op(der%dim + 1), der%dim + 1)
    der%grad => der%op
    der%lapl => der%op(der%dim + 1)

    call derivatives_get_stencil_lapl(der)
    call derivatives_get_stencil_grad(der)

    ! find out the bounday conditions
    call loct_parse_int(check_inp('DerivativesBoundaries'), DER_BC_ZERO_F, i)
    if((i < DER_BC_ZERO_F).or.(i > DER_BC_PERIOD)) then
      write(message(1), '(a,i2,a)') 'DerivativesBoundaries = "', i, '" is unknown to octopus'
      call write_fatal(1)
    end if

    der%zero_bc = (i == DER_BC_ZERO_F .and. sb%periodic_dim < 3)
    der%periodic_bc = (sb%periodic_dim > 0)
    der%boundaries(:) = i
    der%boundaries(1:sb%periodic_dim) = DER_BC_PERIOD

    ! find out how many ghost points we need in which dimension
    der%n_ghost(:) = 0
    do i = 1, der%dim
      if(der%boundaries(i) == DER_BC_ZERO_F .or. der%boundaries(i) == DER_BC_PERIOD) then
        der%n_ghost(i) = maxval(abs(der%lapl%stencil(i,:)))
      end if
    end do

    call pop_sub()
  end subroutine derivatives_init


  ! ---------------------------------------------------------
  subroutine derivatives_end(der)
    type(derivatives_t), intent(inout) :: der

    integer :: i

    call push_sub('derivatives.derivatives_end')

    ASSERT(associated(der%op))

    do i = 1, der%dim+1
      call nl_operator_end(der%op(i))
    end do

    deallocate(der%op)
    nullify   (der%op, der%lapl, der%grad)

    call pop_sub()
  end subroutine derivatives_end


  ! ---------------------------------------------------------
  ! Returns maximum extension of the stencil in spatial direction
  ! dir = 1, 2, 3 for a given derivative der.
  integer function stencil_extent(der, dir)
    type(derivatives_t), intent(in) :: der
    integer,           intent(in) :: dir

    call push_sub('derivatives.stencil_extent')

    select case(der%stencil_type)
      case(DER_STAR)
        stencil_extent = stencil_star_extent(dir, der%order)
      case(DER_VARIATIONAL)
        stencil_extent = stencil_variational_extent(dir, der%order)
      case(DER_CUBE)
        stencil_extent = stencil_cube_extent(dir, der%order)
      case(DER_STARPLUS)
        stencil_extent = stencil_cube_extent(dir, der%order)
      end select
      
    call pop_sub()
  end function stencil_extent


  ! ---------------------------------------------------------
  subroutine derivatives_get_stencil_lapl(der)
    type(derivatives_t), intent(inout) :: der

    integer :: n

    call push_sub('derivatives.derivatives_get_stencil_lapl')

    ASSERT(associated(der%lapl))

    ! get size of stencil
    select case(der%stencil_type)
    case(DER_STAR,DER_VARIATIONAL)
      n = stencil_star_size_lapl(der%dim, der%order)
    case(DER_CUBE)
      n = stencil_cube_size_lapl(der%dim, der%order)
    case(DER_STARPLUS)
      n = stencil_starplus_size_lapl(der%dim, der%order)
    end select

    ! initialize nl operator
    call nl_operator_init(der%lapl, n, "Laplacian")

    ! create stencil
    select case(der%stencil_type)
    case(DER_STAR, DER_VARIATIONAL)
      call stencil_star_get_lapl(der%dim, der%order, der%lapl%stencil)
    case(DER_CUBE)
      call stencil_cube_get_lapl(der%dim, der%order, der%lapl%stencil)
    case(DER_STARPLUS)
      call stencil_starplus_get_lapl(der%dim, der%order, der%lapl%stencil)
    end select

    call pop_sub()

  end subroutine derivatives_get_stencil_lapl


  ! ---------------------------------------------------------
  ! Returns the diagonal elements of the laplacian needed for preconditioning
  subroutine derivatives_lapl_diag(der, lapl)
    type(derivatives_t), intent(in)  :: der
    FLOAT,             intent(out) :: lapl(:)  ! lapl(m%np)

    call push_sub('derivatives_inc.derivatives_lapl_diag')

    ASSERT(ubound(lapl, DIM=1) >= der%m%np)

    ! the laplacian is a real operator
    call dnl_operator_operate_diag (der%lapl, lapl)

    call pop_sub()

  end subroutine derivatives_lapl_diag


  ! ---------------------------------------------------------
  subroutine derivatives_get_stencil_grad(der)
    type(derivatives_t), intent(inout) :: der

    integer :: i, n
    character :: dirchar

    call push_sub('derivatives.derivatives_get_stencil_grad')

    ASSERT(associated(der%grad))

    ! get size of stencil
    select case(der%stencil_type)
    case(DER_STAR, DER_VARIATIONAL)
      n = stencil_star_size_grad(der%order)
    case(DER_CUBE)
      n = stencil_cube_size_grad(der%dim, der%order)
    case(DER_STARPLUS)
      n = stencil_starplus_size_grad(der%dim, der%order)
    end select

    ! initialize nl operator
    do i = 1, der%dim
      if(i == 1) dirchar = 'X'
      if(i == 2) dirchar = 'Y'
      if(i == 3) dirchar = 'Z'
      call nl_operator_init(der%grad(i), n, "Gradient "//dirchar)

      ! create stencil
      select case(der%stencil_type)
      case(DER_STAR, DER_VARIATIONAL)
        call stencil_star_get_grad(i, der%order, der%grad(i)%stencil)
      case(DER_CUBE)
        call stencil_cube_get_grad(der%dim, der%order, der%grad(i)%stencil)
      case(DER_STARPLUS)
        call stencil_starplus_get_grad(der%dim, i, der%order, der%grad(i)%stencil)
      end select
    end do

    call pop_sub()

  end subroutine derivatives_get_stencil_grad


  ! ---------------------------------------------------------
  subroutine derivatives_build(der, m)
    type(derivatives_t),    intent(inout) :: der
    type(mesh_t),   target, intent(in)    :: m

    integer, allocatable :: polynomials(:,:)
    FLOAT,   allocatable :: rhs(:,:)
    integer :: i
    logical :: const_w_, cmplx_op_

    type(nl_operator_t) :: auxop

    call push_sub('derivatives.derivatives_build')

    ASSERT(associated(der%op))
    ASSERT(der%stencil_type>=DER_STAR .and. der%stencil_type<=DER_STARPLUS)
    ASSERT(.not.(der%stencil_type==DER_VARIATIONAL.and.m%use_curvlinear))

    der%m => m    ! make a pointer to the underlying mesh

    const_w_  = .true.
    cmplx_op_ = .false.

    ! need non-constant weights for curvlinear and scattering meshes
    if(m%use_curvlinear) const_w_ = .false.

    ! build operators
    do i = 1, der%dim+1
      call nl_operator_build(m, der%op(i), der%m%np, const_w = const_w_, cmplx_op = cmplx_op_)
    end do

    select case(der%stencil_type)

    case(DER_STAR) ! laplacian and gradient have different stencils
      do i = 1, der%dim + 1
        ALLOCATE(polynomials(der%dim, der%op(i)%n), der%dim*der%op(i)%n)
        ALLOCATE(rhs(der%op(i)%n, 1), der%op(i)%n*1)

        if(i <= der%dim) then  ! gradient
          call stencil_star_polynomials_grad(i, der%order, polynomials)
          call get_rhs_grad(i, rhs(:,1))
        else                      ! laplacian
          call stencil_star_polynomials_lapl(der%dim, der%order, polynomials)
          call get_rhs_lapl(rhs(:,1))
        end if

        call make_discretization(der%dim, der%m, polynomials, rhs, 1, der%op(i:i))
        deallocate(polynomials, rhs)
      end do

    case(DER_CUBE) ! laplacian and gradient have similar stencils
      ALLOCATE(polynomials(der%dim, der%op(1)%n), der%dim*der%op(1)%n)
      ALLOCATE(rhs(der%op(1)%n, der%dim+1), der%op(1)%n*(der%dim+1))
      call stencil_cube_polynomials_lapl(der%dim, der%order, polynomials)

      do i = 1, der%dim
        call get_rhs_grad(i, rhs(:,i))
      end do
      call get_rhs_lapl(rhs(:, der%dim+1))

      call make_discretization(der%dim, der%m, polynomials, rhs, der%dim+1, der%op(:))

      deallocate(polynomials, rhs)

    case(DER_STARPLUS)
      do i = 1, der%dim
        ALLOCATE(polynomials(der%dim, der%op(i)%n), der%dim*der%op(i)%n)
        ALLOCATE(rhs(der%op(i)%n, 1), der%op(i)%n*1)
        call stencil_starplus_pol_grad(der%dim, i, der%order, polynomials)
        call get_rhs_grad(i, rhs(:, 1))
        call make_discretization(der%dim, der%m, polynomials, rhs, 1, der%op(i:i))
        deallocate(polynomials, rhs)
      end do
      ALLOCATE(polynomials(der%dim, der%op(der%dim+1)%n), der%dim*der%op(der%dim+1)%n)
      ALLOCATE(rhs(der%op(i)%n, 1), der%op(i)%n*1)
      call stencil_starplus_pol_lapl(der%dim, der%order, polynomials)
      call get_rhs_lapl(rhs(:, 1))
      call make_discretization(der%dim, der%m, polynomials, rhs, 1, der%op(der%dim+1:der%dim+1))
      deallocate(polynomials, rhs)

    case(DER_VARIATIONAL)
      ! we have the explicit coefficients
      call stencil_variational_coeff_lapl(der%dim, der%order, m%h, der%lapl, alpha = der%lapl_cutoff)

    end select

    ! Here the Laplacian is forced to be self-adjoint, and the gradient to be skew-selfadjoint
    if(m%use_curvlinear) then
      do i = 1, der%dim
        call nl_operator_init(auxop, der%grad(i)%n, "auxop")
        auxop%stencil = der%grad(i)%stencil
        call nl_operator_build(m, auxop, der%m%np, const_w = const_w_, cmplx_op = cmplx_op_)
        call nl_operator_skewadjoint(der%grad(i), auxop, der%m)
        call nl_operator_equal(der%grad(i), auxop)
        call nl_operator_end(auxop)
      end do
      call nl_operator_init(auxop, der%lapl%n, "auxop")
      auxop%stencil = der%lapl%stencil
      call nl_operator_build(m, auxop, der%m%np, const_w = const_w_, cmplx_op = cmplx_op_)
      call nl_operator_selfadjoint(der%lapl, auxop, der%m)
      call nl_operator_equal(der%lapl, auxop)
      call nl_operator_end(auxop)
    end if

    call pop_sub()

  contains

    ! ---------------------------------------------------------
    subroutine get_rhs_lapl(rhs)
      FLOAT, intent(out) :: rhs(:)

      integer :: i, j, k
      logical :: this_one

      ! find right hand side for operator
      rhs(:) = M_ZERO
      do i = 1, der%dim
        do j = 1, der%lapl%n
          this_one = .true.
          do k = 1, der%dim
            if(k == i .and. polynomials(k, j).ne.2) this_one = .false.
            if(k.ne.i .and. polynomials(k, j).ne.0) this_one = .false.
          end do
          if(this_one) rhs(j) = M_TWO
        end do
      end do

    end subroutine get_rhs_lapl

    ! ---------------------------------------------------------
    subroutine get_rhs_grad(dir, rhs)
      integer, intent(in)  :: dir
      FLOAT,   intent(out) :: rhs(:)

      integer :: j, k
      logical :: this_one

      ! find right hand side for operator
      rhs(:) = M_ZERO
      do j = 1, der%grad(dir)%n
        this_one = .true.
        do k = 1, der%dim
          if(k == dir .and. polynomials(k, j).ne.1) this_one = .false.
          if(k.ne.dir .and. polynomials(k, j).ne.0) this_one = .false.
        end do
        if(this_one) rhs(j) = M_ONE
      end do

    end subroutine get_rhs_grad

  end subroutine derivatives_build


  ! ---------------------------------------------------------
  subroutine make_discretization(dim, m, pol, rhs, n, op)
    integer,                intent(in)    :: dim
    type(mesh_t),           intent(in)    :: m
    integer,                intent(in)    :: pol(:,:)  ! pol(dim, op%n)
    integer,                intent(in)    :: n
    FLOAT,                  intent(inout) :: rhs(:,:)   ! rhs_(op%n, n)
    type(nl_operator_t),    intent(inout) :: op(:)

    integer :: p, p_max, i, j, k, pow_max
    FLOAT   :: x(MAX_DIM)
    FLOAT, allocatable :: mat(:,:), sol(:,:), powers(:,:)

    call push_sub('derivatives.make_discretization')

    ALLOCATE(mat(op(1)%n, op(1)%n), op(1)%n*op(1)%n)
    ALLOCATE(sol(op(1)%n, n), op(1)%n*n)

    message(1) = 'Info: Generating weights for finite-difference discretization.'
    call write_info(1)

    ! use to generate power lookup table
    pow_max = maxval(pol)
    ALLOCATE(powers(dim, 0:pow_max), dim*(pow_max+1))
    powers(:,:) = M_ZERO
    powers(:,0) = M_ONE

    p_max = op(1)%np
    if(op(1)%const_w) p_max = 1

    do p = 1, p_max
      mat(1,:) = M_ONE
      do i = 1, op(1)%n
        x(1:dim) = m%x(p + op(1)%ri(i, op(1)%rimap(p)), 1:dim) - m%x(p, 1:dim)

        ! calculate powers
        do j = 1, dim
          powers(j,1) = x(j)
          do k = 2, pow_max
            powers(j,k) = x(j)*powers(j,k-1)
          end do
        end do

        ! generate the matrix
        do j = 2, op(1)%n
          mat(j, i) = powers(1, pol(1, j))
          do k = 2, dim
            mat(j, i) = mat(j, i)*powers(k, pol(k, j))
          end do
        end do
      end do

      call lalg_linsyssolve(op(1)%n, n, mat, rhs, sol)
      do i = 1, n
        op(i)%w_re(:, p) = sol(:, n)
      end do

    end do

    deallocate(mat, sol, powers)

    call pop_sub()
  end subroutine make_discretization

  subroutine der_handle_init(this, der)
    type(der_handle_t),  intent(out) :: this
    type(derivatives_t), intent(in)  :: der

    nullify(this%df, this%zf)
    nullify(this%dlapl, this%zlapl)
#ifdef HAVE_MPI
    this%parallel_in_domains = der%m%parallel_in_domains
    if(this%parallel_in_domains) call pv_handle_init(this%pv_h, der%m%vp, der%comm_method)
#endif
  end subroutine der_handle_init

  subroutine der_handle_end(this)
    type(der_handle_t), intent(inout) :: this

    nullify(this%df, this%zf)
    nullify(this%dlapl, this%zlapl)
#ifdef HAVE_MPI
    if(this%parallel_in_domains) call pv_handle_end(this%pv_h)
#endif
  end subroutine der_handle_end

#ifdef HAVE_MPI    
  logical function derivatives_overlap(this) result(overlap)
    type(derivatives_t),    intent(in) :: this

    overlap = this%comm_method /= BLOCKING  
  end function derivatives_overlap
#endif
  
#include "undef.F90"
#include "real.F90"
#include "derivatives_inc.F90"

#include "undef.F90"
#include "complex.F90"
#include "derivatives_inc.F90"

end module derivatives_m

!! Local Variables:
!! mode: f90
!! coding: utf-8
!! End:
